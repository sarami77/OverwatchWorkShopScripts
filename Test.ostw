import "Test.setting.json";
import "Lib/Utility.del";
import "Lib/Debug.del";
import "Lib/DamageText.del" ;

// Builtin -----------------------------------------------
globalvar define playerTeam;
globalvar define enemyTeam;

globalvar define initCompleted!;

rule: "BuiltinGlobal"
Event.OngoingGlobal
{
    Wait(3, WaitBehavior.AbortWhenFalse);


    while(true){
        if(TeamOf(HostPlayer()) == Team.Team1){
            playerTeam = Team.Team1;
            enemyTeam = Team.Team2;
        }
        else if(TeamOf(HostPlayer()) == Team.Team2){
            playerTeam = Team.Team2;
            enemyTeam = Team.Team1;
        }

        damageTextEnableTeam = enemyTeam;

        SetRespawnMaxTime(AllPlayers(), 0);
        SetMaxHealth(AllPlayers(), MaxValue());
        SetStatus(HostPlayer(), null, Status.Invincible, MaxValue());

        initCompleted = true;
        Wait(1, WaitBehavior.AbortWhenFalse);
    }
}

// Test Code -----------------------------------------------
rule: "debug_global"
Event.OngoingGlobal
{
}

rule: "debug_player"
Event.OngoingPlayer
if(
    HasSpawned(ep)
){
}

// TAG Perk ----------
import "Lib/Perk/PerkCore.del";
import "Lib/Perk/DeathFromAbove.del";

rule: "Debug/getPerk"
Event.OngoingPlayer
Team.Team1
if(
    IsButtonHeld(ep, Button.Interact) &&
    perkState == PerkState.Non &&
    perkType == PerkType.Non
){
    WaitLongPressConfirm();
    perkState = PerkState.Ready;
    perkType = PerkType.DeathFromAbove;
}

// TAG Buddy ----------
import "Lib/Buddy.del";

rule: "Buddy/BuddyContorl"
Event.OngoingGlobal
if(
    initCompleted
){
    define buddyList! = 
    WorkshopSettingToggle("Follower AI Settings(Join on behalf of absent player)", "Random Hero", true) ?
        // random list
        [
            RandomSupportHero,
            RandomTankHero,
            RandomDamageHero,
            RandomSupportHero,
            RandomTankHero,
            RandomDamageHero,
            RandomSupportHero,
            RandomDamageHero
        ]
    :
        // fix list
        [
            Hero.Mercy,
            Hero.Reinhardt,
            Hero.Genji,
            Hero.Moira,
            Hero.Sigma,
            Hero.Hanzo,
            Hero.Ana,
            Hero.Doomfist
        ];

    for(define buddyAddIndex! = 0; buddyAddIndex < CountOf(buddyList); buddyAddIndex++){
        CreateBotsByHeroInput(buddyList[buddyAddIndex], 1, playerTeam);
        CreateBotsByHeroInput(buddyList[buddyAddIndex], 1, enemyTeam);
    }
}

// Debug/Slow -----------------------------------------------
globalvar define slow = 100;

rule: "Debug/Slow/setSlow"
Event.OngoingPlayer
Team.Team1
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Jump)
){
    // 100 -> 50 -> 25 -> 10
    if(slow == 100)
        slow = 50;
    else if(slow == 50)
        slow = 25;
    else if(slow == 25)
        slow = 10;
    else if(slow == 10)
        slow = 100;
    else
        slow = 100;
    SetSlowMotion(slow);
    dbgMsg(<"setSlow <0>", slow>);
}

// Debug/TuneVal -----------------------------------------------

define changeTimeInterval: 0.1;
define initialTuneUnit : 0.01;
globalvar define tuneVal = 0.1;
globalvar define consecutiveChangeCount = 0;
globalvar define tuneUnit = initialTuneUnit;

rule: "showParam"
Event.OngoingGlobal
{
    CreateHudText(HostPlayer(),
        null,
        <"<0> <1>", "tune Val", tuneVal>,
        null,
        Location.Left, -1,
        Color.White, Color.Orange, Color.White,
        HudTextRev.VisibleToAndString, Spectators.VisibleNever);

    CreateHudText(HostPlayer(),
        null,
        <"<0> <1>", "Server Load", ServerLoad()>,
        null,
        Location.Left, -1,
        Color.White, Color.Orange, Color.White,
        HudTextRev.VisibleToAndString, Spectators.VisibleNever);   
}

rule: "Debug/TuneVal/startIncreaseParam"
Event.OngoingPlayer
if(
    HostPlayer() &&
    (
        IsButtonHeld(ep, Button.Interact) &&
        IsButtonHeld(ep, Button.Ability2)
    )
){
    tuneVal += tuneUnit;
    Wait(changeTimeInterval, WaitBehavior.AbortWhenFalse);
    changeTuneUnit();
    LoopIfConditionIsTrue();
}

rule: "Debug/TuneVal/stopIncreaseParam"
Event.OngoingPlayer
if(
    HostPlayer() &&
    !(
        IsButtonHeld(ep, Button.Interact) &&
        IsButtonHeld(ep, Button.Ability2)
    )
){
    resetTuneUnit();
}

rule: "Debug/TuneVal/startDecreaseParam"
Event.OngoingPlayer
if(
    HostPlayer() &&
    (
        IsButtonHeld(ep, Button.Interact) &&
        IsButtonHeld(ep, Button.Ability1)
    )
){
    tuneVal -= tuneUnit;
    Wait(changeTimeInterval, WaitBehavior.AbortWhenFalse);
    changeTuneUnit();
    LoopIfConditionIsTrue();
}

rule: "Debug/TuneVal/stopDecreaseParam"
Event.OngoingPlayer
if(
    HostPlayer() &&
    !(
        IsButtonHeld(ep, Button.Interact) &&
        IsButtonHeld(ep, Button.Ability1)
    )
){
    resetTuneUnit();
}

void resetTuneUnit(){
    tuneUnit = initialTuneUnit;
    consecutiveChangeCount= 0;
}

void changeTuneUnit(){
    consecutiveChangeCount ++;
    if(consecutiveChangeCount < 10)
        tuneUnit =  initialTuneUnit * 1;
    else if(consecutiveChangeCount < 20 )
        tuneUnit = initialTuneUnit * 10;
    else if(consecutiveChangeCount < 30 )
        tuneUnit = initialTuneUnit * 100;
    else
        tuneUnit = initialTuneUnit * 1000;
}