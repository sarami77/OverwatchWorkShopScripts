import "EnemyRush.setting.json";
import "Lib/Debug.del";
import "Lib/Utility.del";
import "Lib/Bot.del";
// import "Lib/Regeneration.del";
// import "Lib/Revive.del";
// import "Lib/DamageText.del";

// TAG Signature and infomation ----------
disabled rule: "ENEMY RUSH -HORDE PVE [ver.2.0] Share code [V7PEF]"-1{}
disabled rule: "You allowed copy or modify code."-1{}
disabled rule: "This script was generated by [Overwatch-Script-To-Workshop] tools"-1{}
disabled rule: "Check detail with https://github.com/ItsDeltin/Overwatch-Script-To-Workshop"-1{}
disabled rule: "Original source code was created by [sarami77#1868]"-1{}
disabled rule: "Check detail with https://github.com/sarami77/OverwatchWorkShopScripts"-1{}

// TAG Initialize ----------
define playerTeam: Team.Team2;
define enemyTeam: Team.Team1;

globalvar define initCompleted!;

rule: "Initialize/initialize"
Event.OngoingGlobal
{
    // Stop records to reduce server load
    DisableInspectorRecording();

    // If run it right after loading the server, some scripts fail, so wait 3 seconds
    Wait(3, WaitBehavior.AbortWhenFalse);

    // Cancel unnecessary default settings

    // Set imported functions public property for user tuning
    // // Regeneration.del
    // regenerationEnableTeam = Team.Team2;
    // regenerationNeedBalanceTeam2 = true;
    // regenerationStartWaitSecondTeam2 =      WorkshopSettingReal("Player Settings", "Regeneration Start Wait Second",            3, 0, 100, 2);
    // regenerationHealthPerSecondTeam2 =      WorkshopSettingReal("Player Settings", "Regeneration Health Per Second",            25, 0, 1000, 3);
    // // Revive.del
    // reviveEnableTeam = Team.Team2;
    // reviveTimeLimit =                       WorkshopSettingReal("Player Settings", "Revive Time Limit",                         10, 0, 100, 4);
    // reviveRange =                           WorkshopSettingReal("Player Settings", "Revive Range",                              3, 0, 100, 5);
    // reviveCompleteSecond =                  WorkshopSettingReal("Player Settings", "Revive Complete Second",                    5, 0, 100, 6);
    // DamageText.del
    // damageTextEnableTeam = Team.Team1;

    // Start watch over load
    slowControlHandler = SlowControlHandler.ServerOverLoadMeasures;

    // show version and share code
    CreateHudText(AllPlayers(),
        null, "ENEMY RUSH -HORDE PVE [ver.2.0] Share code [V7PEF]", null,
        Location.Left, -3,
        Color.White, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

    // show HUD of enemy team assembly
    CreateHudText(AllPlayers(),
        null,
        null,
        <"Enemy Team Assembly \"<0>\"", enemyTeamName>,
        Location.Right, -1,
        Color.White, Color.White, Color.Purple,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

    // Notify completion of init game setting and start game main logic
    initCompleted = true;
}

define phaseTime: 10;

rule: "Initialize/shortenAssemblingHeroes"
Event.OngoingGlobal
if(
    IsAssemblingHeroes()
){
    SetMatchTime(phaseTime);
}

rule: "Initialize/shortenSetup"
Event.OngoingGlobal
if(
    IsInSetup()
){
    SetMatchTime(phaseTime);
}

// TAG ServerOverLoadMeasures ----------
define WaitUntilServerFree(){ WaitUntil(!isServerOverLoading, 5); }

define slowLevel: [100, 90, 80, 70, 50];    // 0～4
globalvar define targetSlowLevel;
globalvar define currentSlowLevel;
globalvar define slowControlHandler;
enum SlowControlHandler{
    None,
    ServerOverLoadMeasures,
    PlayerJoin,
    GameEnd
}

rule: "ServerOverLoadMeasures/increaseSlowLevel"
Event.OngoingGlobal
if(
    initCompleted &&    
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    isServerOverLoading &&
    targetSlowLevel < (CountOf(slowLevel) - 1)
){
    // If the server is overloaded,
    //  apply slow motion every second
    //  to reduce the load to avoid shutting down due to server overload.
    Wait(1, WaitBehavior.AbortWhenFalse);
    // can increase slow level
    if(targetSlowLevel < (CountOf(slowLevel) - 1)){
        // increase slow level
        targetSlowLevel ++;
        // apply slow level
        ApplySlowLevel(targetSlowLevel);
    }
    LoopIfConditionIsTrue();
}

rule: "ServerOverLoadMeasures/decreaseSlowLevel"
Event.OngoingGlobal
if(
    initCompleted &&
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    !isServerOverLoading &&
    targetSlowLevel > 0
){
    // If the server is not overloaded,
    //  reset slow motion every second
    // can decrease slow level
    Wait(0.75, WaitBehavior.AbortWhenFalse);
    if(targetSlowLevel > 0){
        // decrease slow level
        targetSlowLevel --;
        // apply slow level
        ApplySlowLevel(targetSlowLevel);
    }
    LoopIfConditionIsTrue();
}

void ApplySlowLevel(define setSlowLevel)"ApplySlowLevel"{
    // if slow level changed
    if(currentSlowLevel != setSlowLevel){
        // apply slow motion
        currentSlowLevel = setSlowLevel;
        SetSlowMotion(slowLevel[setSlowLevel]);
    }
}

// TAG Payload ----------
globalvar define prevPayloadPosition!;

globalvar define payloadDirection!;

rule: "Payload/updatePayloadDirection"
Event.OngoingGlobal
if(
    PayloadProgressPercentage() > 0
){
    prevPayloadPosition = PayloadPosition();

    while(true){
        define nowPayloadPosition = PayloadPosition();
        if(
            DistanceBetween(nowPayloadPosition, prevPayloadPosition) > 0.01
        ){
            payloadDirection = DirectionTowards(prevPayloadPosition, nowPayloadPosition);
            prevPayloadPosition = nowPayloadPosition;
        }
        Wait(1);
    }
    LoopIfConditionIsTrue();
}

// TAG Player ----------
// TAG Player/FisrtFast
rule: "Player/FisrtFast/setFastSpeed"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    IsAlive() &&
    IsInSpawnRoom()
){
    SetMoveSpeed(ep, 150);
    while(!IsInLineOfSight(ep, ClosestPlayerTo(ep, enemyTeam))){
        Wait(1, WaitBehavior.IgnoreCondition);
    }
    SetMoveSpeed(ep, 100);
}

// TAG EnemBot ----------
define assetList:
    [
        [
            "basic",
            [EnemyType.Boss,        RandomHero,         1],
            [EnemyType.Elite,       RandomHero,         5],
            [EnemyType.Minion,      RandomHero,         10]
        ],
        [
            "boss and minions",
            [EnemyType.Boss,        RandomHero,         1],
            [EnemyType.Minion,      RandomHero,         15]
        ],
        [
            "10 elites",
            [EnemyType.Elite,       RandomHero,         3],
            [EnemyType.Elite,       RandomHero,         3],
            [EnemyType.Elite,       RandomHero,         4]
        ],
        [
            "double bosses",
            [EnemyType.Boss,        RandomHero,         1],
            [EnemyType.Boss,        RandomHero,         1],
            [EnemyType.Minion,      RandomHero,         8]
        ],

        [
            "clones",
            [EnemyType.Boss,        someHero, 		    1],
            [EnemyType.Elite,       someHero, 		    5],
            [EnemyType.Minion,      someHero, 		    10]
        ],
        [
            "double boss clones",
            [EnemyType.Boss,        someHero, 		    2],
            [EnemyType.Elite,       someHero, 		    8]
        ],
        [
            "10 elites clones",
            [EnemyType.Elite,       someHero, 		    10]
        ],

        [
            "giant boss",
            [EnemyType.GiantBoss,   RandomTankHero,     1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],
        [
            "giant boss",
            [EnemyType.GiantBoss,   RandomDamageHero,   1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],		
        [
            "giant boss",
            [EnemyType.GiantBoss,   RandomSupportHero,  1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],

        [
            "giant boss",
            [EnemyType.GiantBoss,   Hero.Pharah,        1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],
        [
            "giant boss",
            [EnemyType.GiantBoss,   Hero.Reinhardt,     1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],
        [
            "giant boss",
            [EnemyType.GiantBoss,   Hero.Sigma,         1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],
        [
            "giant boss",
            [EnemyType.GiantBoss,   Hero.Doomfist,      1],
            [EnemyType.Minion,      RandomSupportHero,  10]
        ],

        [
            "close range",
            [EnemyType.Boss,        Hero.Reaper,        1],
            [EnemyType.Elite,       Hero.Doomfist,      5],
            [EnemyType.Minion,      Hero.Mei,           10]
        ],
        [
            "close range",
            [EnemyType.Boss,        Hero.Doomfist,      1],
            [EnemyType.Elite,       Hero.Reaper,        5],
            [EnemyType.Minion,      Hero.Mei,           10]
        ],
        [
            "melees",
            [EnemyType.Boss,        Hero.Reinhardt,     1],
            [EnemyType.Elite,       Hero.Doomfist,      5],
            [EnemyType.Minion,      Hero.Brigitte,      10]
        ],
        [
            "melees",
            [EnemyType.Boss,        Hero.Brigitte,      1],
            [EnemyType.Elite,       Hero.Doomfist,      5],
            [EnemyType.Minion,      Hero.Reinhardt,     10]
        ],
        [
            "flankers",
            [EnemyType.Boss,        Hero.Genji,         1],
            [EnemyType.Elite,       Hero.Soldier76,     5],
            [EnemyType.Minion,      Hero.Tracer,        10]
        ],
        [
            "flankers",
            [EnemyType.Boss,        Hero.Tracer,        1],
            [EnemyType.Elite,       Hero.Genji,         5],
            [EnemyType.Minion,      Hero.Soldier76,     10]
        ],
        [
            "long range",
            [EnemyType.Boss,        Hero.Orisa, 		1],
            [EnemyType.Elite,       Hero.Bastion, 		5],
            [EnemyType.Minion,      Hero.Ana, 	        10]
        ],
        [
            "long range",
            [EnemyType.Boss,        Hero.Bastion, 		1],
            [EnemyType.Elite,       Hero.Orisa, 		5],
            [EnemyType.Minion,      Hero.Ana, 	        10]
        ],
        [
            "air forces elite",
            [EnemyType.Elite,       Hero.Echo, 			5],
            [EnemyType.Elite,       Hero.Pharah, 		5]
        ],
        [
            "sky queen",
            [EnemyType.Boss,        Hero.Pharah, 		1],
            [EnemyType.Elite,       Hero.Mercy, 		5],
            [EnemyType.Minion,      Hero.Echo,   		10]
        ],
        [
            "sky queen",
            [EnemyType.Boss,        Hero.Echo, 		    1],
            [EnemyType.Elite,       Hero.Mercy, 		5],
            [EnemyType.Minion,      Hero.Pharah,   		10]
        ],
        [
            "snipers elite",
            [EnemyType.Elite,        Hero.Hanzo,		5],
            [EnemyType.Elite,        Hero.Widowmaker,	5]
        ],
        [
            "snipers elite",
            [EnemyType.Elite,        Hero.Ashe,		    5],
            [EnemyType.Elite,        Hero.Widowmaker,	5]
        ],
        [
            "snipers elite",
            [EnemyType.Elite,        Hero.Hanzo,		5],
            [EnemyType.Elite,        Hero.Ana,	        5]
        ],
        [
            "machines",
            [EnemyType.Boss,        Hero.Orisa, 		1],
            [EnemyType.Elite,       Hero.Bastion, 		5],
            [EnemyType.Minion,      Hero.Zenyatta,      10]
        ],
        [
            "machines",
            [EnemyType.Boss,        Hero.Zenyatta, 		1],
            [EnemyType.Elite,       Hero.Orisa, 		5],
            [EnemyType.Minion,      Hero.Bastion,       10]
        ],

        [
            "Reinhardt army",
            [EnemyType.Boss,        Hero.Reinhardt, 	1],
            [EnemyType.Minion,      Hero.Reinhardt, 	15]
        ],
        [
            "Brigitte army",
            [EnemyType.Boss,        Hero.Reinhardt, 	1],
            [EnemyType.Minion,      Hero.Brigitte,   	15]
        ],
        [
            "turrets",
            [EnemyType.Boss,        Hero.Torbjorn, 		1],
            [EnemyType.Elite,       Hero.Symmetra, 		5],
            [EnemyType.Minion,      Hero.Symmetra, 		10]
        ],
        [
            "turrets",
            [EnemyType.Boss,        Hero.Symmetra, 		1],
            [EnemyType.Elite,       Hero.Torbjorn, 		5],
            [EnemyType.Minion,      Hero.Torbjorn, 		10]
        ]
    ];

globalvar define someHero!;

// TAG EnemyBot/BotAsset ----------

rule: "EnemyBot/reloadBotAsset"
Event.OngoingGlobal
if(
    initCompleted
){
    dbgMsg("reloadBotAsset");

    define list = AllLivingPlayers(enemyTeam);
    for(gLoopIndex = 0; gLoopIndex < CountOf(list); gLoopIndex ++){
        define bot = list[gLoopIndex];
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion, enemyColor, bot, bot.eachEffectTextSize);
    }
    someHero = RandomHero;
    // create all bot
    CreateEnemyBotsByAssetList(assetList, enemyTeam);
    Wait(RandomInteger(60, 120), WaitBehavior.AbortWhenFalse);
    if(MatchTime() == 0){
        Wait(RandomInteger(60, 120), WaitBehavior.AbortWhenFalse);
    }
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/EnemyType ----------
playervar define eachDealtDamageScale!;
playervar define eachMaxSpawnInterval!;
playervar define eachMinSpawnInterval!;
playervar define eachChargeUltInterval!;
playervar define eachEffectTextSize!;
playervar define eachNameText!;
playervar define eachNameColor!;

playervar define eachPerkDropProbability;   // dummy

define baseMaxSpawn: 18;
define baseMinSpawn: 6;

rule: "EnemyBot/EnemyType/setEnemyType"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None
){
    if(enemyType == EnemyType.Minion){
        StartScalingPlayer(ep, 0.75, false);
        StartModifyingHeroVoiceLines(ep, 1.5, false);

        SetMaxHealth(ep, 50);

        SetHealingDealt(ep, 50);
        SetHealingReceived(ep, 100);
        
        eachDealtDamageScale = 0.5;
        eachChargeUltInterval = 90;
        eachMaxSpawnInterval = baseMaxSpawn * 1;
        eachMinSpawnInterval = baseMinSpawn * 1;

        eachNameText = "Minion";
        eachNameColor = Color.White;
        eachEffectTextSize = 1;

        eachPerkDropProbability = 3;
    }
    else if(enemyType == EnemyType.Elite){
        StartScalingPlayer(ep, 1.0, false);
        StartModifyingHeroVoiceLines(ep, 1.0, false);

        SetMaxHealth(ep, 200);

        SetHealingDealt(ep, 200);
        SetHealingReceived(ep, 400);

        eachDealtDamageScale = 1.0;
        eachChargeUltInterval = 30;
        eachMaxSpawnInterval = baseMaxSpawn * 3;
        eachMinSpawnInterval = baseMinSpawn * 3;

        eachNameText = "Elite";
        eachNameColor = Color.Yellow;
        eachEffectTextSize = 2;

        eachPerkDropProbability = 12;
    }
    else if(enemyType == EnemyType.Boss){
        StartScalingPlayer(ep, 1.5, false);
        StartModifyingHeroVoiceLines(ep, 0.5, false);

        SetMaxHealth(ep, 1000);

        SetHealingDealt(ep, 200);
        SetHealingReceived(ep, 400);

        eachDealtDamageScale = 2.0;
        eachChargeUltInterval = 10;
        eachMaxSpawnInterval = baseMaxSpawn * 6;
        eachMinSpawnInterval = baseMinSpawn * 6;

        eachNameText = "Boss";
        eachNameColor = Color.Red;
        eachEffectTextSize = 3;

        eachPerkDropProbability = 50;
    }
    else if(enemyType == EnemyType.GiantBoss){
        StartScalingPlayer(ep, 2.5, false);
        StartModifyingHeroVoiceLines(ep, 0.1, false);

        SetMaxHealth(ep, 1000);

        SetHealingDealt(ep, 200);
        SetHealingReceived(ep, 400);

        eachDealtDamageScale = 4.0;
        eachChargeUltInterval = 10;
        eachMaxSpawnInterval = baseMaxSpawn * 6;
        eachMinSpawnInterval = baseMinSpawn * 6;

        eachNameText = "Giant Boss";
        eachNameColor = Color.Purple;
        eachEffectTextSize = 4;

        eachPerkDropProbability = 100;

        // set addtinal health
        AddHealthPoolToPlayer(ep, HealthType.Armor, 2500, true, false);
        AddHealthPoolToPlayer(ep, HealthType.Shields, 2500, true, false);
    }

    UpdateEachDealtDamage();
    UpdateEachSpawnInterval();

    // heal to max health
    while(Health() < MaxHealth()){
        Heal(ep, null, MaxValue());
        Wait(0.1, WaitBehavior.AbortWhenFalse);
    }
}

// TAG EnemyBot/Effect ----------
define enemyColor: Color.Purple;
playervar define botAppearanceEntitys!;

rule: "EnemyBot/Effect/showBotAppearance"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None &&
    IsAlive()
){
    botAppearanceEntitys = [];
    // show evil aura
    botAppearanceEntitys[0] = 
        CreateEffect(AllPlayers(), Effect.Cloud, enemyColor,
            ep, eachEffectTextSize, EffectRev.VisibleToPositionAndRadius);
    // show name text
    botAppearanceEntitys[1] = 
        CreateInWorldText(
            AllPlayers(), eachNameText, 
            ep, eachEffectTextSize, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString,
            eachNameColor, Spectators.DefaultVisibility);
}

rule: "EnemyBot/Effect/hideBotAppearanceAndReek"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None &&
    !IsAlive()
){
    // hide evil aura
    DestroyEffect(botAppearanceEntitys[0]);
    // hide name text
    DestroyInWorldText(botAppearanceEntitys[1]);
    // play dead effect
    PlayContinuousBlast(Color.Red, PositionOf(),
        eachEffectTextSize * 1.5, eachEffectTextSize);
}

// TAG EnemyBot/Teleport ----------
globalvar define prvPayloadPos;
Vector CurrentObjectPosition()"CurrentObjectPosition"{
    Vector objPos;
    define mode! = CurrentGameMode();
    define objNo = CurrentObjectNo();

    if(mode == GameMode.Assault){
        objPos = ObjectivePosition(objNo);
    }
    else if(mode == GameMode.Escort){
        objPos = PayloadPosition();
    }
    else if(mode == GameMode.Hybrid){
        objPos = PayloadPosition();
    }
    else{
        errLog("ERROR ON CurrentObjectPosition");
    }
    return objPos;
}

Number CurrentObjectNo()"CurrentObjectNo"{
    Number curObjNo;
    if(!IsObjectiveComplete(0)){
        curObjNo = 0;
    }
    else if(!IsObjectiveComplete(1)){
        curObjNo = 1;
    }
    else if(!IsObjectiveComplete(2)){
        curObjNo = 2;
    }
    else{
        errLog("ERROR ON CurrentObjectNo");
    }
    return curObjNo;
}

define teleportAreaSize: 10;

define ClosestPlayerToTelPos: ClosestPlayerTo(teleportPosition, playerTeam);
define ClosestPlayerToComTelPos: ClosestPlayerTo(commonTeleportPosition, playerTeam);
define ClosestPlayerToMe: ClosestPlayerTo(ep, playerTeam);

Player ClosestPlayerFromObject()"ClosestPlayerFromObject"{
    return FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(playerTeam),
                (
                    HasSpawned(ae) &&
                    IsAlive(ae) &&
                    !IsInSpawnRoom(ae)
                )
            ),
            DistanceBetween(ae, CurrentObjectPosition())
        )
    );
}

playervar define teleportPosition!;
globalvar define commonTeleportPosition!;

Boolean SetTeleportPositionToPlayerFront()"SetTeleportPositionToPlayerFront"{
    // dbgHudTxt("Set Teleport Position To Player Front");
    
    // set teleportToTarget at closest player from Object
    define teleportToTarget! = ClosestPlayerFromObject();
    if(teleportToTarget == null){
        return false;
    }
    for(pLoopIndex = 0; pLoopIndex < 10; pLoopIndex++){
        // select teleportPosition from middle of Object and teleportToTarget
        teleportPosition =
            NearestWalkablePosition(
                PositionOf(teleportToTarget)
                    + 
                (
                    DirectionTowards(teleportToTarget, CurrentObjectPosition())
                        *
                    DistanceBetween(teleportToTarget, CurrentObjectPosition()) * 0.5
                )
                    +
                RandomPosition(
                    (teleportAreaSize),
                    (teleportAreaSize),
                    (teleportAreaSize)
                )
            );

        define telPosNearTarget! = ClosestPlayerToTelPos;
        if(
            // Has line to target
            IsInLineOfSight(telPosNearTarget, teleportPosition, BarrierLOS.NoBarriersBlock)
        ){
            break;
        }
        Wait(0.1, WaitBehavior.AbortWhenFalse);
        WaitUntilServerFree();
    }
    return true;
}

Boolean SetTeleportPositionToObject()"SetTeleportPositionToObjectt"{
    // dbgHudTxt("Set Teleport Position To Objectt");

    // set teleportToTarget at closest player from Object
    teleportPosition =
        NearestWalkablePosition(
            CurrentObjectPosition() + 
            RandomPosition(
                (teleportAreaSize),
                (teleportAreaSize),
                (teleportAreaSize)
            )
        );
    return true;
}

globalvar define cutOffsetDistance;

define teleportAreaSizeOffset: 20;

Boolean SetTeleportPositionToObjectAround()"SetTeleportPositionToObjectAround"{
    // dbgHudTxt("Set Teleport Position To Object Around");

    // set teleportToTarget at Object
    define teleportToTarget! = CurrentObjectPosition() + Vector(0, 4, 0);

    if(payloadDirection != 0){
        define blockedHead! = BlockedPositon(teleportToTarget, payloadDirection, teleportAreaSizeOffset);
        cutOffsetDistance = teleportAreaSizeOffset - DistanceBetween(teleportToTarget, blockedHead);
        teleportToTarget = blockedHead;
    }

    for(pLoopIndex = 0; pLoopIndex < 10; pLoopIndex++){
        // select teleportPosition from teleportToTarget around
        teleportPosition = 
            NearestWalkablePosition(
                teleportToTarget +
                RandomPosition(
                    (teleportAreaSize + cutOffsetDistance),
                    (teleportAreaSize + cutOffsetDistance),
                    (teleportAreaSize + cutOffsetDistance)
                )
            );
        
        define telPosNearTarget! = ClosestPlayerToTelPos;
        if(
            // Has line to target
            IsInLineOfSight(telPosNearTarget, teleportPosition, BarrierLOS.NoBarriersBlock)
                &&
            // Has ground of teleport position
            IsBlockedBetween(teleportPosition, ShiftedPosition(teleportPosition, Down(), 6))
        ){
            break;
        }
        Wait(0.1, WaitBehavior.AbortWhenFalse);
        WaitUntilServerFree();
    }
    teleportPosition -= PositionOffset();
    return true;
}

define forceUpdateCommonPositionProb: 5;

Boolean SetTeleportPositionToCommonPosition()"SetTeleportPositionToCommonPosition"{
    // dbgHudTxt("Set Teleport Position To CommonPosition");

    // set telPosNearTarget at closest player from commonTeleportPosition
    define telPosNearTarget! = ClosestPlayerToComTelPos;
    if(telPosNearTarget == null){
        return false;
    }
    if(
        // invalid commonTeleportPosition
        commonTeleportPosition ==  0 ||
        // Probability
        ProbabilityTrue(forceUpdateCommonPositionProb) ||
        // has not line closest player from commonTeleportPosition
        !IsInLineOfSight(commonTeleportPosition, telPosNearTarget) ||                      
        // target inside teleport area
        DistanceBetween(commonTeleportPosition, telPosNearTarget) < teleportAreaSize 
    ){
        // new select common position
        // set teleport position to player around
        if(SetTeleportPositionToObjectAround()){
            // save teleport position as common position
            commonTeleportPosition = teleportPosition;
        }
        else{
            // set teleport position from common position
            teleportPosition = commonTeleportPosition;
        }
    }
    else{
        // set teleport position from common position
        teleportPosition = commonTeleportPosition;
    }
    return true;
}

void TeleportWithEffect()"TeleportWithEffect"{
    Wait(SlotOf(ep) * 0.1, WaitBehavior.AbortWhenFalse);
    // show pre teleport effect
    PlayTeleportEffect(true);
    // Teleport
    Teleport(ep, teleportPosition);
    // show teleport effect
    PlayTeleportEffect(false);
}

void PlayTeleportEffect(define isBefore!)"PlayTeleportEffect"{
    // if before teleport
    if(isBefore){
        // play teleport shadow of own
        PlayEffect(AllPlayers(), PlayEffect.BadPickupEffect, enemyColor, ep, 1);
        // play teleport shadow of dest
        PlayEffect(AllPlayers(), PlayEffect.BadPickupEffect, enemyColor, teleportPosition, 1);
        // wait for show teleport shadow of before
        Wait(0.4, WaitBehavior.IgnoreCondition);
    }
    else{
        // play teleport blast
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion, enemyColor, ep, eachEffectTextSize);
    }
}

define forceActiveTeleport: 50;
define teleportToCommonPositionProb: 50;

define IsPlayerOnObject: ([] != AllPlayersOnObjective(playerTeam));

void ExecTeleport()"ExecTeleport"{
    if(
        // during Object attacks
        IsPlayerOnObject ||
        // probability
        ProbabilityTrue(forceActiveTeleport)
    ){
        if(
            // probability
            ProbabilityTrue(teleportToCommonPositionProb)
        ){
            if(SetTeleportPositionToCommonPosition()){
                TeleportWithEffect();
            }
        }
        else{
            if(SetTeleportPositionToObjectAround()){
                TeleportWithEffect();
            }
        }
    }
    else{
        if(IsTeamOnDefense(TeamOf())){
            if(SetTeleportPositionToPlayerFront()){
                TeleportWithEffect();
            }
        }
        else{
            if(SetTeleportPositionToObject()){
                TeleportWithEffect();
            }
        }
    }
}

rule: "EnemyBot/Teleport/teleportByRespawn"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    IsAlive() &&
    IsInSpawnRoom()
){
    ExecTeleport();
    // failsafe
    Wait(1, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/Teleport/teleportByPlayerNotFound"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    IsAlive() &&
    !IsInSpawnRoom()
){
    for(define notHasLineSecond! = 0; notHasLineSecond < 3; notHasLineSecond ++){
        Wait(1, WaitBehavior.AbortWhenFalse);
        if(
            IsInLineOfSight(ep,
                ClosestPlayerFromObject(),
                BarrierLOS.NoBarriersBlock)
        ){
            LoopIfConditionIsTrue();
        }
    }

    ExecTeleport();

    LoopIfConditionIsTrue();
}

rule: "EnemyBot/Teleport/clearCommonTeleportPosition"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() == ep
){
    commonTeleportPosition = 0;
}

// TAG EnemyBot/EnemyAbility ----------
rule: "EnemyBot/EnemyAbility/combatTeleport"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    IsAlive() &&
    !IsInSpawnRoom() &&
    enemyType == EnemyType.GiantBoss &&
    actionState == ActionState.Attack
){
    define teleportWaitSecond! = NormalizedHealth() * 10;
    Wait(RandomReal(teleportWaitSecond, teleportWaitSecond + 3), WaitBehavior.AbortWhenFalse);
    // set teleport position to player around
    if(SetTeleportPositionToObjectAround()){
        define telBeam!;
        // teleport with beam effect
        DestroyEffect(telBeam);
        telBeam = CreateBeamEffect(AllPlayers(),
            BeamType.BadBeam, EyePosition(ep) + Down(), teleportPosition,
            Color.Purple, EffectRev.VisibleToPositionAndRadius);
        TeleportWithEffect();
        DestroyEffect(telBeam);
    }
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/EnemyAbility/rush"
Event.OngoingGlobal
if(
    initCompleted
){
    define rushInterval = (1 - (TotalObjectProgress() / 100)) * 90;
    if(MatchTime() == 0){
        rushInterval += 10;
    }
    Wait(RandomReal(10, 10 + rushInterval), WaitBehavior.AbortWhenFalse);
    dbgMsg("RUSH !");
    Respawn(AllDeadPlayers(enemyTeam));
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/chargeUltimate ----------
rule: "EnemyBot/chargeUltimate"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    HasSpawned() &&
    enemyType != EnemyType.None &&
    !IsUsingUltimate()
){
    SetUltimateCharge(ep, UltimateChargePercent() + (100 / eachChargeUltInterval));
    WaitUpdateMediumCycle();
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/SpawnInterval ----------
define showDeadEffectTime: 2;
playervar define confirmSpawnInterval!;

Number TotalObjectProgress()"TotalObjectProgress"{
    Number progress;

    if(!IsGameInProgress()){
        progress = 100;
    }
    else{
        define mode = CurrentGameMode();
        if(mode == GameMode.Assault){
            if(CurrentObjectNo() == 0){
                progress = (PointCapturePercentage() * 0.5);
            }
            else{
                progress = 50 + (PointCapturePercentage() * 0.5);
            }
        }
        else if(mode == GameMode.Escort){
            progress = PayloadProgressPercentage();
        }
        else if(mode == GameMode.Hybrid){
            if(CurrentObjectNo() == 0){
                progress = (PointCapturePercentage() * 0.3);
            }
            else{
                progress = 30 + (PayloadProgressPercentage() * 0.7);
            }
        }
        else{
            errLog("ERROR ON TotalObjectProgress");
        }
    }
    return progress;
}

void UpdateEachSpawnInterval()"UpdateEachSpawnInterval"{
    confirmSpawnInterval = (eachMaxSpawnInterval - eachMinSpawnInterval);
    confirmSpawnInterval *= (1 - (TotalObjectProgress() / 100));
    confirmSpawnInterval += eachMinSpawnInterval;
    
    define diff = GameProgressDiff();
    define capScale;
    if(40 <= diff){
        capScale = 0.25;
    }
    else if(20 <= diff && diff < 40){
        capScale = 0.5;
    }
    else if(0 <= diff && diff < 20){
        capScale = 0.75;
    }
    else if(-20 <= diff && diff < 0){
        capScale = 1.0;
    }
    else if(-40 <= diff && diff < -20){
        capScale = 1.25;
    }
    else if(-60 <= diff && diff < -40){
        capScale = 1.5;
    }
    else if(diff < -60){
        capScale = 2.0;
    }
    confirmSpawnInterval *= capScale;

    SetRespawnMaxTime(ep, confirmSpawnInterval + showDeadEffectTime);

    if(0 == SlotOf() && isDebugMode){
        dbgHudTxt(<"diff <0>, capScale <1>, cnf <2>, max <3>, min <4>, type <5>",
            diff, capScale, confirmSpawnInterval, eachMaxSpawnInterval, eachMinSpawnInterval, enemyType>);
    }
}

Number GameProgressDiff()"GameProgressDiff"{
    Number prgDiff! = 0;
    Number objPrg! = RoundToInteger(TotalObjectProgress());  // MAX 100
    Number timePrg  = RoundToInteger(MatchTime() / 3.0);  // MAX 300
    timePrg = 100 - timePrg;
    prgDiff = objPrg - timePrg;

    if(0 == SlotOf() && isDebugMode){
        dbgHudTxt(<"prgDiff <0>, objPrg <1>, timePrg <2>    ",
            prgDiff, objPrg, timePrg>);
    }

    return prgDiff;
}

rule: "EnemyBot/SpawnInterval/update"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None
){
    UpdateEachSpawnInterval();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/SpawnInterval/fastRespawnBySucide"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() == ep
){
    Wait(showDeadEffectTime, WaitBehavior.AbortWhenFalse);
    Respawn();
}

// TAG EnemyBot/ReceivedDamage ----------
globalvar define confirmReceiveDamage!;

void UpdateEachReceivedDamage()"UpdateEachReceivedDamage"{
    // set base damage
    confirmReceiveDamage = 100;
    // apply team balance
    confirmReceiveDamage *= ConvertToBalancedValue(1.5, playerTeam);
    SetDamageReceived(AllPlayers(enemyTeam), confirmReceiveDamage);
}

rule: "EnemyBot/ReceivedDamage/update"
Event.OngoingGlobal
if(
    initCompleted
){
    UpdateEachReceivedDamage();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/DealtDamage ----------
playervar define confirmDealtDamage!;
globalvar define threatLevel!;
globalvar define autoTune!;

define upTuneScale: 2.5;

rule: "EnemyBot/DealtDamage/upTune"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() != ep
){
    if(enemyType == EnemyType.Minion){
        autoTune += 0.01 * upTuneScale;
    }
    else if(enemyType == EnemyType.Elite){
        autoTune += 0.03 * upTuneScale;
    }
    else if(enemyType == EnemyType.Boss){
        autoTune += 0.06 * upTuneScale;
    }
    else if(enemyType == EnemyType.GiantBoss){
        autoTune += 0.06 * upTuneScale;
    }
}

rule: "EnemyBot/DealtDamage/downTune"
Event.OnDeath
if(
    TeamOf() == playerTeam &&
    Attacker() != ep
){
    autoTune -= 1.0;
}

void UpdateEachDealtDamage()"UpdateEachDealtDamage"{
    // set base damage
    confirmDealtDamage = WorkshopSettingReal("Enemy Bot Settings", "Base Deal Damage", 35, 0, 1000, 1);
    // apply team balance
    confirmDealtDamage /= ConvertToBalancedValue(1.5, playerTeam);
    // apply auto tune
    confirmDealtDamage += autoTune;
        // set threat Level
        threatLevel = confirmDealtDamage;
    // apply enemy type scale
    confirmDealtDamage *= eachDealtDamageScale;
    SetDamageDealt(ep, confirmDealtDamage);
}

rule: "EnemyBot/DealtDamage/update"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None
){
    UpdateEachDealtDamage();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

// TAG Debug ----------
disabled rule: "Debug/builtin"
Event.OngoingGlobal
if(
    initCompleted
){
    CreateEffect(HostPlayer(), Effect.Sphere, Color.Yellow,
        commonTeleportPosition, teleportAreaSize, EffectRev.PositionAndRadius);
}

globalvar define debugMode!;

enum DebugMode{
    Non,
    SlowLevelOnly,
    ShowParam,
    WithRecord
}

rule: "Debug/changeDebugMode"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Jump) 
){
    // change debug mode
    WaitLongPressConfirm();
    if(debugMode == DebugMode.Non){
        debugMode = DebugMode.SlowLevelOnly;
    }
    else if(debugMode == DebugMode.SlowLevelOnly){
        debugMode = DebugMode.ShowParam;
    }
    else if(debugMode == DebugMode.ShowParam){
        debugMode = DebugMode.WithRecord;
    }
    else if(debugMode == DebugMode.WithRecord){
        debugMode = DebugMode.Non;
    }

    if(debugMode == DebugMode.Non){
        DisableInspectorRecording();
        HideDebugHud();
    }
    else if(debugMode == DebugMode.SlowLevelOnly){
        DisableInspectorRecording();
        HideDebugHud();
        ShowDebugHud(HostPlayer());
    }
    else if(debugMode == DebugMode.ShowParam){
        DisableInspectorRecording();
        HideDebugHud();
        ShowDebugHud(HostPlayer());
    }
    else if(debugMode == DebugMode.WithRecord){
        EnableInspectorRecording();
        HideDebugHud();
        ShowDebugHud(HostPlayer());
    }
}

define End(): CountOf(debugHudEntitys);
define botOfEnemyType(define enemyType): FirstOf(FilteredArray(AllPlayers(enemyTeam), ae.enemyType == enemyType));
globalvar define debugHudEntitys!;

define isDebugMode: debugMode != DebugMode.Non;

// show debug icon and hud
void ShowDebugHud(define player!)"ShowDebugHud"{
    if(
        debugMode == DebugMode.ShowParam ||
        debugMode == DebugMode.WithRecord
    ){
        if(
            debugMode == DebugMode.WithRecord
        ){
            CreateHudText(player,
                null,
                null,
                "● is Recording",
                Location.Left, -1,
                Color.White, Color.White, Color.Red,
                HudTextRev.VisibleToAndString, Spectators.VisibleNever);
            debugHudEntitys[End()] = LastTextID();
        }

        // CreateHudText(player,
        //     null,
        //     <"<0> <1>", "Regeneration Rate", regenRateTeam1>,
        //     null,
        //     Location.Left, -1,
        //     Color.White, Color.Blue, Color.White,
        //     HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        // debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> RcvDmg <1> DltDmg <2> SpwItv <3> ChgUlt <4>", "Giant Boss Status",
                confirmReceiveDamage,
                botOfEnemyType(EnemyType.GiantBoss).confirmDealtDamage,
                botOfEnemyType(EnemyType.GiantBoss).confirmSpawnInterval,
                botOfEnemyType(EnemyType.GiantBoss).eachChargeUltInterval
            >,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> RcvDmg <1> DltDmg <2> SpwItv <3> ChgUlt <4>", "Boss Status",
                confirmReceiveDamage,
                botOfEnemyType(EnemyType.Boss).confirmDealtDamage,
                botOfEnemyType(EnemyType.Boss).confirmSpawnInterval,
                botOfEnemyType(EnemyType.Boss).eachChargeUltInterval
            >,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> RcvDmg <1> DltDmg <2> SpwItv <3> ChgUlt <4>", "Elite Status",
                confirmReceiveDamage,
                botOfEnemyType(EnemyType.Elite).confirmDealtDamage,
                botOfEnemyType(EnemyType.Elite).confirmSpawnInterval,
                botOfEnemyType(EnemyType.Elite).eachChargeUltInterval
            >,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> RcvDmg <1> DltDmg <2> SpwItv <3> ChgUlt <4>", "Minion Status",
                confirmReceiveDamage,
                botOfEnemyType(EnemyType.Minion).confirmDealtDamage,
                botOfEnemyType(EnemyType.Minion).confirmSpawnInterval,
                botOfEnemyType(EnemyType.Minion).eachChargeUltInterval
            >,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "threat Level", threatLevel>,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "auto Tune", autoTune>,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "cutOffsetDistance", cutOffsetDistance>,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();


        CreateHudText(player,
            null,
            <"<0> <1>", "TotalObjectProgress()", TotalObjectProgress()>,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "GameProgressDiff()", GameProgressDiff()>,
            null,
            Location.Left, -1,
            Color.White, Color.Red, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "Server Load", ServerLoad()>,
            null,
            Location.Left, -1,
            Color.White, Color.Orange, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();
    }
    
    if(
        debugMode == DebugMode.ShowParam ||
        debugMode == DebugMode.WithRecord
    ){
        CreateHudText(player,
            null,
            <"<0> <1>", "Server Load Average", ServerLoadAverage()>,
            null,
            Location.Left, -1,
            Color.White, Color.Orange, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();

        CreateHudText(player,
            null,
            <"<0> <1>", "Server Load Peak", ServerLoadPeak()>,
            null,
            Location.Left, -1,
            Color.White, Color.Orange, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();
    }

        CreateHudText(player,
            null,
            <"<0> <1>", "current Slow Level", currentSlowLevel>,
            null,
            Location.Left, -1,
            Color.White, Color.Orange, Color.White,
            HudTextRev.VisibleToAndString, Spectators.VisibleNever);
        debugHudEntitys[End()] = LastTextID();
}

// Hide debug icon and hud
void HideDebugHud()"HideDebugHud"{
    for(gLoopIndex = 0; gLoopIndex < CountOf(debugHudEntitys); gLoopIndex ++){
        DestroyHudText(debugHudEntitys[gLoopIndex]);
    }
}

rule: "Debug/resetBotList"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    !IsButtonHeld(ep, Button.Reload) &&
    IsButtonHeld(ep, Button.Crouch)
){
    // Recalculate portal
    WaitLongPressConfirm();

    define list = AllPlayers(enemyTeam);
    for(gLoopIndex = 0; gLoopIndex < CountOf(list); gLoopIndex ++){
        define bot = list[gLoopIndex];
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion, enemyColor, bot, bot.eachEffectTextSize);
    }
    someHero = RandomHero;
    // create all bot
    CreateEnemyBotsByAssetList(assetList, enemyTeam);
}

rule: "Debug/increaseMatchTime"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Ability2)
){
    WaitLongPressConfirm();
    SetMatchTime(MatchTime() + 30);
    LoopIfConditionIsTrue();
}

rule: "Debug/decreaseMatchTime"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Ability1)
){
    WaitLongPressConfirm();
    SetMatchTime(MatchTime() - 30);
    LoopIfConditionIsTrue();
}
