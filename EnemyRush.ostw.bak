import "EnemyRush.setting.json";
import "Lib/Debug.del";
import "Lib/Utility.del";
import "Lib/ServerOverLoadMeasures.del";
import "Lib/Bot.del";
import "Lib/Buddy.del";
// import "Lib/Mutation.del";

// TAG Signature and infomation ----------
disabled rule: "ENEMY RUSH -HORDE PVE [ver.2.0] Share code [V7PEF]. You allowed copy or modify code." -1 {}
disabled rule: "This script was generated by [Overwatch-Script-To-Workshop] tools. Check detail with https://github.com/ItsDeltin/Overwatch-Script-To-Workshop" -1 {}
disabled rule: "Original source code was created by [sarami77#1868]. Check detail with https://github.com/sarami77/OverwatchWorkShopScripts" -1 {}

// TAG Initialize ----------
define playerTeam: Team.Team2;
define enemyTeam: Team.Team1;

globalvar define initCompleted!;

rule: "Initialize/initialize"
Event.OngoingGlobal
{
    // Stop records to reduce server load
    DisableInspectorRecording();

    // If run it right after loading the server, some scripts fail, so wait 3 seconds
    Wait(3, WaitBehavior.AbortWhenFalse);

    // Start watch over load
    slowControlHandler = SlowControlHandler.ServerOverLoadMeasures;

    // show version and share code
    CreateHudText(AllPlayers(),
        null, "ENEMY RUSH -HORDE PVE [ver.3.0] Share code [V7PEF]", null,
        Location.Left, -999,
        Color.White, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

    // show HUD of enemy team assembly
    CreateHudText(AllPlayers(),
        null,
        null,
        <"Enemy Team Assembly \"<0>\"", enemyTeamName>,
        Location.Left, -998,
        Color.White, Color.White, Color.Purple,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

    // Notify completion of init game setting and start game main logic
    initCompleted = true;
}

rule: "Initialize/shortenAssemblingHeroes"
Event.OngoingGlobal
if(
    IsAssemblingHeroes()
){
    // SetMatchTime(0);
    SetMatchTime(25);
}

rule: "Initialize/shortenSetup"
Event.OngoingGlobal
if(
    IsInSetup()
){
    SetMatchTime(25);
}

// TAG Buddy ----------
rule: "Buddy/BuddyContorl"
Event.OngoingGlobal
if(
    initCompleted
){
    define buddyAssetList! = 
    WorkshopSettingToggle("Follower AI Settings(Join on behalf of absent player)", "Random Hero", true) ?
        // random list
        [
            Hero.Baptiste,
            RandomTankHero,
            RandomDamageHero,
            RandomSupportHero,
            RandomTankHero,
            RandomDamageHero,
            RandomSupportHero,
            RandomDamageHero
        ]
    :
        // fix list
        [
            Hero.Mercy,
            Hero.Reaper,
            Hero.Reinhardt,
            Hero.Kiriko,
            Hero.Soldier76,
            Hero.Doomfist,
            Hero.Zenyatta,
            Hero.Widowmaker
        ];

    // slice buddy list by setting
    buddyAssetList = ArraySlice(buddyAssetList, 0,
        WorkshopSettingInteger(
            "Follower AI Settings(Join on behalf of absent player)",
            "Follower Max Num", 8, 0, 8, 1)
    );

    define prevPlayerNum! = -1;
    define maxSlotNum! = NumberOfSlots(playerTeam);
    define maxBuddyNum! = CountOf(buddyAssetList);

    while(true){
        define playerNum! =
            CountOf(
                FilteredArray(
                    AllPlayers(playerTeam),
                    !IsDummyBot(ae)
                )
            );
        define buddyNum! =
            CountOf(
                FilteredArray(
                    AllPlayers(playerTeam),
                    IsDummyBot(ae)
                )
            );
        define needBuddyNum! = 
            Min(
                maxSlotNum - playerNum,
                maxBuddyNum
            );

        if(
            // not filed by need buddy number
            buddyNum != needBuddyNum ||
            // player number changed
            prevPlayerNum != playerNum
        ){
            // get avg ult charge of bots
            define ultCharge! = GetAvgUltChgOfDummyBot(playerTeam);
            // save player num
            prevPlayerNum = playerNum;
            // relaod buddy list
            DestroyBots(playerTeam);
            // get empty slot num
            define emptySlotNum! = maxSlotNum - playerNum;
            // build add hero list
            define addList! = ArraySlice(buddyAssetList, 0, emptySlotNum);
            for(define buddyAddIndex! = 0; buddyAddIndex < CountOf(addList); buddyAddIndex++){
                if(
                    IsAssemblingHeroes()
                ){
                    Wait(RandomReal(0, 3));
                }
                CreateBotsByHeroInput(addList[buddyAddIndex], 1, playerTeam);
            }
            // set avg ult charge of bots
            SetAvgUltChgOfDummyBot(playerTeam, ultCharge);
        }

        // show human player outline
        StartForcingPlayerOutlines(
            FilteredArray(AllPlayers(playerTeam), !IsDummyBot(ae)),
            HostPlayer(),
            true,
            Color.Green,
            OutlineType.Always
        );
    
        Wait(10, WaitBehavior.IgnoreCondition);
    }
}

rule: "Buddy/Teleport/init"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    IsDummyBot() &&
    HasSpawned()
){
    watchUpdateRate = 2;
    if(IsGameInProgress()){
        // teleport to player near object
        Teleport(ep, ClosestPlayerTo(CurrentObjectPosition(), playerTeam));
    }
    else{
        // teleport to spawn point
        Teleport(ep, SpawnPoints(playerTeam)[0]);
    }
}

rule: "Buddy/fastRespawnBySucide"
Event.OnDeath
if(
    TeamOf() == playerTeam &&
    IsDummyBot() &&
    Attacker() == ep
){
    Respawn();
}

// TAG Payload ----------
globalvar define prevPayloadPosition!;

globalvar define payloadDirection!;

rule: "Payload/updatePayloadDirection"
Event.OngoingGlobal
if(
    PayloadProgressPercentage() > 0
){
    prevPayloadPosition = PayloadPosition();

    while(true){
        define nowPayloadPosition! = PayloadPosition();
        if(
            IsPlayerOnObject &&
            DistanceBetween(nowPayloadPosition, prevPayloadPosition) > 0.01
        ){
            payloadDirection = DirectionTowards(prevPayloadPosition, nowPayloadPosition);
            prevPayloadPosition = nowPayloadPosition;
        }
        Wait(1, WaitBehavior.IgnoreCondition);
    }
}

// TAG Player ----------
// TAG Player/FisrtFast
rule: "Player/FisrtFast/setFastSpeed"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    IsAlive() &&
    IsInSpawnRoom()
){
    SetMoveSpeed(ep, 150);
    while(!IsInLineOfSight(ep, ClosestPlayerTo(ep, enemyTeam))){
        Wait(1, WaitBehavior.IgnoreCondition);
    }
    SetMoveSpeed(ep, DefaultPercentage());
}

// TAG EnemBot ----------
globalvar define someHero!;

define assetList:
    [
        // random hero
        [   // assetNo 0
            "basic",
            [EnemyType.General,     RandomHero,         1],
            [EnemyType.Elite,       RandomHero,         5],
            [EnemyType.Minion,      RandomHero,         10]
        ],
        [   // assetNo 1
            "General and minions",
            [EnemyType.General,     RandomHero,         1],
            [EnemyType.Minion,      RandomHero,         15]
        ],
        [   // assetNo 2
            "all elites",
            [EnemyType.Elite,       RandomHero,         5],
            [EnemyType.Elite,       RandomHero,         5],
            [EnemyType.Elite,       RandomHero,         6]
        ],
        [   // assetNo 3
            "double General",
            [EnemyType.General,     RandomHero,         1],
            [EnemyType.General,     RandomHero,         1],
            [EnemyType.Minion,      RandomHero,         14]
        ],
        [   // assetNo 4
            "the boss",
            [EnemyType.Boss,   RandomValueInArray([
                Hero.Genji, Hero.Tracer, Hero.Soldier76, Hero.Bastion
            ]),         1],
            [EnemyType.Minion,      RandomHero,         11]
        ],
        [   // assetNo 5
            "minion horde",
            [EnemyType.Minion,   RandomValueInArray([
                Hero.Reinhardt, Hero.Roadhog, Hero.Genji, Hero.Soldier76, Hero.Bastion, Hero.Tracer, Hero.Pharah, Hero.Echo
            ]),        16]
        ],
        [   // assetNo 6
            "the dps boss",
            [EnemyType.Boss,   RandomValueInArray([
                Hero.Reaper, Hero.Genji, Hero.Soldier76, Hero.Bastion, Hero.Tracer, Hero.Pharah, Hero.Echo
                                            ]),         1],
            [EnemyType.Minion,      RandomHero,         11]
        ],
        [   // assetNo 7
            "finale",
            [EnemyType.Boss,   RandomValueInArray([
                Hero.Reinhardt, Hero.Sigma, Hero.Doomfist, Hero.Roadhog, Hero.Orisa, Hero.Zarya
                                            ]),         1],
            [EnemyType.Minion,      RandomHero,         11]
        ],
        [   // assetNo 8
            "many turrets",
            [EnemyType.Elite,      RandomValueInArray([
                Hero.Symmetra, Hero.Torbjorn, Hero.WreckingBall
                                            ]),         16]
        ]
    ];

define assetLotteryList: [
    // 8
    0, 0, 0,
    1, 1, 1,
    2, 2, 2,
    3, 3,
    4
];

// TAG EnemyBot/BotAsset ----------
globalvar define reloadAssetTimer = -1;

rule: "EnemyBot/initeloadAssetTimer"
Event.OngoingGlobal
if(
    initCompleted &&
    (
        (IsInSetup() && MatchTime() <= 5 ) ||
        (IsGameInProgress())
    )
){
    reloadAssetTimer = 0;
}

rule: "EnemyBot/coutndownReloadAssetTimer"
Event.OngoingGlobal
if(
    0 < reloadAssetTimer
){
    reloadAssetTimer --;
    Wait(1, WaitBehavior.IgnoreCondition);
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/reloadBotAsset"
Event.OngoingGlobal
if(
    reloadAssetTimer == 0
){
    dbgMsg("reloadBotAsset start");

    // create all bot
    someHero = RandomHero;
    PlayEffectsToTeam(enemyTeam);
    DestroyBots(enemyTeam);
    CreateBotsByAsset(assetList[RandomValueInArray([assetLotteryList])], enemyTeam);
    // set next timer
    reloadAssetTimer = RandomInteger(90, 150);
}

globalvar define progressStage;

rule: "EnemyBot/reloadBotAssetByProgress"
Event.OngoingGlobal
if(
    initCompleted &&
    IsGameInProgress()
){
    define progress! = TotalObjectProgress();
    define assetNo! = -1;
    define assetTimer;

    if(
        progressStage == 0 && progress >= 30
    ){
        progressStage ++;
        assetNo = 5;
        assetTimer = 90;
    }
    else if(
        progressStage == 1 && progress >= 60
    ){
        progressStage ++;
        assetNo = 6;
        assetTimer = 90;
    }
    else if(
        progressStage == 2 && progress >= 90
    ){
        progressStage ++;
        assetNo = 7;
        assetTimer = -1;
    }

    if(assetNo != -1){
        // create all bot
        PlayEffectsToTeam(enemyTeam);
        DestroyBots(enemyTeam);
        CreateBotsByAsset(assetList[assetNo], enemyTeam);
        // set next timer
        reloadAssetTimer = assetTimer;
    }

    Wait(2, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

void PlayEffectsToTeam(define team!) "PlayEffectsToTeam"{
    define players! = AllPlayers(team);
    for(define playersIndex! = 0; playersIndex < CountOf(players); playersIndex ++){
        // play blast
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion,
            TeamOf(players[playersIndex]) == enemyTeam ?
                enemyColor :
                Color.Yellow,
            PositionOf(players[playersIndex]),
            TeamOf(players[playersIndex]) == enemyTeam ?
                players[playersIndex].eachTextSize : 
                basicInWorldTextSize + 0.5);
    }
    Wait(0.1, WaitBehavior.IgnoreCondition);
}

// TAG EnemyBot/EnemyType ----------
playervar define eachDealtDamageScale!;
playervar define eachMaxSpawnInterval!;
playervar define eachMinSpawnInterval!;
playervar define eachChargeUltInterval!;

playervar define eachTextOffset!;
playervar define eachTextSize!;
playervar define eachNameText!;
playervar define eachNameColor!;

playervar define eachPerkDropProbability;   // dummy

define minionSpawnTimeMin: 10 * 1;
define eliteSpawnTimeMin: 10 * 2;
define generalBossSpawnTimeMin: 10 * 4;
define spawnTimeMinToMaxMulti: 1.5;

rule: "EnemyBot/EnemyType/setEnemyType"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    HasSpawned() &&
    enemyType != EnemyType.None
){
    // set no back when Low Health
    backWhenLowHealth = false;

    // setEnemyType
    if(enemyType == EnemyType.Minion){
        watchUpdateRate = 18;
        eachTextOffset = 0.75;
        StartScalingPlayer(ep, 0.75, false);
        StartModifyingHeroVoiceLines(ep, 1.5, false);

        SetMaxHealth(ep, 25);
        SetHealingDealt(ep, 50);
        SetHealingReceived(ep, 25);
        
        eachDealtDamageScale = 0.5;
        eachChargeUltInterval = 90;
        eachMinSpawnInterval = minionSpawnTimeMin;
        eachMaxSpawnInterval = minionSpawnTimeMin * spawnTimeMinToMaxMulti;

        eachNameText = "Minion";
        eachNameColor = Color.White;
        eachTextSize = basicInWorldTextSize;
    }
    else if(enemyType == EnemyType.Elite){
        watchUpdateRate = 6;
        eachTextOffset = 1.0;
        StartScalingPlayer(ep, 1.0, false);
        StartModifyingHeroVoiceLines(ep, 1.0, false);

        SetMaxHealth(ep, 100);
        SetHealingDealt(ep, 200);
        SetHealingReceived(ep, 100);

        eachDealtDamageScale = 1.0;
        eachChargeUltInterval = 30;
        eachMinSpawnInterval = eliteSpawnTimeMin;
        eachMaxSpawnInterval = eliteSpawnTimeMin * spawnTimeMinToMaxMulti;

        eachNameText = "Elite";
        eachNameColor = Color.Yellow;
        eachTextSize = basicInWorldTextSize + 0.5;
    }
    else if(enemyType == EnemyType.General){
        watchUpdateRate = 2;
        eachTextOffset = 1.5;
        StartScalingPlayer(ep, 1.5, false);
        StartModifyingHeroVoiceLines(ep, 0.5, false);

        SetMaxHealth(ep, 1000);
        SetHealingDealt(ep, 400);
        SetHealingReceived(ep, 200);

        eachDealtDamageScale = 2.0;
        eachChargeUltInterval = 10;
        eachMinSpawnInterval = generalBossSpawnTimeMin;
        eachMaxSpawnInterval = generalBossSpawnTimeMin * spawnTimeMinToMaxMulti;

        eachNameText = "General";
        eachNameColor = Color.Orange;
        eachTextSize = basicInWorldTextSize + 1;
    }
    else if(enemyType == EnemyType.Boss){
        watchUpdateRate = 2;
        eachTextOffset = 2.5;
        StartScalingPlayer(ep, 2.5, false);
        StartModifyingHeroVoiceLines(ep, 0.1, false);

        SetMaxHealth(ep, 1000);
        SetHealingDealt(ep, 400);
        SetHealingReceived(ep, 200);

        eachDealtDamageScale = 2.0;
        eachChargeUltInterval = 10;
        eachMinSpawnInterval = generalBossSpawnTimeMin;
        eachMaxSpawnInterval = generalBossSpawnTimeMin * spawnTimeMinToMaxMulti;

        eachNameText = "Boss";
        eachNameColor = Color.Red;
        eachTextSize = basicInWorldTextSize + 1.5;

        // set addtinal health
        Wait(0.1, WaitBehavior.AbortWhenFalse);
        define health! = MaxHealth(ep) * 0.5;
        AddHealthPoolToPlayer(ep, HealthType.Armor, health, true, false);
        AddHealthPoolToPlayer(ep, HealthType.Shields, health, true, false);
    }

    // update param
    UpdateEachDealtDamage();
    UpdateEachSpawnInterval();
}

rule: "EnemyBot/Effect/setFullHealth"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None &&
    IsAlive()
){
    // heal to max health
    Wait(0.5, WaitBehavior.IgnoreCondition);
    while(Health() < MaxHealth()){
        Heal(ep, null, MaxValue());
        Wait(0.5, WaitBehavior.IgnoreCondition);
    }
}

// TAG EnemyBot/Effect ----------
define enemyColor: Color.Purple;
playervar define botAppearanceEntitys!;

rule: "EnemyBot/Effect/showBotAppearance"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None &&
    IsAlive()
){
    botAppearanceEntitys = [];
    // show evil aura
    botAppearanceEntitys[0] = 
        CreateEffect(AllPlayers(), Effect.Cloud, enemyColor,
            ep, eachTextSize, EffectRev.VisibleToPositionAndRadius);
    // show name text
    botAppearanceEntitys[1] = 
        CreateInWorldText(
            AllPlayers(), eachNameText, 
            ep + Vector(0, eachTextOffset * 2.55, 0), eachTextSize, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString,
            eachNameColor, Spectators.DefaultVisibility);
}

rule: "EnemyBot/Effect/hideBotAppearanceAndReek"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None &&
    !IsAlive()
){
    // hide evil aura
    DestroyEffect(botAppearanceEntitys[0]);
    // hide name text
    DestroyInWorldText(botAppearanceEntitys[1]);
    // play dead effect
    PlayContinuousBlast(Color.Red, PositionOf(),
        eachTextSize * 1.5, eachTextSize, 0);
}

// TAG EnemyBot/Teleport ----------
playervar define teleportPosition!;
globalvar define commonTeleportPosition!;
define teleportAreaSize: 10;

Player ClosestPlayerFromObject()"ClosestPlayerFromObject"{
    return FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(playerTeam),
                (
                    HasSpawned(ae) &&
                    IsAlive(ae)
                    // !IsInSpawnRoom(ae)
                )
            ),
            DistanceBetween(ae, CurrentObjectPosition())
        )
    );
}

Boolean SetTeleportPositionToPlayerFront() playervar "SetTeleportPositionToPlayerFront"{
    // set teleportToTarget at closest player from Object
    define teleportToTarget! = ClosestPlayerFromObject();

    for(pLoopIndex = 0; pLoopIndex < 10; pLoopIndex++){
        WaitRandomForDistributeServerLoad();

        define currentObjectPosition! = CurrentObjectPosition();
    
        // select teleportPosition from middle of Object and teleportToTarget
        teleportPosition =
            NearestWalkablePosition(
                PositionOf(teleportToTarget)
                    + 
                (
                    DirectionTowards(teleportToTarget, currentObjectPosition)
                        *
                    DistanceBetween(teleportToTarget, currentObjectPosition) * 0.35
                )
                    +
                RandomPosition(
                    (teleportAreaSize),
                    (teleportAreaSize),
                    (teleportAreaSize)
                )
            );
        break;
    }
    return true;
}

Boolean SetTeleportPositionToPlayerAround() playervar "SetTeleportPositionToPlayerAround"{
    define teleportToTarget! = ClosestPlayerFromObject();

    for(pLoopIndex = 0; pLoopIndex < 10; pLoopIndex++){
        WaitRandomForDistributeServerLoad();

        // select teleportPosition from current object around
        teleportPosition = 
            NearestWalkablePosition(
                PositionOf(teleportToTarget)
                    +
                RandomPosition(
                    (teleportAreaSize),
                    (teleportAreaSize),
                    (teleportAreaSize)
                )
            );
        
        define telPosNearTarget! = ClosestPlayerTo(teleportPosition, playerTeam);
        if(
            // Has line to target
            IsInLineOfSight(
                PositionOffsetY1() + PositionOf(telPosNearTarget),
                PositionOffsetY1() + teleportPosition,
                BarrierLOS.NoBarriersBlock)  
        ){
            break;
        }
        Wait(0.5, WaitBehavior.AbortWhenFalse);
    }
    return true;
}

globalvar define additinalTeleportAreaSize;
define teleportAreaSizeOffset: 30;
define OnCurrentObjectPosition(): CurrentObjectPosition() + Vector(0, 5, 0); // not collision in payload object position

Boolean SetTeleportPositionToObjectAhead() playervar "SetTeleportPositionToObjectAhead"{
    define teleportToTarget! = OnCurrentObjectPosition();
    
    if(
        payloadDirection != 0 &&
        IsPlayerOnObject
    ){
        // get bloacked position of payload ahead
        define blockedHead! = BlockedPositon(teleportToTarget, payloadDirection, teleportAreaSizeOffset);
        // increase teleportAreaSize by cut off distance for spam payload block by teleport
        additinalTeleportAreaSize = teleportAreaSizeOffset - DistanceBetween(teleportToTarget, blockedHead);
        additinalTeleportAreaSize /= 2;
        // change teleportTarget to payload ahead
        teleportToTarget = blockedHead;
    }
    else{
        additinalTeleportAreaSize = 0;
    }

    for(pLoopIndex = 0; pLoopIndex < 10; pLoopIndex++){
        WaitRandomForDistributeServerLoad();

        // select teleportPosition from teleportToTarget around
        teleportPosition = 
            NearestWalkablePosition(
                teleportToTarget
                    +
                RandomPosition(
                    (teleportAreaSize + additinalTeleportAreaSize),
                    (teleportAreaSize + additinalTeleportAreaSize),
                    (teleportAreaSize + additinalTeleportAreaSize)
                )
            );

        define telPosNearTarget! = ClosestPlayerTo(teleportPosition, playerTeam);
        if(
            // Has line to target
            IsInLineOfSight(
                PositionOffsetY1() + PositionOf(telPosNearTarget),
                PositionOffsetY1() + teleportPosition,
                BarrierLOS.NoBarriersBlock)            
        ){
            break;
        }
        Wait(0.5, WaitBehavior.AbortWhenFalse);
    }
    teleportPosition -= PositionOffset();
    return true;
}

Boolean SetTeleportPositionToCommonPosition() playervar "SetTeleportPositionToCommonPosition"{
    // set telPosNearTarget at closest player from commonTeleportPosition
    if(
        // update commonTeleportPosition conditions
        // invalid commonTeleportPosition
        commonTeleportPosition ==  0 ||
        // Probability
        ProbabilityTrue(3) ||
        // player too close from commonTeleportPosition
        DistanceBetween(commonTeleportPosition, ClosestPlayerTo(commonTeleportPosition, playerTeam)) < 3 ||
        // has not line commonTeleportPosition to current object 
        !IsInLineOfSight(
            PositionOffsetY1() + CurrentObjectPosition(),
            PositionOffsetY1() + commonTeleportPosition,
            BarrierLOS.NoBarriersBlock
        )
    ){
        // new select common position
        // set teleport position to player around
        if(SetTeleportPositionToObjectAhead()){
            // save teleport position as common position
            commonTeleportPosition = teleportPosition;
        }
        else{
            // set teleport position from common position
            teleportPosition = commonTeleportPosition;
        }
    }
    else{
        // set teleport position from common position
        teleportPosition = commonTeleportPosition;
    }
    return true;
}

define showTeleportBeamTime: 0.4;
void PlayTeleportEffect(define isBefore!) playervar "PlayTeleportEffect"{
    // if before teleport
    if(isBefore){
        // play teleport shadow of own
        PlayEffect(AllPlayers(), PlayEffect.BadPickupEffect, enemyColor, ep, 1);
        // play teleport shadow of dest
        PlayEffect(AllPlayers(), PlayEffect.BadPickupEffect, enemyColor, teleportPosition, 1);
        // show teleport beam effect
        DestroyEffect(telBeam);
        define fromSky! = IsInSpawnRoom();
        telBeam = CreateBeamEffect(AllPlayers(),
            BeamType.BadBeam,
            fromSky ? 
                teleportPosition + Vector(0, 30, 0) :
                BodyPosition(ep),
            teleportPosition + PositionOffset(),
            Color.Purple, EffectRev.VisibleToPositionAndRadius);
        // wait for show teleport shadow of before
        Wait(showTeleportBeamTime, WaitBehavior.IgnoreCondition);
    }
    else{
        // play teleport blast
        PlayEffect(AllPlayers(), PlayEffect.BadExplosion, enemyColor, ep, eachTextSize);
        // hide teleport beam effect
        DestroyEffect(telBeam);
        // play teleport sound
        PlayEffect(AllPlayers(), PlayEffect.RingExplosionSound, null, ep, 40);
    }
}

playervar define telBeam!;

void TeleportWithEffect() playervar "TeleportWithEffect"{
    define waitTeleportTimer! = 
        IndexOfArrayValue(
            FilteredArray(
                AllPlayers(enemyTeam), IsInSpawnRoom(ae)
            ),
            ep
        ) *
        0.1;
    waitTeleportTimer = waitTeleportTimer < 0 ? 0 : waitTeleportTimer;
    Wait(waitTeleportTimer, WaitBehavior.AbortWhenFalse);
    // show pre teleport effect
    PlayTeleportEffect(true);
    // Teleport
    Teleport(ep, teleportPosition);
    // show teleport effect
    PlayTeleportEffect(false);
    // hide teleport beam effect
    DestroyEffect(telBeam);
}

define forceActiveTeleport: 30;
define teleportToCommonPositionProb: 50;

define IsPlayerOnObject: ([] != AllPlayersOnObjective(playerTeam));
define IsEnemyOnObject: ([] != AllPlayersOnObjective(enemyTeam));

void HybridTeleport()"HybridTeleport"{
    if(
        // during Object attacks
        IsPlayerOnObject ||
        // probability
        ProbabilityTrue(forceActiveTeleport)
    ){
        if(
            // probability
            ProbabilityTrue(teleportToCommonPositionProb)
        ){
            if(SetTeleportPositionToCommonPosition()){
                TeleportWithEffect();
            }
        }
        else{
            if(SetTeleportPositionToObjectAhead()){
                TeleportWithEffect();
            }
        }
    }
    else{
        if(IsTeamOnDefense(TeamOf())){
            if(SetTeleportPositionToPlayerFront()){
                TeleportWithEffect();
            }
        }
        else{
            if(SetTeleportPositionToPlayerAround()){
                TeleportWithEffect();
            }
        }
    }
}

rule: "EnemyBot/Teleport/teleportByRespawn"
Event.OngoingPlayer
if(
    IsGameInProgress() &&
    TeamOf() == enemyTeam &&
    IsInSpawnRoom()
){
    HybridTeleport();
    // failsafe
    Wait(3, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

define checkTeleportConditionMaxCount: 3;

rule: "EnemyBot/Teleport/teleportForDefenceObject"
Event.OngoingPlayer
if(
    IsGameInProgress() &&
    TeamOf() == enemyTeam &&
    IsAlive() &&
    !IsInSpawnRoom()
){
    for(define checkTeleportConditionCount! = 0; checkTeleportConditionCount < checkTeleportConditionMaxCount; checkTeleportConditionCount ++){
        WaitRandomForDistributeServerLoad();

        Wait(1, WaitBehavior.AbortWhenFalse);
        // check not teleport condition
        if(
            actionState != ActionState.Non &&        // in combat
            (
                DistanceBetween(ep, CurrentObjectPosition()) <= 30 ||   // near object
                !IsPlayerOnObject                                       // player not on object
            )
        ){
            // reset count up
            LoopIfConditionIsTrue();
        }
    }
    // teleport when over checkTeleportConditionMaxCount 
    HybridTeleport();
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/Teleport/clearCommonTeleportPosition"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() == ep
){
    commonTeleportPosition = 0;
}

// TAG EnemyBot/EnemyAbility ----------
globalvar define enemyAbilityStatus;

void WaitEnemyAbility()"WaitEnemyAbility"{
    Wait(
        10 +
        RandomInteger(0, 10) +
        (20 * 1 - (TotalObjectProgress() / DefaultPercentage())),
        WaitBehavior.AbortWhenFalse
    );
}

rule: "EnemyBot/EnemyAbility/rush"
Event.OngoingGlobal
if(
    IsGameInProgress() &&
    !IsOverTime()
){
    enemyAbilityStatus[0] = false;
    WaitEnemyAbility();
    define rushDurling! = RandomInteger(10, 20);
    for(rushCount; rushDurling > 0; rushDurling--){
        Respawn(AllDeadPlayers(enemyTeam));

        enemyAbilityStatus[0] = rushDurling;
        Wait(1, WaitBehavior.AbortWhenFalse);
    }

    LoopIfConditionIsTrue();
}

rule: "EnemyBot/EnemyAbility/shotCall"
Event.OngoingGlobal
if(
    IsGameInProgress()
){
    enemyAbilityStatus[1] = false;
    WaitEnemyAbility();
    define deployedEnemys! = FilteredArray(
        AllLivingPlayers(enemyTeam),
        !IsInSpawnRoom(ae)
    );
    define shotCallDelay! = RandomReal(0.1, 1);
    for(define scLoopIndex! = 0; scLoopIndex < CountOf(deployedEnemys); scLoopIndex ++){
        SetUltimateCharge(deployedEnemys[scLoopIndex], DefaultPercentage());
        PressButton(deployedEnemys[scLoopIndex], Button.Ultimate);

        enemyAbilityStatus[1] = CountOf(deployedEnemys) - scLoopIndex;
        Wait(shotCallDelay, WaitBehavior.AbortWhenFalse);
    }

    LoopIfConditionIsTrue();
}

rule: "EnemyBot/EnemyAbility/combatTeleport"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    (
        enemyType == EnemyType.Boss ||
        enemyType == EnemyType.General
    ) &&
    IsAlive() &&
    actionState != ActionState.Non
){
    define interval! = NormalizedHealth() * 10 + RandomInteger(1, 5);
    Wait(interval, WaitBehavior.AbortWhenFalse);
    // set teleport position to object around
    if(SetTeleportPositionToPlayerAround()){
        TeleportWithEffect();
    }
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/EnemyAbility/hopToObject"
Event.OngoingPlayer
if(
    initCompleted &&
    TeamOf() == enemyTeam &&
    IsAlive() &&
    TotalObjectProgress() >= 95
){
    if(
        !IsInSpawnRoom() &&
        IsOnGround() &&
        IsPlayerOnObject &&
        CountOf(AllPlayersOnObjective(enemyTeam)) <= (NumberOfPlayers(enemyTeam) / 3) &&
        DistanceBetween(ep, OnCurrentObjectPosition()) >= 5
    ){
        PressButton(ep, Button.Jump);
        ApplyImpulse(ep, 
            VectorTowards(ep, OnCurrentObjectPosition()), 10,
            Relative.ToWorld, ContraryMotion.Cancel);
    }
    
    Wait(RandomReal(0.8, 1.2), WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/chargeUltimate ----------
rule: "EnemyBot/chargeUltimate"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    HasSpawned() &&
    enemyType != EnemyType.None &&
    !IsUsingUltimate()
){
    SetUltimateCharge(ep, UltimateChargePercent() + (DefaultPercentage() / eachChargeUltInterval));
    WaitUpdateASecond();
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/SpawnInterval ----------
define showDeadEffectTime: 1.5;
playervar define confirmSpawnInterval!;

Number TotalObjectProgress()"TotalObjectProgress"{
    Number progress;

    if(!IsGameInProgress()){
        progress = DefaultPercentage();
    }
    else{
        define mode = CurrentGameMode();
        if(mode == GameMode.Assault){
            if(CurrentObjectNo() == 0){
                progress = (PointCapturePercentage() * 0.5);
            }
            else{
                progress = 50 + (PointCapturePercentage() * 0.5);
            }
        }
        else if(mode == GameMode.Escort){
            progress = PayloadProgressPercentage();
        }
        else if(mode == GameMode.Hybrid){
            if(CurrentObjectNo() == 0){
                progress = (PointCapturePercentage() * 0.3);
            }
            else{
                progress = 30 + (PayloadProgressPercentage() * 0.7);
            }
        }
        else{
            errLog("ERROR ON TotalObjectProgress");
        }
    }
    return progress;
}

globalvar define spawnTimeScale;

void UpdateEachSpawnInterval()"UpdateEachSpawnInterval"{
    confirmSpawnInterval = (eachMaxSpawnInterval - eachMinSpawnInterval);
    confirmSpawnInterval *= (1 - (TotalObjectProgress() / DefaultPercentage()));
    confirmSpawnInterval += eachMinSpawnInterval;
    
    define diff = GameProgressDiff();
    if(70 <= diff){
        spawnTimeScale = 0;
    }
    else if(40 <= diff && diff < 70){
        spawnTimeScale = 0.25;
    }
    else if(10 <= diff && diff < 40){
        spawnTimeScale = 0.5;
    }
    else if(-10 <= diff && diff < 10){
        spawnTimeScale = 1.0;
    }
    else if(-40 <= diff && diff < -10){
        spawnTimeScale = 1.5;
    }
    else if(-70 <= diff && diff < -40){
        spawnTimeScale = 2.5;
    }
    else if(diff < -70){
        spawnTimeScale = 4.0;
    }
    confirmSpawnInterval *= spawnTimeScale;
    SetRespawnMaxTime(ep, confirmSpawnInterval + showDeadEffectTime);
}

Number GameProgressDiff()"GameProgressDiff"{
    Number prgDiff! = 0;
    Number objPrg! = RoundToInteger(TotalObjectProgress());  // MAX 100
    Number timePrg  = RoundToInteger(MatchTime() / 3.0);  // MAX 300
    timePrg = DefaultPercentage() - timePrg;
    prgDiff = objPrg - timePrg;
    return prgDiff;
}

rule: "EnemyBot/SpawnInterval/update"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None
){
    UpdateEachSpawnInterval();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

rule: "EnemyBot/SpawnInterval/fastRespawnBySucide"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() == ep
){
    Wait(showDeadEffectTime, WaitBehavior.AbortWhenFalse);
    Respawn();
}

// TAG EnemyBot/ReceivedDamage ----------
globalvar define confirmReceiveDamage!;

void UpdateEachReceivedDamage()"UpdateEachReceivedDamage"{
    // set base damage
    confirmReceiveDamage = DefaultPercentage();
    // apply team balance
    confirmReceiveDamage *= ConvertToBalancedValue(1.5, playerTeam);
    SetDamageReceived(AllPlayers(enemyTeam), confirmReceiveDamage);
}

rule: "EnemyBot/ReceivedDamage/update"
Event.OngoingGlobal
if(
    initCompleted
){
    UpdateEachReceivedDamage();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/DealtDamage ----------
playervar define confirmDealtDamage!;
globalvar define autoTuneDmgDlt!;
globalvar define autoTuneScale! = WorkshopSettingReal("Enemy Bot Settings", "Dynamic Difficult Scale", 1.0, 0, 1000, 0);

define internalRate: 0.4;

rule: "EnemyBot/DealtDamage/upTune"
Event.OnDeath
if(
    TeamOf() == enemyTeam &&
    Attacker() != ep
){
    if(enemyType == EnemyType.Minion){
        autoTuneDmgDlt += 0.25 * internalRate * autoTuneScale;
    }
    else if(enemyType == EnemyType.Elite){
        autoTuneDmgDlt += 1.0 *  internalRate * autoTuneScale;
    }
    else if(enemyType == EnemyType.General){
        autoTuneDmgDlt += 10.0 * internalRate * autoTuneScale;
    }
    else if(enemyType == EnemyType.Boss){
        autoTuneDmgDlt += 30.0 * internalRate * autoTuneScale;
    }
}

rule: "EnemyBot/DealtDamage/downTune"
Event.OnDeath
if(
    TeamOf() == playerTeam &&
    Attacker() != ep
){
    autoTuneDmgDlt -= 1.0;
    autoTuneDmgDlt = autoTuneDmgDlt < 0 ? 0 : autoTuneDmgDlt;
}

void UpdateEachDealtDamage()"UpdateEachDealtDamage"{
    // set base damage
    confirmDealtDamage = WorkshopSettingReal("Enemy Bot Settings", "Base Deal Damage", 40, 0, 1000, 1);
    // apply team balance
    confirmDealtDamage /= ConvertToBalancedValue(1.5, playerTeam);
    // apply auto tune
    confirmDealtDamage += autoTuneDmgDlt;
    // apply enemy type scale
    confirmDealtDamage *= eachDealtDamageScale;
    SetDamageDealt(ep, confirmDealtDamage);
}

rule: "EnemyBot/DealtDamage/update"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    enemyType != EnemyType.None
){
    UpdateEachDealtDamage();
    WaitUpdateLongCycle();
    LoopIfConditionIsTrue();
}

// TAG EnemyBot/ObstIcon ----------
playervar define obstIcon;
define color: isBlink ? Color.Red : Color.Yellow;

rule: "EnemyBot/ObstIcon/show"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    (
        IsPlayerOnObject &&
        IsOnObjective(ep)
    )
){
    DestroyIcon(obstIcon);
    obstIcon = CreateIcon(AllPlayers(playerTeam), ep, Icon.ExclamationMark,
        IconRev.PositionAndColor, color, true);
}

rule: "EnemyBot/ObstIcon/hide"
Event.OngoingPlayer
if(
    TeamOf() == enemyTeam &&
    !(
        IsPlayerOnObject &&
        IsOnObjective(ep)
    )
){
    DestroyIcon(obstIcon);
    obstIcon = 0;
}

globalvar define isBlink = false;

rule: "EnemyBot/ObstIcon/blinkColor"
Event.OngoingGlobal
if(
    initCompleted
){
    while(true){
        isBlink = !isBlink;
        Wait(0.1, WaitBehavior.AbortWhenFalse); 
    }
}

// TAG FinalStand ----------
rule: "FinalStand/FinalStand"
Event.OngoingGlobal
if(
    IsGameInProgress() &&
    MatchTime() <= 0
){
    BigMessage(AllPlayers(), "Final Stand !");
    slowControlHandler = SlowControlHandler.GameLogic;
    SetSlowMotion(10);
    Wait(0.3, WaitBehavior.AbortWhenFalse);   // show slow

    PlayEffectsToTeam(Team.All);
    Resurrect(AllPlayers());
    Heal(AllPlayers(), null, MaxValue());
    SetUltimateCharge(AllPlayers(), DefaultPercentage());

    Wait(0.3, WaitBehavior.AbortWhenFalse);   // show slow
    SetSlowMotion(DefaultPercentage());
    slowControlHandler = SlowControlHandler.ServerOverLoadMeasures;

    define scList! = AllPlayers(enemyTeam);
    define scDelay! = RandomReal(0, 1);
    for(define scLoopIndex! = 0; scLoopIndex < CountOf(scList); scLoopIndex ++){
        SetUltimateCharge(scList[scLoopIndex], DefaultPercentage());
        PressButton(scList[scLoopIndex], Button.Ultimate);
        Wait(scDelay, WaitBehavior.AbortWhenFalse);
    }

}

// TAG Debug ----------
rule: "Debug/defaultDebugMode"
Event.OngoingGlobal
Team.All
if(
    initCompleted
){
    debugMode = DebugMode.Non;

    // CreateIcon(commonTeleportPosition != 0 ? AllPlayers(): null,
    //     commonTeleportPosition + PositionOffsetY1(), Icon.Spiral,
    //     IconRev.VisibleToAndPosition, enemyColor, true);
    // CreateEffect(commonTeleportPosition != 0 ? AllPlayers(): null, Effect.Cloud, enemyColor,
    //     commonTeleportPosition + PositionOffsetY1(), 3, EffectRev.VisibleToPositionRadiusAndColor);
    // CreateBeamEffect(commonTeleportPosition != 0 ? AllPlayers(): null,
    //     BeamType.BadBeam, HostPlayer(), commonTeleportPosition + PositionOffsetY1(),
    //     enemyColor, EffectRev.VisibleToPositionAndRadius);
}

rule: "Debug/changeDebugMode"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Jump) 
){
    // change debug mode
    WaitLongPressConfirm();
    if(debugMode == DebugMode.Non){
        debugMode = DebugMode.SlowLevelOnly;
    }
    else if(debugMode == DebugMode.SlowLevelOnly){
        debugMode = DebugMode.ShowParam;
    }
    else if(debugMode == DebugMode.ShowParam){
        debugMode = DebugMode.WithRecord;
    }
    else if(debugMode == DebugMode.WithRecord){
        debugMode = DebugMode.Non;
    }

    // reload hud
    HideDebugHud();
    ShowDebugHud(HostPlayer());
}

globalvar define debugHuds!;

define Last():
    CountOf(debugHuds);

// show debug icon and hud
void ShowDebugHud(define player!)"ShowDebugHud"{
    if(
        debugMode == DebugMode.WithRecord
    ){
        EnableInspectorRecording();
        debugHuds[Last()] = CreateDebugHud("â—Recording", true, Color.Red);
    }
    else{
        DisableInspectorRecording();
    }
    
    if(
        debugMode == DebugMode.SlowLevelOnly ||
        debugMode == DebugMode.ShowParam ||
        debugMode == DebugMode.WithRecord
    ){
        debugHuds[Last()] = CreateDebugHud("Server Load", ServerLoad(), ServerLoad() < 255 ? Color.Orange : Color.Red);
        debugHuds[Last()] = CreateDebugHud("game Logic Speed", gameLogicSpeed, gameLogicSpeed >= 100 ? Color.Orange : Color.Red);
    }
    if(
        debugMode == DebugMode.ShowParam ||
        debugMode == DebugMode.WithRecord
    ){  
        debugHuds[Last()] = CreateDebugHud("watch Time Speed Increase", watchTimeSpeedIncrease, watchTimeSpeedIncrease >= watchTimeSpeedDecrease ? Color.Orange : Color.Red);
        debugHuds[Last()] = CreateDebugHud("count Wait Random For Distribute Server Load", countWaitRandomForDistributeServerLoad, Color.Orange);
        debugHuds[Last()] = CreateDebugHud("human player num", CountOf(FilteredArray(AllPlayers(playerTeam), !IsDummyBot(ae))), Color.Team2);
        debugHuds[Last()] = CreateDebugHud("player team player num", <"<0> / <1>", NumberOfLivingPlayers(playerTeam), NumberOfPlayers(playerTeam)>, Color.Team2);
        debugHuds[Last()] = CreateDebugHud("enemy team player num", <"<0> / <1>", NumberOfLivingPlayers(enemyTeam), NumberOfPlayers(enemyTeam)>, Color.Team1);
        debugHuds[Last()] = CreateDebugHud("enemy spawn interval", <"scale = <0> / M = <1> / E = <2> / G = <3> / B = <4>",
            spawnTimeScale,
            FirstOf(FilteredArray(AllPlayers(enemyTeam), ae.enemyType == EnemyType.Minion)).confirmSpawnInterval,
            FirstOf(FilteredArray(AllPlayers(enemyTeam), ae.enemyType == EnemyType.Elite)).confirmSpawnInterval,
            FirstOf(FilteredArray(AllPlayers(enemyTeam), ae.enemyType == EnemyType.General)).confirmSpawnInterval,
            FirstOf(FilteredArray(AllPlayers(enemyTeam), ae.enemyType == EnemyType.Boss)).confirmSpawnInterval
        >, Color.Team1);
        debugHuds[Last()] = CreateDebugHud("enemy rush status", enemyAbilityStatus[0], enemyAbilityStatus[0] == false ? Color.Team1 : Color.Yellow);
        debugHuds[Last()] = CreateDebugHud("enemy shot call status", enemyAbilityStatus[1], enemyAbilityStatus[1] == false ? Color.Team1 : Color.Yellow);
        debugHuds[Last()] = CreateDebugHud("reload Asset Timer", reloadAssetTimer, reloadAssetTimer > 5 ? Color.Team1 : Color.Yellow);
        debugHuds[Last()] = CreateDebugHud("auto Tune Dmg Dlt", autoTuneDmgDlt, Color.Team1);
    }
}

// Hide debug icon and hud
void HideDebugHud()"HideDebugHud"{
    for(gLoopIndex = 0; gLoopIndex < CountOf(debugHuds); gLoopIndex ++){
        DestroyHudText(debugHuds[gLoopIndex]);
    }
}

rule: "Debug/resetBotList"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    !IsButtonHeld(ep, Button.Reload) &&
    IsButtonHeld(ep, Button.Crouch)
){
    // Recalculate portal
    WaitLongPressConfirm();
    // create all bot
    someHero = RandomHero;
    PlayEffectsToTeam(enemyTeam);
    DestroyBots(enemyTeam);
    CreateBotsByAsset(assetList[RandomValueInArray([assetLotteryList])], enemyTeam);
    // set next timer
    reloadAssetTimer = RandomInteger(90, 180);
}

rule: "Debug/increaseMatchTime"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Ability2)
){
    WaitLongPressConfirm();
    SetMatchTime(MatchTime() + 30);
    LoopIfConditionIsTrue();
}

rule: "Debug/decreaseMatchTime"
Event.OngoingPlayer
Team.All
if(
    HostPlayer() == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Ability1)
){
    WaitLongPressConfirm();
    SetMatchTime(MatchTime() - 30);
    LoopIfConditionIsTrue();
}

disabled rule: "Output Completed" 99999 {}
