import "Utility.del";

// Function enable team
// Set team in import destination parent file
globalvar define Revive_enableTeam! = Team.Team1;
globalvar define Revive_timeLimit! = 10;
define Revive_isEnableTeam:
    Revive_enableTeam == TeamOf() ||
    Revive_enableTeam == Team.All;

playervar define downPosition!;
playervar define reviveTimeRemain!;
playervar define reviveCountDown!;
playervar define needHelpEntitys!;

define reviveTime: 3;
define reviveRange : 2.5;
define CanRevive():
    IsDead() &&
    IsOnGround();

define NearTeammate():
    CountOf(
        FilteredArray(
            AllPlayers(TeamOf()), (
                ep != ArrayElement() &&
                HasSpawned(ArrayElement()) &&
                IsAlive(ArrayElement()) &&
                DistanceBetween(downPosition, ArrayElement()) <= reviveRange
            )
        )
    ) > 0;

rule: "Player/Revive/setTimeLimit"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    IsDead()
){
    // set revive time limit by player dead
    DisableRespawning();
    SetRespawnMaxTime(ep, Revive_timeLimit);
    reviveTimeRemain = Revive_timeLimit;
}

playervar define returnPosition!;

rule: "Player/Revive/saveReturnPosition"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    IsAlive() &&
    IsInAir()
){
    returnPosition = PositionOf();
    Wait(5, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

rule: "Player/Revive/returnGround"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    IsDead() &&
    !IsOnGround()
){
    if(IsDeadByAbyss()){
        Teleport(EventPlayer(), NearestWalkablePosition(returnPosition));
    }
    else{
        WaitUpdateMediumCycle();
        LoopIfConditionIsTrue();
    }
}

rule: "Player/Revive/showNeedHelp"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    CanRevive()
){
    // Display HELP on the downed player's position
    // As for the player position, the appearance position and the down position will not match the dead body where the ragdoll works
    // Therefore, save and use the position when down, not the position of the player
    // If down in the abyss of the map, can not revive, so do not display HELP
    if(!IsDeadByAbyss()){
        downPosition = PositionOf(EventPlayer());
        needHelpEntitys = [];
        
        CreateInWorldText(
            AllPlayers(TeamOf()), 
                <"Revive <0> <1> <2>", EventPlayer(), HeroIconString(HeroOf(EventPlayer())), reviveTimeRemain>,
            downPosition, 1, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
            Color.White, Spectators.VisibleAlways);

        needHelpEntitys[0] = LastTextID();
        CreateIcon(
            AllPlayers(TeamOf()), downPosition,
            Icon.Skull, IconRev.VisibleToAndPosition, Color.Red, true);
        needHelpEntitys[1] = LastCreatedEntity();
    }
}

rule: "Player/Revive/hideNeedHelp"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    !CanRevive()
){
    // Hide HELP on the downed player's position
    DestroyIcon(needHelpEntitys[1]);
    DestroyInWorldText(needHelpEntitys[0]);
}

rule: "Player/Revive/countDownTimeLimit"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    CanRevive() &&
    !NearTeammate() &&
    reviveTimeRemain > 0
){
    // Count down revive time limit on the downed player's position
    WaitUpdateMediumCycle();
    reviveTimeRemain -= 1;
    if(reviveTimeRemain <= 0){
        Wait(0.1, WaitBehavior.IgnoreCondition);
        Respawn();
    }
    else{
        if(!IsDeadByAbyss() && !NearTeammate()){
            PlayEffect(
                AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
                Color.White, EventPlayer(), 1);
        }
        LoopIfConditionIsTrue();
    }
}

globalvar define reviveTimeoutCount! = 0;

rule: "Player/Revive/timeLimitOut"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    CanRevive() &&
    !NearTeammate() &&
    reviveTimeRemain <= 0
){
    // Count down revive time limit on the downed player's position
    reviveTimeoutCount ++;
}

rule: "Player/Revive/reviveByTeammate //by near standing"
Event.OngoingPlayer
if(
    Revive_isEnableTeam &&
    CanRevive() &&
    NearTeammate()
){
    // revived when a teammate is nearby
    // The event player is the player who receives the revival.
    if(!IsDeadByAbyss()){
        // Save the reviver to display a message to the reviver
        define reviver! = ClosestPlayerTo(EventPlayer(), Revive_enableTeam);

        SmallMessage(reviver,
            <"reviving <0> <1>. Standby...", EventPlayer(), HeroIconString(HeroOf(EventPlayer()))>);
        SmallMessage(EventPlayer(),
            <"<0> <1> reviving you.", reviver, HeroIconString(HeroOf(reviver))>);

        // reviving effect and progress count
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
            Color.Yellow, EventPlayer(), 1);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.BuffImpactSound,
            Color.Yellow, EventPlayer(), 100);

        // skip this wait by timer too short
        Wait(1, WaitBehavior.AbortWhenFalse);   
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
            Color.Yellow, EventPlayer(), 1);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.BuffImpactSound,
            Color.Yellow, EventPlayer(), 100);

        // skip this wait by timer too short
        Wait(1, WaitBehavior.AbortWhenFalse);   
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
            Color.Yellow, EventPlayer(), 1);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.BuffImpactSound,
            Color.Yellow, EventPlayer(), 100);
        // skip this wait by timer too short
        Wait(1, WaitBehavior.AbortWhenFalse);   

        // revive effect and sound
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
            Color.Yellow, EventPlayer(), 1);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.RingExplosion,
            Color.Yellow, EventPlayer(), 8);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.BuffExplosionSound,
            Color.White, EventPlayer(), MaxLength());

        Resurrect(EventPlayer());

        SmallMessage(reviver,
            <"revived <0> <1>.", EventPlayer(), HeroIconString(HeroOf(EventPlayer()))>);
        SmallMessage(EventPlayer(),
            <"<0> <1> revivied you.", reviver, HeroIconString(HeroOf(reviver))>);
    }
}

rule: "Player/Revive/failsafe"
Event.OngoingPlayer
if(
    IsDead()
){
    Wait(Revive_timeLimit + 3, WaitBehavior.AbortWhenFalse);
    Respawn(ep);
}

// TAG Debug ----------
disabled rule: "Debug/forceReviveTeammate // for bot only"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    IsAlive() &&
    0 < NumberOfDeadPlayers(Team.Team1)
){
    define reviveTarget! =
        FirstOf(
            FilteredArray(
                AllPlayers(Team.Team1),
                (
                    IsDead(ArrayElement()) &&
                    IsInLineOfSight(EventPlayer(), ArrayElement())
                )
            )
        );

    if(
        reviveTarget != null
    ){
        ApplyImpulse(EventPlayer(), 
            VectorTowards(EventPlayer(), reviveTarget + PositionOffset()),
            DistanceBetween(EventPlayer(), reviveTarget) * 1.3,
            Relative.ToWorld, ContraryMotion.Cancel);
    }
    WaitApplyImpulseCycle();
    LoopIfConditionIsTrue();    
}
