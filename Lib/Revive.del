import "Utility.del";

// TAG public property
globalvar define reviveEnableTeams! = [Team.Team1, Team.Team2];
define isReviveEnableTeam: IsTrueForAny(reviveEnableTeams, TeamOf(ep) == ae);

globalvar define reviveTimeLimit! = 10;
globalvar define reviveRange! = 2.5;
globalvar define reviveCompleteSecond! = 5;

playervar define downPosition!;
playervar define reviveTimeRemain!;
playervar define needHelpEntitys!;

define CanRevive():
    IsDead() &&
    IsOnGround();

rule: "Player/Revive/setTimeLimit"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    IsDead()
){
    // set revive time limit by player dead
    reviveTimeRemain = reviveTimeLimit;
}

playervar define returnPosition!;

rule: "Player/Revive/saveReturnPosition"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    IsAlive() &&
    IsInAir()
){
    returnPosition = PositionOf();
    Wait(5, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

rule: "Player/Revive/returnGround"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    IsDead() &&
    !IsOnGround()
){
    WaitRandomForDistributeServerLoad();
    if(IsDeadByAbyss()){
        WaitRandomForDistributeServerLoad();
        Teleport(ep, NearestWalkablePosition(returnPosition));
        Abort();
    }
}

rule: "Player/Revive/showNeedHelp"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    CanRevive()
){
    // Display HELP on the downed player's position
    // As for the player position, the appearance position and the down position will not match the dead body where the ragdoll works
    // Therefore, save and use the position when down, not the position of the player
    // If down in the abyss of the map, can not revive, so do not display HELP
    downPosition = PositionOf(ep);
    if(!IsDeadByAbyss()){
        needHelpEntitys = [];
        needHelpEntitys[0] = CreateInWorldText(
            AllPlayers(TeamOf()),  <"Revive <0> <1> <2>", ep, HeroIconString(HeroOf(ep)), reviveTimeRemain>,
            downPosition, basicInWorldTextSize, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
            Color.White, Spectators.VisibleAlways);
        needHelpEntitys[1] = CreateIcon(
            AllPlayers(TeamOf()), downPosition,
            Icon.Skull, IconRev.VisibleToAndPosition, Color.Red, true);
        needHelpEntitys[2] = CreateEffect(
            AllPlayers(TeamOf()), Effect.Ring, Color.White,
            downPosition, reviveRange, EffectRev.VisibleToPositionAndRadius);
    }
}

rule: "Player/Revive/hideNeedHelp"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    !CanRevive()
){
    // Hide HELP on the downed player's position
    DestroyEffect(needHelpEntitys[2]);
    DestroyIcon(needHelpEntitys[1]);
    DestroyInWorldText(needHelpEntitys[0]);
}

define isReviving:
    isReviveEnableTeam &&
    CanRevive() &&
    CountOf(
        FilteredArray(
            AllLivingPlayers(TeamOf()),
            (
                ep != ae &&
                DistanceBetween(downPosition, ae) <= reviveRange
            )
        )
    ) > 0;

rule: "Player/Revive/countDownTimeLimit"
Event.OngoingPlayer
if(
    isReviveEnableTeam &&
    CanRevive() &&
    reviveTimeRemain >= 0
){
    // Count down revive time limit on the downed player's position
    WaitUpdateASecond();
    // countdown
    if(reviveTimeRemain > 0){
        reviveTimeRemain -= 1;
    }
    // check timeout 
    if(
        reviveTimeRemain <= 0
    ){
        WaitUntil(!isReviving, reviveCompleteSecond);
        AbortIf(IsAlive());
        Respawn();

        reviveTimeoutCount ++;
        Abort();
    }
    LoopIfConditionIsTrue();
}

globalvar define reviveTimeoutCount! = 0;

playervar define reviveProgressRingSize;

playervar define reviver!;

rule: "Player/Revive/reviveByTeammate"
Event.OngoingPlayer
if(
    isReviving
){
    // revived when a teammate is nearby
    // The event player is the player who receives the revival.

    // Save the reviver to display a message to the reviver
    reviver = FirstOf(
            SortedArray(
                AllLivingPlayers(TeamOf()),
                DistanceBetween(ep, ae)
            )
        );

    reviveProgressRingSize = reviveRange;
    needHelpEntitys[3] = CreateEffect(
        AllPlayers(TeamOf()), Effect.Ring, Color.Yellow,
        downPosition, reviveProgressRingSize, EffectRev.VisibleToPositionAndRadius);
    ChaseVariableOverTime(reviveProgressRingSize, 0, reviveCompleteSecond, TimeChaseReevaluation.None);

    SmallMessage(reviver,
        <"reviving <0> <1>.", ep, HeroIconString(HeroOf(ep))>);
    SmallMessage(ep,
        <"<0> <1> reviving you.", reviver, HeroIconString(HeroOf(reviver))>);

    // reviving effect and progress count
    for(define reviveProgressSecond! = 0; reviveProgressSecond < reviveCompleteSecond; reviveProgressSecond ++){
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
            Color.Yellow, downPosition, 1);
        PlayEffect(
            AllPlayers(Team.All), PlayEffect.BuffImpactSound,
            Color.Yellow, downPosition, MaxValue());
            
        define reviveCountDown! = reviveCompleteSecond - reviveProgressSecond;
        if(0 < reviveCountDown && reviveCountDown < reviveCompleteSecond){
            SmallMessage(ep, <"<0>", reviveCountDown>);
            SmallMessage(reviver, <"<0>", reviveCountDown>);
        }
        WaitUpdateASecond();
    }

    StopChasingVariable(reviveProgressRingSize);

    // revive
    revive(reviver, ep, reviveRange * 3);
}

rule: "Player/Revive/abortReviveByTeammate"
Event.OngoingPlayer
if(
    !isReviving
){
    StopChasingVariable(reviveProgressRingSize);
    DestroyEffect(needHelpEntitys[3]);

    if(IsDead()){
        SmallMessage(reviver, <"revive aborted.">);
        SmallMessage(ep, <"revive aborted.">);
    }
}

// TAG Debug ----------
