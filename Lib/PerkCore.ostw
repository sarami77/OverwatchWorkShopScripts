import "Utility.ostw";
import "Debug.ostw";

// Function enable team is Team1
playervar define perk = [];
enum Perk{
    perkCratePosition,
    perkCrateState,
    perkCrateEntitys,
    perkType,
    perkState,
    perkHudEntitys,
    playerClosestCrate,
    perkTimeLimit,
    originalHero,
    perkName,
    perkDetail,
    perkEffectAndText,
    perkEffectStatus,
    perkTargets,
    notPickupPerk,
    dropedPerkType,
    perkEffectCount,
    perkEffectCoolTime,
    firstAnimationDelay,
    keepMovingSecond,
    fireBallSpeed,
    fireBallPower,
    fireBallSize,
    fireBallBlastRange,
    fireBallTriggerDelay,
    thunderStrikePower,
    approachingAngle,
    isInAir,
    deployDragonNum,
    titanHealthPoolID,
    titanRider,
    ridingTitan
}

playervar define perkPosition;
playervar define perkPosition2;
playervar define landPosition;
playervar define landPosition2;
playervar define landZonePosition;
playervar define aimPosition;
playervar define aimPosition2;

enum PerkCrateState{
    NotDropped,
    Dropped
}

enum PerkType{  // TAG perk type
    Non,
    SuperLanding,
    GrapplingBeam,
    Sprinter,
    Adrenaline,
    HeroicAgility,
    Savior,
    Fearless,
    SteelFormation,
    PersonalShelter,
    BioticStation,
    TrapBeam,
    BattleFlag,
    ChainExplosion,
    IceAge,
    AimHack,
    Vampire,
    HotShot,
    ShotCaller,
    GrudgeFireBall,
    GrudgeThunderStrike,
    ArtilleryRequest,
    StrafingSupport,
    MissileBarrage,
    CruisingMissile,
    OrbitalLaser,
    CarePackage,
    DeathFromAbove,
    LegendalyNinja,
    MachDelivery,
    Juggernaut,
    BeamSniper,
    DragonStorm,
    Titan,
    Annihilator,
    End
}

enum PerkState{
    Non,
    Ready,
    Using
}

globalvar define lotteryPerkList;
globalvar define lotteryPerkListOrder;

void addLottelyPerk(define perk, define count)"addLottelyPerk"{
    for(define countIdx = 0; countIdx < count; countIdx++)
        lotteryPerkList[CountOf(lotteryPerkList)] = perk;
}

rule: "Perk/buildLotteryPerkList // list of parks you can pick up"
Event.OngoingGlobal
{
    lotteryPerkListOrder = 0;
    lotteryPerkList = [];     // TAG lottery perk list
    addLottelyPerk(PerkType.SuperLanding,           WorkshopSettingInteger("Perk Settings", "(Super Landing) Included Amount",              10, 0, 1000, 0));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.GrapplingBeam,          WorkshopSettingInteger("Perk Settings", "(Grappling Beam) Included Amount",             10, 0, 1000, 1));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Sprinter,               WorkshopSettingInteger("Perk Settings", "(Sprinter) Included Amount",                   10, 0, 1000, 2));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Adrenaline,             WorkshopSettingInteger("Perk Settings", "(Adrenaline) Included Amount",                 10, 0, 1000, 3));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.HeroicAgility,          WorkshopSettingInteger("Perk Settings", "(Heroic Agility) Included Amount",             10, 0, 1000, 4));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Savior,                 WorkshopSettingInteger("Perk Settings", "(Savior) Included Amount",                     10, 0, 1000, 5));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Fearless,               WorkshopSettingInteger("Perk Settings", "(Fearless) Included Amount",                   10, 0, 1000, 6));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.SteelFormation,         WorkshopSettingInteger("Perk Settings", "(Steel Formation) Included Amount",            10, 0, 1000, 7));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.PersonalShelter,        WorkshopSettingInteger("Perk Settings", "(Personal Shelter) Included Amount",           10, 0, 1000, 8));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.BioticStation,          WorkshopSettingInteger("Perk Settings", "(Biotic Station) Included Amount",             10, 0, 1000, 9));   lotteryPerkListOrder++;
    addLottelyPerk(PerkType.TrapBeam,               WorkshopSettingInteger("Perk Settings", "(Trap Beam) Included Amount",                  10, 0, 1000, 10));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.BattleFlag,             WorkshopSettingInteger("Perk Settings", "(Battle Flag) Included Amount",                10, 0, 1000, 11));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.ChainExplosion,         WorkshopSettingInteger("Perk Settings", "(Chain Explosion) Included Amount",            10, 0, 1000, 12));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.IceAge,                 WorkshopSettingInteger("Perk Settings", "(Ice Age) Included Amount",                    10, 0, 1000, 13));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.AimHack,                WorkshopSettingInteger("Perk Settings", "(Aim Hack) Included Amount",                   10, 0, 1000, 14));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Vampire,                WorkshopSettingInteger("Perk Settings", "(Vampire) Included Amount",                    10, 0, 1000, 15));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.HotShot,                WorkshopSettingInteger("Perk Settings", "(Hot Shot) Included Amount",                   10, 0, 1000, 16));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.ShotCaller,             WorkshopSettingInteger("Perk Settings", "(Shot Caller) Included Amount",                10, 0, 1000, 17));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.GrudgeFireBall,         WorkshopSettingInteger("Perk Settings", "(Grudge Fire Ball) Included Amount",           10, 0, 1000, 18));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.GrudgeThunderStrike,    WorkshopSettingInteger("Perk Settings", "(Grudge Thunder Strike) Included Amount",      10, 0, 1000, 19));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.StrafingSupport,        WorkshopSettingInteger("Perk Settings", "(Strafing Support) Included Amount",           10, 0, 1000, 20));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.MissileBarrage,         WorkshopSettingInteger("Perk Settings", "(Missile Barrage) Included Amount",            10, 0, 1000, 21));  lotteryPerkListOrder++;    
    addLottelyPerk(PerkType.ArtilleryRequest,       WorkshopSettingInteger("Perk Settings", "(Artillery Request) Included Amount",          10, 0, 1000, 22));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.CruisingMissile,        WorkshopSettingInteger("Perk Settings", "(Cruising Missile) Included Amount",           10, 0, 1000, 23));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.OrbitalLaser,           WorkshopSettingInteger("Perk Settings", "(Orbital Laser) Included Amount",              10, 0, 1000, 24));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.CarePackage,            WorkshopSettingInteger("Perk Settings", "(Care Package) Included Amount",               10, 0, 1000, 25));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.DeathFromAbove,         WorkshopSettingInteger("Perk Settings", "(Death From Above) Included Amount",           10, 0, 1000, 26));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.LegendalyNinja,         WorkshopSettingInteger("Perk Settings", "(Legendaly Ninja) Included Amount",            10, 0, 1000, 27));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.MachDelivery,           WorkshopSettingInteger("Perk Settings", "(Mach Delivery) Included Amount",              10, 0, 1000, 28));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Juggernaut,             WorkshopSettingInteger("Perk Settings", "(Juggernaut) Included Amount",                 10, 0, 1000, 29));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.BeamSniper,             WorkshopSettingInteger("Perk Settings", "(Beam Sniper) Included Amount",                10, 0, 1000, 30));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.DragonStorm,            WorkshopSettingInteger("Perk Settings", "(Dragon Storm) Included Amount",               10, 0, 1000, 31));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Titan,                  WorkshopSettingInteger("Perk Settings", "(Titan) Included Amount",                      10, 0, 1000, 32));  lotteryPerkListOrder++;
    addLottelyPerk(PerkType.Annihilator,            WorkshopSettingInteger("Perk Settings", "(Annihilator) Included Amount",                10, 0, 1000, 33));  lotteryPerkListOrder++;
}

rule: "Perk/CreatState/NotDropped"
Event.OngoingPlayer
//Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.NotDropped
){
    // Reset param
    perk[Perk.perkCratePosition] = 0;
    // Hide perk effect and text
    DestroyInWorldText(perk[Perk.perkCrateEntitys][0]);
    DestroyEffect(perk[Perk.perkCrateEntitys][1]);
}

rule: "Perk/CreatState/Dropped"
Event.OngoingPlayer
//Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.Dropped
){
    // show perk effect and text
    CreateInWorldText(
        AllPlayers(), "Perk Crate", 
        perk[Perk.perkCratePosition], 1, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);
    perk[Perk.perkCrateEntitys][0] = LastTextID();
    CreateEffect(AllPlayers(), Effect.Orb, Color.White,
        perk[Perk.perkCratePosition], 1, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkCrateEntitys][1] = LastCreatedEntity();
}

rule: "Perk/Enemy/dropPerkCrate // drop by some probability on death"
Event.OnDeath
Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.NotDropped &&
    Attacker() != EventPlayer()
){
    // The more people who do not have a park, the higher the drop rate
    if(
        RandomReal(0, 100)
            <=
        ( 
            WorkshopSettingReal("Perk Settings", "Perk Crate Drop Rate When Enemy Kill", 8.0, 0, 100, -1)		// TAG perk drop rate
        )
    ){
        // Set perk crate position from killed enemry position
        perk[Perk.perkCrateState] = PerkCrateState.NotDropped;
        WaitChangeState();
        perk[Perk.perkCratePosition] = BelowPosition(PositionOf()) + PositionOffset();
        perk[Perk.perkCrateState] = PerkCrateState.Dropped;
    }
}

rule: "Perk/Enemy/clearPerkCrate //by 30second timer"
Event.OngoingPlayer
//Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.Dropped
){
    // delete perk crate after a some time
    Wait(30, WaitBehavior.AbortWhenFalse);  // wait clear perk
    perk[Perk.perkCrateState] = PerkCrateState.NotDropped;
}

define pickupRange : 2.5;
define canPickupCratePlayers:
    FilteredArray(
        AllPlayers(Team.Team1),
        (
            IsAlive(ArrayElement()) &&
            ArrayElement().perk[Perk.perkState] == PerkState.Non &&
            ArrayElement().perk[Perk.perkType] == PerkType.Non &&
            DistanceBetween(perk[Perk.perkCratePosition], ArrayElement()) <= pickupRange &&
            !(ArrayElement().perk[Perk.notPickupPerk] && ArrayElement() == EventPlayer())
        )
    );

rule: "Perk/Enemy/perkPickUpByPlayer //Giving a park to the player who touched the crate"
Event.OngoingPlayer
//Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.Dropped &&
    CountOf(canPickupCratePlayers) > 0
){
    // When a player approaches the position where the perk crate drops,
    //  give the player a random perk
    WaitConditionConfirmed();
    perk[Perk.playerClosestCrate] = FirstOf(canPickupCratePlayers);
    // play pickup effect, message
    PlayEffect(
        AllPlayers(), PlayEffect.GoodPickupEffect,
        Color.White, perk[Perk.playerClosestCrate], 1);
    PlayEffect(
        perk[Perk.playerClosestCrate], PlayEffect.BuffExplosionSound,
        Color.White, perk[Perk.playerClosestCrate], MaxLength());
    SmallMessage(perk[Perk.playerClosestCrate], "Got perk. Check detail from left list.");

    // set perk
    if(perk[Perk.dropedPerkType] != PerkType.Non){
        perk[Perk.playerClosestCrate].perk[Perk.perkType] = perk[Perk.dropedPerkType];
        perk[Perk.dropedPerkType] = PerkType.Non;
    }
    else{
        perk[Perk.playerClosestCrate].perk[Perk.perkType] = RandomValueInArray(lotteryPerkList);
    }
    perk[Perk.playerClosestCrate].perk[Perk.perkState] = PerkState.Ready;
    // delete perk crate when picked up
    perk[Perk.perkCrateState] = PerkCrateState.NotDropped;
}

rule: "Perk/Enemy/alreadykPickUpByPlayer"
Event.OngoingPlayer
//Team.Team2
Player.All
if(
    perk[Perk.perkCrateState] == PerkCrateState.Dropped &&
    CountOf(
        FilteredArray(
            AllPlayers(Team.Team1),
            (
                TeamOf(ArrayElement()) == Team.Team1 &&
                IsAlive(ArrayElement()) &&
                ArrayElement().perk[Perk.perkState] != PerkState.Non &&
                DistanceBetween(perk[Perk.perkCratePosition], ArrayElement()) <= pickupRange
            )
        )
    ) > 0
){
    WaitConditionConfirmed();
    perk[Perk.playerClosestCrate] = 
        FirstOf(
            FilteredArray(
                AllPlayers(Team.Team1),
                (
                    TeamOf(ArrayElement()) == Team.Team1 &&
                    IsAlive(ArrayElement()) &&
                    ArrayElement().perk[Perk.perkState] != PerkState.Non &&
                    DistanceBetween(perk[Perk.perkCratePosition], ArrayElement()) <= pickupRange
                )
            )
        );

    // If player already have perk
    SmallMessage(perk[Perk.playerClosestCrate], "Already have perk.");
    SmallMessage(perk[Perk.playerClosestCrate], <"First, double press interact button <0> to cancel current perk.", ButtonGuide(Button.Interact)> );
}

rule: "Perk/Player/ParkState/Non"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkType] == PerkType.Non &&
    perk[Perk.perkState] == PerkState.Non
){
    // Hide perk hud
    HidePerkHUD();
}

rule: "Perk/Player/ParkState/Ready"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkType] != PerkType.Non &&
    perk[Perk.perkState] == PerkState.Ready
){
    // Update perk hud
    HidePerkHUD();
    ShowPerkHUD();
}

rule: "Perk/Player/ParkState/Using"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkType] != PerkType.Non &&
    perk[Perk.perkState] == PerkState.Using
){
    // Update perk hud
    HidePerkHUD();
    ShowPerkHUD();
}

// TAG Common Property ----------
class PerkHero{
    public define hero = null;
    public define statusUnkillable = false;
    public define statusInvincible = false;
    public define primaryFireEnabled = true;
    public define secondaryFireEnabled = true;
    public define ability1Enabled = true;
    public define ability2Enabled = true;
    public define maxHealth = 100;
    public define healingReceived = 100;
    public define damageDealt = 100;
    public define gravity = 100;
    public define moveSpeed = 100;
    public define jumpSpeed = 100;
    public define projectileSpeed = 100;
    public define ultimateCharge = 0;
}

void ForcePlayerPerkHero(
    define perkTarget,
    PerkHero perkHero
)"ForcePlayerPerkHero"{
    // save now hero for reset
    perkTarget.perk[Perk.originalHero] = HeroOf(perkTarget);
    ForcePlayerHero(EventPlayer(), perkHero.hero);
    
    // set primaryFireEnabled
    SetPrimaryFireEnabled(perkTarget, perkHero.primaryFireEnabled);
    // set secondaryFireEnabled
    SetSecondaryFireEnabled(perkTarget, perkHero.secondaryFireEnabled);
    // set ability1Enabled
    SetAbility1Enabled(perkTarget, perkHero.ability1Enabled);
    // set ability2Enabled
    SetAbility2Enabled(perkTarget, perkHero.ability2Enabled);
    
    // set damageDealt
    SetDamageDealt(perkTarget, perkHero.damageDealt);
    // set gravity
    SetGravity(perkTarget, perkHero.gravity);
    // set moveSpeed
    SetMoveSpeed(perkTarget, perkHero.moveSpeed);
    // set jumpSpeed
    SetJumpVerticalSpeed(perkTarget, perkHero.jumpSpeed);
    // set projectileSpeed
    SetProjectileSpeed(perkTarget, perkHero.projectileSpeed);

    // set maxHealth. 
    SetMaxHealth(perkTarget, perkHero.maxHealth);
    Wait(0.25, WaitBehavior.IgnoreCondition); // wait change MaxHealth
    // heal to maxHealth. Heal before set healingReceived
    Heal(perkTarget, null, MaxValue());
    Wait(0.25, WaitBehavior.IgnoreCondition); // wait heal
    // set healingReceived
    SetHealingReceived(perkTarget, perkHero.healingReceived);
    // set ultimateCharge
    SetUltimateCharge(perkTarget, perkHero.ultimateCharge);

    // set status Unkillable
    if(perkHero.statusUnkillable) SetStatus(perkTarget, null, Status.Unkillable, MaxValue());
    // set status Invincible
    if(perkHero.statusInvincible) SetStatus(perkTarget, null, Status.Invincible, MaxValue());
}

void StopForcingPerkHero(define perkTarget)"StopForcingPerkHero"{
    // reset hero
    ForcePlayerHero(perkTarget, perkTarget.perk[Perk.originalHero]);
    StopForcingHero(perkTarget);

    // reset primaryFireEnabled
    SetPrimaryFireEnabled(perkTarget, true);
    // reset secondaryFireEnabled
    SetSecondaryFireEnabled(perkTarget, true);
    // reset ability1Enabled
    SetAbility1Enabled(perkTarget, true);
    // reset ability2Enabled
    SetAbility2Enabled(perkTarget, true);

    // reset damageDealt
    SetDamageDealt(perkTarget, DefaultPercentage());
    // reset gravity
    SetGravity(perkTarget, DefaultPercentage());
    // reset moveSpeed
    SetMoveSpeed(perkTarget, DefaultPercentage());
    // set jumpSpeed
    SetJumpVerticalSpeed(perkTarget, DefaultPercentage());
    // reset projectileSpeed
    SetProjectileSpeed(perkTarget, DefaultPercentage());
    // reset ultimateCharge
    SetUltimateCharge(perkTarget, 0);

    // reset healingReceived
    SetHealingReceived(perkTarget, DefaultPercentage());
    // reset maxHealth
    SetMaxHealth(perkTarget, DefaultPercentage());
    Wait(0.25, WaitBehavior.IgnoreCondition); // wait change MaxHealth
    // heal to maxHealth. Heal after reset healingReceived
    Heal(perkTarget, null, MaxValue());
    Wait(0.25, WaitBehavior.IgnoreCondition); // wait heal

    // reset statusUnkillble
    ClearStatus(perkTarget, Status.Unkillable);
    // reset statusInvincible
    ClearStatus(perkTarget, Status.Invincible);
}

enum PerkEffectStatus{
    Ready,
    Triggered,
    SecondTriggered,
    Reloading,
    Disabled
}

// show perk name and player name
define CreatePerkTitleHud(define title):
    CreateHudText(AllPlayers(),
        <"<0> <1> <2>", EventPlayer(), HeroIconString(HeroOf()), title>, null, null,
        Location.Left, (SlotOf() * 10) + 0,
        Color.Team1, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

// show perk detail
define CreateperkDetailHud(define detail):
    CreateHudText(AllPlayers(),
        null, <" <0>", detail>, null,
        Location.Left, (SlotOf() * 10) + 1,
        Color.Team1, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

// show perk status and active time limit
define CreatePerkStatusHud():
    CreateHudText(AllPlayers(),
        null, null, 
        perk[Perk.perkState] == PerkState.Ready ? 
            <"  perk ready."> :
            <"  perk active until <0> seconds.", perk[Perk.perkTimeLimit]>,
        Location.Left, (SlotOf() * 10) + 2,
        Color.White, Color.White,
        perk[Perk.perkState] == PerkState.Ready ?
            Color.Yellow : Color.Orange,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

// show perk operation
define CreatePerkOperationHud():
    CreateHudText(EventPlayer(),
        null, null, 
        perk[Perk.perkState] == PerkState.Ready ?
            <"  Hold interact button <0> to activate.\n\r  Double press interact button <0> to drop as crate.", ButtonGuide(Button.Interact)> :
            <"  Double press interact button <0> to cancel.", ButtonGuide(Button.Interact)>,
        Location.Left, (SlotOf() * 10) + 3,
        Color.White, Color.White, 
        perk[Perk.perkState] == PerkState.Ready ?
            Color.Yellow : Color.Orange,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

// show perk effect status on top
define CreatePerkEffectHud(define text):
    CreateHudText(EventPlayer(),
        null, null, text,
        Location.Top, 4,
        Color.White, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

// show active perk name on player head up
define CreateHeadUpText(define text):
    CreateInWorldText(
        AllPlayers(),
        text,
        EventPlayer(), 1, Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);

// show each perk hud
void ShowPerkHUD()"ShowPerkHUD"{
    // TAG perk detail text HUD
    if(perk[Perk.perkType] == PerkType.SuperLanding){
        perk[Perk.perkName] = "Super Landing";
        perk[Perk.perkDetail] = "Jump more higher and landing attack with impact. Can also hovering in air a little.";
    }
    else if(perk[Perk.perkType] == PerkType.GrapplingBeam){
        perk[Perk.perkName] = "Grappling Beam";
        perk[Perk.perkDetail] = "Shot grappling beam and move quickly in air. Can also attract enemies until close range.";
    }
    else if(perk[Perk.perkType] == PerkType.Sprinter){
        perk[Perk.perkName] = "Sprinter";
        perk[Perk.perkDetail] = "Move faster when keep moving. Reset by stop or received damage.";
    }
    else if(perk[Perk.perkType] == PerkType.Adrenaline){
        perk[Perk.perkName] = "Adrenaline";
        perk[Perk.perkDetail] = "Move faster when low health. Ideal for avoiding danger.";
    }
    else if(perk[Perk.perkType] == PerkType.HeroicAgility){
        perk[Perk.perkName] = "Heroic Agility";
        perk[Perk.perkDetail] = "Movement speed and jump length are enhanced. Fight with agility.";
    }
    else if(perk[Perk.perkType] == PerkType.Savior){
        perk[Perk.perkName] = "Savior";
        perk[Perk.perkDetail] = "Instantly revives teammate. Stun enemy when attacked enemy near low health teammates.";
    }
    else if(perk[Perk.perkType] == PerkType.Fearless){
        perk[Perk.perkName] = "Fearless";
        perk[Perk.perkDetail] = "When surrounded by enemies, damage dealt and healing received continues to increase.";
    }
    else if(perk[Perk.perkType] == PerkType.SteelFormation){
        perk[Perk.perkName] = "Steel Formation";
        perk[Perk.perkDetail] = "Reduce half received damage of near teammate and you. Let's unite.";
    }
    else if(perk[Perk.perkType] == PerkType.PersonalShelter){
        perk[Perk.perkName] = "Personal Shelter";
        perk[Perk.perkDetail] = "Place safe shelter. But for personal use only. Recommend for sniper or support hero.";
    }
    else if(perk[Perk.perkType] == PerkType.BioticStation){
        perk[Perk.perkName] = "Biotic Station";
        perk[Perk.perkDetail] = "Place powerful healing field. But effective time is short.";
    }
    else if(perk[Perk.perkType] == PerkType.TrapBeam){
        perk[Perk.perkName] = "Trap Beam";
        perk[Perk.perkDetail] = "Place beam to between own position and aiming point. Beam stuns touching enemies.";
    }
    else if(perk[Perk.perkType] == PerkType.BattleFlag){
        perk[Perk.perkName] = "Battle Flag";
        perk[Perk.perkDetail] = "Teammate can teleport at you from spawn room. Promote ultimate charge of near teammates and you.";
    }
    else if(perk[Perk.perkType] == PerkType.ChainExplosion){
        perk[Perk.perkName] = "Chain Explosion";
        perk[Perk.perkDetail] = "Killed enemy explodes and splash addtinal damage to around. Explosion is chain around.";
    }
    else if(perk[Perk.perkType] == PerkType.IceAge){
        perk[Perk.perkName] = "Ice Age";
        perk[Perk.perkDetail] = "killed enemy explode and splash freeze effect to around. Freeze time is short but range is wide.";
    }
    else if(perk[Perk.perkType] == PerkType.AimHack){
        perk[Perk.perkName] = "Aim Hack";
        perk[Perk.perkDetail] = "Automatically aim at enemy's head. No problem, bot can't report.";
    }
    else if(perk[Perk.perkType] == PerkType.Vampire){
        perk[Perk.perkName] = "Vampire";
        perk[Perk.perkDetail] = "When deal damage to enemy, absorbs half of dealt damage as own health.";
    }
    else if(perk[Perk.perkType] == PerkType.HotShot){
        perk[Perk.perkName] = "Hot Shot";
        perk[Perk.perkDetail] = "When kill enemy, ammo instantly reloaded and ability instantly recharged. barrier resource excluded.";
    }
    else if(perk[Perk.perkType] == PerkType.ShotCaller){
        perk[Perk.perkName] = "Shot Caller";
        perk[Perk.perkDetail] = "When use ultimate ability, teammate's ultimate ability instantly full charged.";
    }
    else if(perk[Perk.perkType] == PerkType.GrudgeFireBall){
        perk[Perk.perkName] = "Grudge Fire Ball";
        perk[Perk.perkDetail] = "Charge power by received damage, shot power as fire ball. More power make more damage and blast range.";
    }
    else if(perk[Perk.perkType] == PerkType.GrudgeThunderStrike){
        perk[Perk.perkName] = "Grudge Thunder Strike";
        perk[Perk.perkDetail] = "Charge power by received damage, shot power as thunder strike. More power make more strike count.";
    }
    else if(perk[Perk.perkType] == PerkType.ArtilleryRequest){
        perk[Perk.perkName] = "Artillery Request";
        perk[Perk.perkDetail] = "Request artillery fire to aim point. Attack wide area with 10-second shell barrage.";
    }
    else if(perk[Perk.perkType] == PerkType.StrafingSupport){
        perk[Perk.perkName] = "Strafing Support";
        perk[Perk.perkDetail] = "Request strafing of attack jet at aim point. Can be reused after cool down.";
    }
    else if(perk[Perk.perkType] == PerkType.MissileBarrage){
        perk[Perk.perkName] = "Missile Barrage";
        perk[Perk.perkDetail] = "Request guided missiles barrage at aim point. Can be reused after cool down.";
    }
    else if(perk[Perk.perkType] == PerkType.CruisingMissile){
        perk[Perk.perkName] = "Cruising Missile";
        perk[Perk.perkDetail] = "Big missile that guiding to aim point. Destroy enemies of wide area.";
    }
    else if(perk[Perk.perkType] == PerkType.OrbitalLaser){
        perk[Perk.perkName] = "Orbital Laser";
        perk[Perk.perkDetail] = "Laser irradiation from orbital satellite. Can control irradiation point for 60 seconds.";
    }
    else if(perk[Perk.perkType] == PerkType.CarePackage){
        perk[Perk.perkName] = "Care Package";
        perk[Perk.perkDetail] = "Request drop of support supplies. Can get multiple perk crates.";
    }
    else if(perk[Perk.perkType] == PerkType.DeathFromAbove){
        perk[Perk.perkName] = "Death From Above";
        perk[Perk.perkDetail] = "Raining cannon shells and bullets from gunship. Turn the earth into hell!";
    }
    else if(perk[Perk.perkType] == PerkType.LegendalyNinja){
        perk[Perk.perkName] = "Legendaly Ninja";
        perk[Perk.perkDetail] = "Ninja with super agility, dragon blade, immortality. Can add activity time by kill enemy.";
    }
    else if(perk[Perk.perkType] == PerkType.MachDelivery){
        perk[Perk.perkName] = "Mach Delivery";
        perk[Perk.perkDetail] = "When kill or deal critical damage by pulse bomb, pulse bomb instantly full charged.";
    }
    else if(perk[Perk.perkType] == PerkType.Juggernaut){
        perk[Perk.perkName] = "Juggernaut";
        perk[Perk.perkDetail] = "Heavy mechanical infantry with plasma auto canon and powerful gravity bomb.";
    }
    else if(perk[Perk.perkType] == PerkType.BeamSniper){
        perk[Perk.perkName] = "Beam Sniper";
        perk[Perk.perkDetail] = "Sniper with powerful beam rifle. Penetrate wall and enemy body when max charge shot.";
    }
    else if(perk[Perk.perkType] == PerkType.DragonStorm){
        perk[Perk.perkName] = "Dragon Storm";
        perk[Perk.perkDetail] = "Many dragons fall from sky, massive damage enemy of wide area. Can add activity time by kill enemy.";
    }
    else if(perk[Perk.perkType] == PerkType.Titan){
        perk[Perk.perkName] = "Titan";
        perk[Perk.perkDetail] = "Giant with big barrier, big hammer, super toughness. Protect team and rampage!";
    }
    else if(perk[Perk.perkType] == PerkType.Annihilator){
        perk[Perk.perkName] = "Annihilator";
        perk[Perk.perkDetail] = "Execute in sight enemies one after another by super-fast attack like lightning bolt.";
    }

    // TAG perk common action
    perk[Perk.perkHudEntitys] = [];
    WaitRedraw();
    // show title hud
    CreatePerkTitleHud(perk[Perk.perkName]);
    perk[Perk.perkHudEntitys][0] = LastTextID();
    WaitRedraw();
    // show detail hud
    CreateperkDetailHud(perk[Perk.perkDetail]);
    perk[Perk.perkHudEntitys][1] = LastTextID();
    WaitRedraw();
    // show status hud
    CreatePerkStatusHud();
    perk[Perk.perkHudEntitys][2] = LastTextID();
    WaitRedraw();
    // show operation hud
    CreatePerkOperationHud();
    perk[Perk.perkHudEntitys][3] = LastTextID();
    WaitRedraw();
    // show head up text
    if(perk[Perk.perkState] == PerkState.Using){
        CreateHeadUpText(perk[Perk.perkName]);
        perk[Perk.perkHudEntitys][4] = LastTextID();
        WaitRedraw();
    }
    // show Aura effct
    if(perk[Perk.perkState] == PerkState.Using){
        CreateEffect(AllPlayers(), Effect.BadAura, Color.White,
            EventPlayer(), 1, EffectRev.VisibleToPositionAndRadius);
        perk[Perk.perkHudEntitys][5] = LastCreatedEntity();
        WaitRedraw();
    }
}

void HidePerkHUD()"HidePerkHUD"{
    DestroyHudText(perk[Perk.perkHudEntitys][0]);
    DestroyHudText(perk[Perk.perkHudEntitys][1]);
    DestroyHudText(perk[Perk.perkHudEntitys][2]);
    DestroyHudText(perk[Perk.perkHudEntitys][3]);
    DestroyInWorldText(perk[Perk.perkHudEntitys][4]);
    DestroyEffect(perk[Perk.perkHudEntitys][5]);
}

rule: "Perk/Player/usePerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    IsAlive() &&
    perk[Perk.perkState] == PerkState.Ready &&
    IsButtonHeld(EventPlayer(), Button.Interact)
){
    WaitLongPressConfirm();
    PlayEffect(
        AllPlayers(), PlayEffect.GoodPickupEffect,
        Color.White, EventPlayer(), 1);
    PlayEffect(
        EventPlayer(), PlayEffect.BuffExplosionSound,
        Color.White, EventPlayer(), MaxLength());
    perk[Perk.perkState] = PerkState.Using;
}

rule: "Perk/Player/dropPerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    IsAlive() &&
    perk[Perk.perkState] == PerkState.Ready &&
    intaractDoublePressed
){
    WaitConditionConfirmed();
    perk[Perk.notPickupPerk] = true;
    perk[Perk.dropedPerkType] = perk[Perk.perkType];
    perk[Perk.perkCratePosition] = BelowPosition(PositionOf()) + PositionOffset();
    perk[Perk.perkCrateState] = PerkCrateState.Dropped;
    perk[Perk.perkState] = PerkState.Non;
}

rule: "Perk/Player/releaseNotPickupPerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.notPickupPerk]
){
    Wait(2, WaitBehavior.AbortWhenFalse);
    perk[Perk.notPickupPerk] = false;
}

rule: "Perk/Player/countDownTimeLimit"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkState] == PerkState.Using &&
    perk[Perk.perkTimeLimit] > 0
){
    // count down perk active limit time
    WaitUpdateMediumCycle();
    perk[Perk.perkTimeLimit] -= 1;
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/timeLimitOut"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkState] == PerkState.Using &&
    perk[Perk.perkTimeLimit] <= 0
){
    // lose perk by active limit time out
    WaitConditionConfirmed();
    perk[Perk.perkState] = PerkState.Non;
}

rule: "Perk/Player/cancelPerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    IsAlive() &&
    perk[Perk.perkState] == PerkState.Using &&
    intaractDoublePressed
){
    // lose perk by cancel operation of interact key hold
    WaitConditionConfirmed();
    PlayEffect(
        EventPlayer(), PlayEffect.DebuffImpactSound,
        Color.White, EventPlayer(), MaxLength());
    perk[Perk.perkState] = PerkState.Non;
}

rule: "Perk/Player/remindPerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkState] == PerkState.Ready
){
    // remind use perk.
    Wait(30, WaitBehavior.AbortWhenFalse);
    SmallMessage(EventPlayer(), <"Forgotten perk?"> );
    SmallMessage(EventPlayer(), <"Hold interact button <0> to activate perk.", ButtonGuide(Button.Interact)> );
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/losePerk //by dead"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    IsDead() &&
    perk[Perk.perkState] == PerkState.Using
){
    // lose perk by player dead
    SmallMessage(EventPlayer(), "Lost active perk due to death.");
    perk[Perk.perkState] = PerkState.Non;
}

// TAG Debug ----------
define enableDebugPerk: false;  // TAG enable Debug Perk

rule: "Debug/showDebugPerkHUD"
Event.OngoingGlobal
if(
    enableDebugPerk
){
    CreateHudText(
        HostPlayer(),
        null,
        null,
        <"enable Debug Perk. Relpeat press interact button <0> to select perk.", ButtonGuide(Button.Interact)>,
        Location.Left, -1,
        Color.White, Color.White, Color.White,
        HudTextRev.VisibleToAndString, Spectators.VisibleNever);
}

globalvar define backupPerkPerkState;
globalvar define backupPerkPerkType;

rule: "Debug/choicePerk"
Event.OngoingPlayer
Team.All
Player.All
if(
    enableDebugPerk &&
    HostPlayer() == EventPlayer() &&
    IsButtonHeld(EventPlayer(), Button.Interact)
){
    WaitShortPressConfirm();
    AbortIf(IsButtonHeld(EventPlayer(), Button.Interact));
    AbortIf(perk[Perk.perkState] == PerkState.Using);

    backupPerkPerkState = perk[Perk.perkState];
    perk[Perk.perkState] = PerkState.Non;
    WaitUntil(perk[Perk.perkType] == PerkType.Non, 1);

    if(backupPerkPerkState == PerkState.Non){
        if(backupPerkPerkType == PerkType.Non){
            backupPerkPerkType ++;
        }
        perk[Perk.perkType] = backupPerkPerkType;
        perk[Perk.perkState] = PerkState.Ready;  
    }
    else if(backupPerkPerkState == PerkState.Ready){
        if(backupPerkPerkType < PerkType.End){
            perk[Perk.perkType] = backupPerkPerkType + 1;
        }
        else{
            perk[Perk.perkType] = 1;
        }
        backupPerkPerkType = perk[Perk.perkType];
        perk[Perk.perkState] = PerkState.Ready;  
    }
    backupPerkPerkState = perk[Perk.perkState];
}

rule: "Debug/givePerkRandomToSelf"
Event.OngoingPlayer
Team.All
Player.All
if(
    HostPlayer() == EventPlayer() &&
    IsButtonHeld(EventPlayer(), Button.Interact) &&
    IsButtonHeld(EventPlayer(), Button.PrimaryFire)
){
    WaitLongPressConfirm();

    // Give perk to own player
    perk[Perk.perkState] = PerkState.Non;
    WaitUntil(perk[Perk.perkType] == PerkType.Non, 1);
    perk[Perk.perkType] = RandomValueInArray(lotteryPerkList);
    perk[Perk.perkState] = PerkState.Ready;   

    // Use perk by force
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    perk[Perk.perkState] = PerkState.Using;       
}

rule: "Debug/givePerkRandomToAll"
Event.OngoingPlayer
Team.All
Player.All
if(
    HostPlayer() == EventPlayer() &&
    IsButtonHeld(EventPlayer(), Button.Interact) &&
    IsButtonHeld(EventPlayer(), Button.SecondaryFire)
){
    WaitLongPressConfirm();

    // Give perk to all player
    AllPlayers(Team.Team1).perk[Perk.perkState] = PerkState.Non;   
    WaitUntil(AllPlayers(Team.Team1).perk[Perk.perkType] == PerkType.Non, 1);
    AllPlayers(Team.Team1).perk[Perk.perkType] = RandomValueInArray(lotteryPerkList);
    AllPlayers(Team.Team1).perk[Perk.perkState] = PerkState.Ready;

    // Use perk by force    
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    AllPlayers(Team.Team1).perk[Perk.perkState] = PerkState.Using;
}

define isInCombat: 
    IsAlive() &&
    IsInLineOfSight(EventPlayer(), ClosestPlayerTo(EventPlayer(), Team.Team2));

disabled rule: "Debug/forceGetPerk // for bot only"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    isInCombat &&
    perk[Perk.perkState] == PerkState.Non
){
    Wait(RandomInteger(1, 5), WaitBehavior.AbortWhenFalse);

    WaitUntil(AllPlayers(Team.Team1).perk[Perk.perkType] == PerkType.Non, 1);
    EventPlayer().perk[Perk.perkType] = RandomValueInArray(lotteryPerkList);
    EventPlayer().perk[Perk.perkState] = PerkState.Ready;
}

disabled rule: "Debug/forceUsePerk // for bot only"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    isInCombat &&
    perk[Perk.perkState] == PerkState.Ready
){
    Wait(RandomInteger(1, 5), WaitBehavior.AbortWhenFalse);

    StartHoldingButton(EventPlayer(), Button.Interact);
    Wait(0.5, WaitBehavior.IgnoreCondition);
    StopHoldingButton(EventPlayer(), Button.Interact);
}

disabled rule: "Debug/forceUseMelee // for bot only"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    isInCombat &&
    perk[Perk.perkState] == PerkState.Using
){
    Wait(RandomInteger(1, 5), WaitBehavior.AbortWhenFalse);

    StartHoldingButton(EventPlayer(), Button.Melee);
    Wait(0.5, WaitBehavior.IgnoreCondition);
    StopHoldingButton(EventPlayer(), Button.Melee);  
}

disabled rule: "Debug/overridePerk"
Event.OngoingPlayer
Team.Team1
Player.All
if(
    perk[Perk.perkState] == PerkState.Using
){
    perk[Perk.perkType] = PerkType.StrafingSupport;
}