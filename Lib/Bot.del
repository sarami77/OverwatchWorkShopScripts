import "Utility.del";
import "Debug.del";

// TAG duumy driver ----------

// TAG Bot/Spawn ----------
enum BotAsset{
    EnemyType,
    hero,
    count
}

// TAG EnemyType ----------
playervar define enemyType;
enum EnemyType{
    None,
    Minion,
    Elite,
    Boss,
    GiantBoss
}

// TAG BotAsset ----------
define RandomHero: RandomValueInArray(AllHeroes());
define RandomTankHero: RandomValueInArray(AllTankHeroes());
define RandomDamageHero: RandomValueInArray(AllDamageHeroes());
define RandomSupportHero: RandomValueInArray(AllSupportHeroes());

globalvar define botSlot;

void CreateAllBotByAsset(define assetList)"CreateAllBotByAsset"{
    DestroyAllBot();
    Wait(1, WaitBehavior.IgnoreCondition);
    // create bot by asset
    define asset = ValueInArray(assetList, RandomInteger(0, CountOf(assetList) - 1));
    for(define idx = 0; idx < CountOf(asset); idx++){
        for(define botCount = 0; botCount < asset[idx][BotAsset.count]; botCount++){
            // create bot
            CreateDummyBot(
                asset[idx][BotAsset.hero],
                Team.Team2,
                botSlot,
                SpawnPoints(Team.Team2)[0],
                RandomDirection()
            );
            Wait(0.1, WaitBehavior.IgnoreCondition);
            WaitUntil(!isServerOverLoading, 3);          

            // set Bot type as enemy type
            define spawnBot = PlayersInSlot(botSlot, Team.Team2);
            spawnBot.enemyType = asset[idx][BotAsset.EnemyType];
            Respawn(spawnBot);
            // next slot
            botSlot ++;
        }
    }
}

void DestroyAllBot()"DestroyAllBot"{
    if(0 < NumberOfPlayers(Team.Team2)){
        for(define idx = 0; idx < 24; idx ++){
            define spawnBot = PlayersInSlot(idx, Team.Team2);
            spawnBot.enemyType = EnemyType.None;
            DestroyDummyBot(
                Team.Team2, idx);
        }
        botSlot = 0;
    }
}


void DebugCreateBot(define heroList, define num)"DebugCreateAllBot"{
    if(0 < NumberOfPlayers(Team.All)){
        for(define idx = 0; idx < 24; idx ++){
            define spawnBot = PlayersInSlot(idx, Team.Team2);
            spawnBot.enemyType = EnemyType.None;
            DestroyDummyBot(
                Team.Team1, idx);
        }
        for(define idx = 0; idx < 24; idx ++){
            define spawnBot = PlayersInSlot(idx, Team.Team2);
            spawnBot.enemyType = EnemyType.None;
            DestroyDummyBot(
                Team.Team2, idx);
        }
        botSlot = 0;
    }
    Wait(0.1, WaitBehavior.IgnoreCondition);

    // create bot by asset
    for(define idx = 0; idx < num; idx++){
        define team = idx % 2 == 1 ? Team.Team1 : Team.Team2;
        while(!HasSpawned(PlayersInSlot(botSlot, team))){
            // create bot
            CreateDummyBot(
                RandomValueInArray(heroList),
                team,
                botSlot,
                SpawnPoints(team)[0],
                RandomDirection()
            );
            Wait(0.1, WaitBehavior.IgnoreCondition);
        }
        // next slot
        botSlot ++;
    }
}

// TAG Bot/SetBotInfo ----------
playervar define actionState!;
playervar define statusText!;
playervar define target!;
playervar define targetPos!;
playervar define moveDir!;
playervar define sideStepDir!;
playervar define sideStepSecond!;
playervar define heroName!;
playervar define combatRange!;
playervar define combatRangeMin!;
playervar define combatRangeMax!;
playervar define primaryFireButtonBehaivor!;
playervar define primaryFireButtonReleaseMin!;
playervar define primaryFireButtonReleaseMax!;
playervar define primaryFireButtonHoldMin!;
playervar define primaryFireButtonHoldMax!;
playervar define secondaryFireButtonBehaivor!;
playervar define secondaryFireButtonReleaseMin!;
playervar define secondaryFireButtonReleaseMax!;
playervar define secondaryFireButtonHoldMin!;
playervar define secondaryFireButtonHoldMax!;
playervar define ability1ButtonReleaseMin!;
playervar define ability1ButtonReleaseMax!;
playervar define ability1ButtonHoldMin!;
playervar define ability1ButtonHoldMax!;
playervar define ability2ButtonReleaseMin!;
playervar define ability2ButtonReleaseMax!;
playervar define ability2ButtonHoldMin!;
playervar define ability2ButtonHoldMax!;

playervar define watchFriendProbability!;

define targetPosRange: 6;

rule: "Bot/SetCombatRoutine"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
        heroName = "Default";
        watchFriendProbability = 0;

        combatRange = CombatRange.Middle;

        primaryFireButtonBehaivor = ButtonBehaivor.HoldOnly;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 5;
        primaryFireButtonHoldMin = 0;
        primaryFireButtonHoldMax = 5;

        secondaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        secondaryFireButtonReleaseMin = 0;
        secondaryFireButtonReleaseMax = 5;
        secondaryFireButtonHoldMin = 0;
        secondaryFireButtonHoldMax = 5;

        ability1ButtonReleaseMin = 0;
        ability1ButtonReleaseMax = 5;
        ability1ButtonHoldMin = 0;
        ability1ButtonHoldMax = 5;

        ability2ButtonReleaseMin = 0;
        ability2ButtonReleaseMax = 5;
        ability2ButtonHoldMin = 0;
        ability2ButtonHoldMax = 5;


    switch(HeroOf()){
        case Hero.Ana:
        heroName = "Ana";
        watchFriendProbability = 100;
        combatRange = CombatRange.Far;
        secondaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        secondaryFireButtonHoldMin = 10;
        secondaryFireButtonHoldMax = 10;
        break;

        case Hero.Ashe:
        heroName = "Ashe";
        combatRange = CombatRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.Repeat;
        secondaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        secondaryFireButtonHoldMin = 10;
        secondaryFireButtonHoldMax = 10;
        break;

        case Hero.Baptiste:
        heroName = "Baptiste";
        watchFriendProbability = 75;
        combatRange = CombatRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        break;

        case Hero.Bastion:
        heroName = "Bastion";
        combatRange = CombatRange.Far;
        secondaryFireButtonReleaseMin = 5;
        secondaryFireButtonReleaseMax = 10;
        break;

        case Hero.Brigitte:
        heroName = "Brigitte";
        watchFriendProbability = 50;
        combatRange = CombatRange.Melee;
        break;

        case Hero.Doomfist:
        heroName = "Doomfist";
        combatRange = CombatRange.Melee;
        secondaryFireButtonHoldMin = 2;
        secondaryFireButtonHoldMax = 3;
        break;

        case Hero.Dva:
        heroName = "Doomfist";
        combatRange = CombatRange.Close;
        break;

        case Hero.Echo:
        heroName = "Echo";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Genji:
        heroName = "Genji";
        combatRange = CombatRange.Melee;
        break;

        case Hero.Hanzo:
        heroName = "Hanzo";
        combatRange = CombatRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 0;
        primaryFireButtonHoldMin = 1.5;
        primaryFireButtonHoldMax = 2;
        secondaryFireButtonBehaivor = ButtonBehaivor.Non;
        break;

        case Hero.Junkrat:
        heroName = "Junkrat";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Lucio:
        heroName = "Lucio";
        watchFriendProbability = 50;
        combatRange = CombatRange.Middle;
        break;

        case Hero.Mccree:
        heroName = "Mccree";
        combatRange = CombatRange.Close;
        break;

        case Hero.Mei:
        heroName = "Mei";
        combatRange = CombatRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        break;

        case Hero.Mercy:
        heroName = "Mercy";
        watchFriendProbability = 100;
        combatRange = CombatRange.Far;
        break;

        case Hero.Moira:
        heroName = "Moira";
        watchFriendProbability = 75;
        combatRange = CombatRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        secondaryFireButtonBehaivor = ButtonBehaivor.HoldOnly;
        primaryFireButtonHoldMin = 3;
        primaryFireButtonHoldMax = 6;
        break;

        case Hero.Orisa:
        heroName = "Orisa";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Pharah:
        heroName = "Pharah";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Reaper:
        heroName = "Reaper";
        combatRange = CombatRange.Close;
        break;

        case Hero.Reinhardt:
        heroName = "Reinhardt";
        combatRange = CombatRange.Melee;
        break;

        case Hero.Roadhog:
        heroName = "Reaper";
        combatRange = CombatRange.Close;
        break;

        case Hero.Sigma:
        heroName = "Sigma";
        combatRange = CombatRange.Middle;
        secondaryFireButtonReleaseMin = 0;
        secondaryFireButtonReleaseMax = 10;
        secondaryFireButtonHoldMin = 0;
        secondaryFireButtonHoldMax = 1;
        break;

        case Hero.Soldier76:
        heroName = "Soldier76";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Sombra:
        heroName = "Sombra";
        combatRange = CombatRange.Close;
        break;

        case Hero.Symmetra:
        heroName = "Symmetra";
        combatRange = CombatRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        break;

        case Hero.Torbjorn:
        heroName = "Torbjorn";
        combatRange = CombatRange.Middle;
        break;

        case Hero.Tracer:
        heroName = "Tracer";
        combatRange = CombatRange.Close;
        break;

        case Hero.Widowmaker:
        heroName = "Widowmaker";
        combatRange = CombatRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        primaryFireButtonReleaseMin = 1;
        primaryFireButtonReleaseMax = 1.5;
        primaryFireButtonHoldMin = 0;
        primaryFireButtonHoldMax = 2;

        secondaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        secondaryFireButtonReleaseMin = 0;
        secondaryFireButtonReleaseMax = 5;
        secondaryFireButtonHoldMin = 10;
        secondaryFireButtonHoldMax = 10;

        ability1ButtonReleaseMin = 10;
        ability1ButtonReleaseMax = 15;
        break;

        case Hero.Winston:
        heroName = "Winston";
        combatRange = CombatRange.Melee;
        break;

        case Hero.WreckingBall:
        heroName = "WreckingBall";
        combatRange = CombatRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        primaryFireButtonHoldMin = 5;
        primaryFireButtonHoldMax = 15;

        secondaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        primaryFireButtonHoldMin = 10;
        primaryFireButtonHoldMax = 15;
        break;

        case Hero.Zarya:
        heroName = "Zarya";
        combatRange = CombatRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        break;

        case Hero.Zenyatta:
        heroName = "Zenyatta";
        watchFriendProbability = 25;
        combatRange = CombatRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.HoldAndRelease;
        break;
    }


    // set combat range
    switch(combatRange){
        case CombatRange.Melee:
            combatRangeMin = 1;
            combatRangeMax = 3;
            break;
        case CombatRange.Close:
            combatRangeMin = 1;
            combatRangeMax = 10;
            break;
        case CombatRange.Middle:
            combatRangeMin = 10;
            combatRangeMax = 20;
            break;
        case CombatRange.Far:
            combatRangeMin = 20;
            combatRangeMax = 30;
            break;
    }
}

enum State{
    Non,
    Combat,
    Searching
}

enum CombatRange{
    Non,
    Melee,
    Close,
    Middle,
    Far
}

enum ButtonBehaivor{
    Non,
    HoldOnly,
    HoldAndRelease,
    Repeat
}

// TAG Bot/ButtonControl ----------
enum ButtonControl{
    button,
    buttonOperation,
    waitDuringMin,
    waitDuringMax,
    holdDuringMin,
    holdDuringMax
}

define noShooting: false;

playervar define isRepeating!;
playervar define repeatDuringMin!;
playervar define repeatDuringMax!;

void StartButtonControl(define buttonControl){
    AbortIf(noShooting);

    if(buttonControl[ButtonControl.buttonOperation] == ButtonBehaivor.HoldOnly){
        while(true){
            StartHoldingButton(ep, buttonControl[ButtonControl.button]);
            Wait(RandomReal(buttonControl[ButtonControl.holdDuringMin], buttonControl[ButtonControl.holdDuringMax]), WaitBehavior.AbortWhenFalse);
            StopHoldingButton(ep, buttonControl[ButtonControl.button]);
        }
    }
    else if(buttonControl[ButtonControl.buttonOperation] == ButtonBehaivor.HoldAndRelease){
        while(true){
            Wait(RandomReal(buttonControl[ButtonControl.waitDuringMin], buttonControl[ButtonControl.waitDuringMax]), WaitBehavior.AbortWhenFalse);
            StartHoldingButton(ep, buttonControl[ButtonControl.button]);
            Wait(RandomReal(buttonControl[ButtonControl.holdDuringMin], buttonControl[ButtonControl.holdDuringMax]), WaitBehavior.AbortWhenFalse);
            StopHoldingButton(ep, buttonControl[ButtonControl.button]);
        }
    }
    else if(buttonControl[ButtonControl.buttonOperation] == ButtonBehaivor.Repeat){
        repeatDuringMin = buttonControl[ButtonControl.holdDuringMin];
        repeatDuringMax = buttonControl[ButtonControl.holdDuringMax];
        while(true){
            Wait(RandomReal(buttonControl[ButtonControl.waitDuringMin], buttonControl[ButtonControl.waitDuringMax]), WaitBehavior.AbortWhenFalse);
            isRepeating = true;
            while(isRepeating){
                PressButton(ep, buttonControl[ButtonControl.button]); 
                Wait(0.2, WaitBehavior.AbortWhenFalse);
            }
        }
    }
}

rule: "Bot/ButtonControl/isRepeating"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat &&
    isRepeating
){
    Wait(RandomReal(repeatDuringMin, repeatDuringMax), WaitBehavior.AbortWhenFalse);
    isRepeating = false;
}

rule: "Bot/ButtonControl/PrimaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.PrimaryFire,
            primaryFireButtonBehaivor,
            primaryFireButtonReleaseMin,
            primaryFireButtonReleaseMax,
            primaryFireButtonHoldMin,
            primaryFireButtonHoldMax
        ]
    );
}

rule: "Bot/ButtonControl/SeconaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.SecondaryFire,
            secondaryFireButtonBehaivor,
            secondaryFireButtonReleaseMin,
            secondaryFireButtonReleaseMax,
            secondaryFireButtonHoldMin,
            secondaryFireButtonHoldMax
        ]
    );
}

rule: "Bot/ButtonControl/Ability1/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.Ability1,
            ButtonBehaivor.HoldAndRelease,
            ability1ButtonReleaseMin,
            ability1ButtonReleaseMax,
            ability1ButtonHoldMin,
            ability1ButtonHoldMax
        ]
    );
}

rule: "Bot/ButtonControl/Ability2/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.Ability2,
            ButtonBehaivor.HoldAndRelease,
            ability2ButtonReleaseMin,
            ability2ButtonReleaseMax,
            ability2ButtonHoldMin,
            ability2ButtonHoldMax
        ]
    );
}

rule: "Bot/ButtonControl/Ultimate/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat &&
    UltimateChargePercent() >= 100
){
    StartButtonControl(
        [
            Button.Ultimate,
            ButtonBehaivor.HoldAndRelease,
            0,
            10,
            0,
            1
        ]
    );
}

rule: "Bot/ButtonControl/Jump/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.Jump,
            ButtonBehaivor.HoldAndRelease,
            0,
            5,
            0,
            5
        ]
    );
}

rule: "Bot/ButtonControl/Crouch/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.Crouch,
            ButtonBehaivor.HoldAndRelease,
            5,
            10,
            0,
            3
        ]
    );
}

rule: "Bot/ButtonControl/Reload/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    StartButtonControl(
        [
            Button.Reload,
            ButtonBehaivor.HoldAndRelease,
            5,
            15,
            0,
            1
        ]
    );
}

rule: "Bot/ButtonControl/WeaponSwitch/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    while(true){
        Wait(RandomReal(0, 30), WaitBehavior.AbortWhenFalse);
        SetWeapon(ep, RandomInteger(0, 1));
    }
}

rule: "Bot/ButtonControl/StopButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState != State.Combat
){
    StopHoldingButton(ep, Button.PrimaryFire);
    StopHoldingButton(ep, Button.SecondaryFire);
    StopHoldingButton(ep, Button.Ability1);
    StopHoldingButton(ep, Button.Ability2);
    StopHoldingButton(ep, Button.Ultimate);
    StopHoldingButton(ep, Button.Jump);
    StopHoldingButton(ep, Button.Crouch);
}


// TAG Bot/ActionStateControl ----------
define AttackTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(OppositeTeamOf(TeamOf())),
                (
                    HasSpawned(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock) &&
                    IsAlive(ae)
                )
            ),
            DistanceBetween(ep, ae)
        )
    );

define HealTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(TeamOf()),
                (
                    ae != ep &&
                    HasSpawned(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock) &&
                    IsAlive(ae) &&
                    NormalizedHealth(ae) <= 0.9
                )
            ),
            DistanceBetween(ep, ae)
        )
    );

define watchUpdateRate:
    isServerOverLoading ? 2 : 1;

rule: "Bot/ActionState/StateControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
    // check target
    if(
        ProbabilityTrue(25) ||                 // Probability
        target == 0 ||                         // no target
        !IsInLineOfSight(ep, target) ||        // targhet lost
        IsDead(target) ||                      // target dead
        (   
            TeamOf(target) == TeamOf(ep) &&    // heal target almost full health
            NormalizedHealth(target) >= 0.9
        )       
    ){
        // set target
        if(
            ProbabilityTrue(watchFriendProbability)    // each support hero Probability
        ){
            // set heal target
            target = HealTarget();
            // no heal target
            if(target == 0){
                // set attack target
                target = AttackTarget();
            }
        }
        else{
            // set attack target
            target = AttackTarget();
        }

        // has target
        if(target != 0){
            // set target
            targetPos = PositionOf(target);
        }
    }

    // change state
    if(
        IsDead()
    ){
        // non by dead
        actionState = State.Non; 
    }
    else
    { 
        if(target != 0){
            // combat by contact player
            actionState = State.Combat; 
        }
        else
        {
            if(targetPos != 0){
                // searching by target lost
                actionState = State.Searching;
            }
            else{
                // non by target not found
                actionState = State.Non;
            }       
        }
    }
    Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

define StartMoveInDir(define dir):
    StartThrottleInDirection(
        ep, dir, 1,
        Relative.ToPlayer,
        ThrottleBehavior.ReplaceExistingThrottle,
        ThrottleRev.DirectionAndMagnitude);

define StopMove(): StopThrottleInDirection();

void StartMove()"StartMove"{
    if(moveDir != 0 && sideStepDir != 0){
        StartMoveInDir(moveDir + sideStepDir);
    }
    else if(moveDir == 0 && sideStepDir != 0){
        StartMoveInDir(sideStepDir);
    }
    else if(moveDir != 0 && sideStepDir == 0){
        StartMoveInDir(moveDir);
    }
    else{
        StopMove();
    }
}

define stackRange: 0.75;

playervar define prevPosition;

void checkStackAndBreak()"checkStackAndBreak"{
    if(
        prevPosition != 0 &&
        sideStepDir != 0 &&
        moveDir != 0 &&
        DistanceBetween(PositionOf(), prevPosition) < stackRange
    ){
        statusText = <"Combat/BreakStack">;
        
        while(
            DistanceBetween(PositionOf(), prevPosition) < stackRange &&
            ProbabilityTrue(70)
        ){
            // change move direction
            sideStepDir = RandomValueInArray([0, 0, Right(), Left()]);
            moveDir = RandomValueInArray([Forward(), Forward(), Backward()]);
            StartMove();
            // try jump or croch
            define breakStackButton = RandomValueInArray([Button.Jump, Button.Crouch]);
            StartHoldingButton(ep, breakStackButton);
            Wait(RandomReal(1, 2), WaitBehavior.AbortWhenFalse);
            StopHoldingButton(ep, breakStackButton);
        }
    }
    else{
        prevPosition = PositionOf();
    }
}

// TAG Bot/ActionState/Non ----------
rule: "Bot/ActionState/Non"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Non
){
    statusText = <"Non">;
    target = 0;
    targetPos = 0;
    StopMove();
    StopFacing();
}

// TAG Bot/ActionState/Searching ----------
rule: "Bot/ActionState/Searching"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Searching
){
    statusText = <"Searching">;
    // reload
    PressButton(ep, Button.Reload);

    // set initial status

    // convert targetpPos to rechable position
    targetPos = BelowPosition(targetPos);

    // set face direction to target lost posotion
    StartFacing(
        ep,
        DirectionTowards(
            EyePosition(),
            targetPos),
        300,
        Relative.ToWorld, FacingRev.DirectionAndTurnRate
    );

    if(sideStepDir == 0){
        //sideStepDir = RandomValueInArray([Right(),Left()]);
        sideStepDir = 0;
    }
    else{
        // set reverse side step for find player
        sideStepDir = -sideStepDir;
    }
    
    // action loop
    while(true){
        // set move directiion
        if(!IsBelowAbyss(ForwardSensor())){
            moveDir = Forward();
            statusText = <"Searching/Walking">;
        }
        else{
            moveDir = 0;
            statusText = <"Searching/Keep_Distance">;
        }

        // reached target lost position
        if(DistanceBetween(targetPos, ep) <= targetPosRange){
            Wait(1, WaitBehavior.AbortWhenFalse);

            statusText = <"Searching/Canceled">;
            Wait(1, WaitBehavior.AbortWhenFalse);
            actionState = State.Non;
        }

        // set move direction
        StartMove();

        // check stack
        checkStackAndBreak();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
    }
}

rule: "Bot/ActionState/Searching/timeout"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Searching
){
    Wait(8, WaitBehavior.AbortWhenFalse);

    statusText = <"Searching/Timeout">;
    Wait(1, WaitBehavior.IgnoreCondition);
    actionState = State.Non;
}

// TAG Bot/ActionState/Combat ----------
rule: "Bot/ActionState/Combat"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == State.Combat
){
    statusText = <"Combat">;

    // set initial status
    moveDir = 0;
    sideStepDir = 0;

    // set face direction
    StartFacing(
        ep,
        DirectionTowards(
            EyePosition(),
            EyePosition(target)),
        300,
        Relative.ToWorld, FacingRev.DirectionAndTurnRate
    );   
    
    // action loop
    while(true){
        // set move direction
        define distToTarget = DistanceBetween(ep, target);
        // target is enemy
        // if(TeamOf(target) != TeamOf()){
            // target is far
            if(
                distToTarget > combatRangeMax &&
                !IsBelowAbyss(ForwardSensor())
            ){
                moveDir = Forward();
                statusText = <"Combat/Forward">;
            }
            // target is close
            else if(
                distToTarget < combatRangeMin &&
                !IsBelowAbyss(BackwardSensor())
            ){
                moveDir = Backward();
                statusText = <"Combat/Backward">;
            }
            else{
                moveDir = 0;
                statusText = <"Combat/Keep_Distance">;
            }
        // }
        // // target is friend
        // else{
        //     // target is far
        //     if(
        //         distToTarget > 10 &&
        //         !IsBelowAbyss(ForwardSensor())
        //     ){
        //         moveDir = Forward();
        //         statusText = <"Combat/Forward">;
        //     }
        //     else{
        //         moveDir = 0;
        //         statusText = <"Combat/Keep_Distance">;
        //     }
        // }

        // set sidestep direction
        if(
            sideStepSecond == 0
        ){
            if(ProbabilityTrue(50)){
                if(ProbabilityTrue(50)){
                    sideStepDir = Right();
                    sideStepSecond = RandomInteger(0, 3);
                }
                else{
                    sideStepDir = Left();
                    sideStepSecond = RandomInteger(0, 3);
                }
            }
            else{
                sideStepDir = 0;
                sideStepSecond = RandomInteger(0, 3);
            }
        }

        // set move direction
        StartMove();

        // check stack
        checkStackAndBreak();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
    }
}

rule: "Bot/Bot/ActionState/Combat/sideStepSecond"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    0 < sideStepSecond
){
    while(true){
        Wait(1, WaitBehavior.AbortWhenFalse);
        sideStepSecond --;
    }
}

// Debug -----------------------------------------------
define maxBotNum: 6;
disabled rule: "Debug/SpawnOneBot"
Event.OngoingPlayer
if(
    ep == HostPlayer() &&
    IsButtonHeld(ep, Button.Interact)
){
    WaitLongPressConfirm();
    define team = botSlot % 2 == 0 ? Team.Team2 : Team.Team1;
    DestroyDummyBot(team, botSlot);
    CreateDummyBot(RandomValueInArray(AllHeroes()), team, botSlot,
        AimingPosition(), FacingDirectionOf()
    );
    if(
        botSlot < maxBotNum &&
        team == Team.Team2
    )
        botSlot ++;
    else
        botSlot = 0;
}

disabled rule: "Debug/ShowDebugInfo"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){  
    // show status text
    CreateInWorldText(
        HostPlayer(), statusText,
        ep + (Up() * 1.5), 1, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);
    // show targetPos path
    CreateBeamEffect(
            ep.actionState == State.Combat ||
            ep.actionState == State.Searching ?
                HostPlayer() : null,
        BeamType.GoodBeam,
        EyePosition(),
            actionState == State.Combat ?
                target :
                targetPos,
            actionState == State.Combat ?
                TeamOf(target) != TeamOf() ?
                    Color.Red :
                    Color.Blue :
                Color.White,
        EffectRev.VisibleToPositionRadiusAndColor);
    // show targetPos
    CreateEffect(
        ep.actionState == State.Searching ? HostPlayer() : null,
        Effect.Ring, Color.Red,
        targetPos, targetPosRange, EffectRev.VisibleToPositionAndRadius);
}
