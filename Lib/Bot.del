import "Utility.del";
import "Debug.del";

// TAG public property
globalvar define botAimScat = WorkshopSettingReal("Enemy Bot Settings", "Aim Scattering (0 is head shot)",              0.25, 0, 1000, 3);

// TAG Bot/Spawn ----------
enum BotAsset{
    EnemyType,
    hero,
    count
}

// TAG EnemyType ----------
playervar define enemyType;
enum EnemyType{
    None,
    Minion,
    Elite,
    Boss,
    GiantBoss
}

// TAG BotAsset ----------
define RandomHero: RandomValueInArray(AllHeroes());
define RandomTankHero: RandomValueInArray(AllTankHeroes());
define RandomDamageHero: RandomValueInArray(AllDamageHeroes());
define RandomSupportHero: RandomValueInArray(AllSupportHeroes());

globalvar define botSlot!;
globalvar define enemyTeamName!;

void CreateEnemyBotsByAssetList(define assetList, define team)"CreateEnemyBotsByAssetList"{
    DestroyBots(Team.Team2);
    define asset = ValueInArray(assetList, RandomInteger(0, CountOf(assetList) - 1));
    // set team name
    enemyTeamName = asset[0];
    // create bot by asset
    for(define assetIdx = 1; assetIdx < CountOf(asset); assetIdx++){
        for(define botCount = 0; botCount < asset[assetIdx][BotAsset.count]; botCount++){
            // create bot
            CreateDummyBot(
                asset[assetIdx][BotAsset.hero],
                team,
                botSlot,
                SpawnPoints(team)[0],
                RandomDirection()
            );
            Wait(0.1, WaitBehavior.IgnoreCondition);
                WaitUntil(!isServerOverLoading, 3);
            // set Bot type as enemy type
            define bot! = PlayersInSlot(botSlot, team);
            bot.enemyType = asset[assetIdx][BotAsset.EnemyType];

            // next slot
            botSlot ++;
        }
    }
}

void CreateBothBotsByInput(define heroList, define num)"CreateBothBotsByInput"{
    DestroyBots(Team.Team1);
    DestroyBots(Team.Team2);
    Wait(0.1, WaitBehavior.IgnoreCondition);

    // create bot by asset
    for(define slotIdx = 0; slotIdx < num; slotIdx++){
        // asign team
        define team = (slotIdx % 2 == 0) ? Team.Team1 : Team.Team2;
        // try until spawn entity
        while(
            PlayersInSlot(botSlot, team) == null &&
            !HasSpawned(PlayersInSlot(botSlot, team))
        ){
            // create bot
            CreateDummyBot(
                RandomValueInArray(heroList),
                team,
                botSlot,
                SpawnPoints(team)[0],
                RandomDirection()
            );
            Wait(0.1, WaitBehavior.IgnoreCondition);
            WaitUntil(!isServerOverLoading, 3);
        }   
        
        if(team == Team.Team2){
            // next slot
            botSlot ++;
        }
    }
}

void DestroyBots(define team)"DestroyBots"{
    for(define slotIdx = 0; slotIdx < 24; slotIdx ++){
        // try until despawn entity
        // while(HasSpawned(PlayersInSlot(slotIdx, team))){
            DestroyDummyBot(
                team, slotIdx);
            Wait(0.01, WaitBehavior.IgnoreCondition);
        // }
    }
    botSlot = 0;
}

// TAG Bot/SetBotInfo ----------

playervar define heroName;
playervar define supportProbability;
playervar define supportButtons;
playervar define attackButtons;

playervar define primaryFireButtonBehaivor;
playervar define primaryFireButtonReleaseMin;
playervar define primaryFireButtonReleaseMax;
playervar define primaryFireButtonPressMin;
playervar define primaryFireButtonPressMax;
playervar define secondaryFireButtonBehaivor;
playervar define secondaryFireButtonReleaseMin;
playervar define secondaryFireButtonReleaseMax;
playervar define secondaryFireButtonPressMin;
playervar define secondaryFireButtonPressMax;
playervar define ability1ButtonReleaseMin;
playervar define ability1ButtonReleaseMax;
playervar define ability1ButtonPressMin;
playervar define ability1ButtonPressMax;
playervar define ability2ButtonReleaseMin;
playervar define ability2ButtonReleaseMax;
playervar define ability2ButtonPressMin;
playervar define ability2ButtonPressMax;
playervar define ultimateButtonReleaseMin;
playervar define ultimateButtonReleaseMax;
playervar define ultimateButtonPressMin;
playervar define ultimateButtonPressMax;

playervar define attackRange;
playervar define attackRangeMin;
playervar define attackRangeMax;

rule: "Bot/SetActionRoutine"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
        attackRange = AttackRange.Middle;
        heroName = "Default";
        supportProbability = 0;
        supportButtons = [];
        attackButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];

        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 3;
        primaryFireButtonPressMin = 0;
        primaryFireButtonPressMax = 10;

        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonReleaseMin = 0;
        secondaryFireButtonReleaseMax = 3;
        secondaryFireButtonPressMin = 0;
        secondaryFireButtonPressMax = 10;

        ability1ButtonReleaseMin = 0;
        ability1ButtonReleaseMax = 3;
        ability1ButtonPressMin = 0;
        ability1ButtonPressMax = 5;

        ability2ButtonReleaseMin = 0;
        ability2ButtonReleaseMax = 3;
        ability2ButtonPressMin = 0;
        ability2ButtonPressMax = 5;

        ultimateButtonReleaseMin = 0;
        ultimateButtonReleaseMax = 3;
        ultimateButtonPressMin = 0;
        ultimateButtonPressMax = 5;

    switch(HeroOf()){
        case Hero.Ana:
        heroName = "Ana";
        attackRange = AttackRange.Far;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 10;
        secondaryFireButtonPressMax = 15;
        supportProbability = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability2
        ];
        break;

        case Hero.Ashe:
        heroName = "Ashe";
        attackRange = AttackRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleaseRepeat;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 10;
        secondaryFireButtonPressMax = 10;
        break;

        case Hero.Baptiste:
        heroName = "Baptiste";
        attackRange = AttackRange.Far;
        supportProbability = 100;
        supportButtons = [
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        attackButtons = [
            Button.PrimaryFire,
            Button.Ability1
        ];
        break;

        case Hero.Bastion:
        heroName = "Bastion";
        attackRange = AttackRange.Far;
        secondaryFireButtonReleaseMin = 10;
        secondaryFireButtonReleaseMax = 15;
        ability1ButtonReleaseMin = 5;
        ability1ButtonReleaseMax = 20;
        ability1ButtonPressMin = 0.5;
        ability1ButtonPressMax = 0.5;
        ultimateButtonReleaseMax = 40;
        break;

        case Hero.Brigitte:
        heroName = "Brigitte";
        attackRange = AttackRange.Melee;
        secondaryFireButtonReleaseMax = 6;
        primaryFireButtonPressMax = 15;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 1;
        ultimateButtonReleaseMax = 30;
        supportProbability = 50;
        supportButtons = [
            Button.Ability2
        ];
        attackButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability1
        ];
        break;

        case Hero.Doomfist:
        heroName = "Doomfist";
        attackRange = AttackRange.Melee;
        primaryFireButtonReleaseMax = 3;
        secondaryFireButtonPressMin = 2;
        secondaryFireButtonPressMax = 3;
        break;

        case Hero.Dva:
        heroName = "Dva";
        attackRange = AttackRange.Close;
        ultimateButtonReleaseMax = 30;
        break;

        case Hero.Echo:
        heroName = "Echo";
        attackRange = AttackRange.Middle;
        break;

        case Hero.Genji:
        heroName = "Genji";
        attackRange = AttackRange.Melee;
        break;

        case Hero.Hanzo:
        heroName = "Hanzo";
        attackRange = AttackRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMax = 1;
        primaryFireButtonPressMin = 1.5;
        primaryFireButtonPressMax = 2;
        secondaryFireButtonBehaivor = ButtonBehaivor.Non;
        break;

        case Hero.Junkrat:
        heroName = "Junkrat";
        attackRange = AttackRange.Middle;
        break;

        case Hero.Lucio:
        heroName = "Lucio";
        attackRange = AttackRange.Middle;
        supportProbability = 50;
        supportButtons = [
            Button.Ability1,
            Button.Ability2
        ];
        break;

        case Hero.Cassidy:
        heroName = "Cassidy";
        attackRange = AttackRange.Close;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 2;
        secondaryFireButtonReleaseMax = 15;
        break;

        case Hero.Mei:
        heroName = "Mei";
        attackRange = AttackRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        break;

        case Hero.Mercy:
        heroName = "Mercy";
        attackRange = AttackRange.Far;
        secondaryFireButtonReleaseMax = 10;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 2;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 2;
        supportProbability = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        break;

        case Hero.Moira:
        heroName = "Moira";
        attackRange = AttackRange.Middle;
        supportProbability = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        attackButtons = [
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        break;

        case Hero.Orisa:
        heroName = "Orisa";
        attackRange = AttackRange.Far;
        break;

        case Hero.Pharah:
        heroName = "Pharah";
        attackRange = AttackRange.Middle;
        break;

        case Hero.Reaper:
        heroName = "Reaper";
        attackRange = AttackRange.Close;
        break;

        case Hero.Reinhardt:
        heroName = "Reinhardt";
        attackRange = AttackRange.Melee;
        secondaryFireButtonReleaseMax = 10;
        primaryFireButtonPressMax = 20;
        break;

        case Hero.Roadhog:
        heroName = "Reaper";
        attackRange = AttackRange.Close;
        break;

        case Hero.Sigma:
        heroName = "Sigma";
        attackRange = AttackRange.Middle;
        primaryFireButtonPressMax = 5;
        secondaryFireButtonReleaseMax = 15;
        secondaryFireButtonPressMax = 1;
        break;

        case Hero.Soldier76:
        heroName = "Soldier76";
        attackRange = AttackRange.Middle;
        break;

        case Hero.Sombra:
        heroName = "Sombra";
        attackRange = AttackRange.Close;
        secondaryFireButtonPressMin = 1.5;
        ability2ButtonReleaseMax = 10;
        ability2ButtonPressMax = 0.1;
        break;

        case Hero.Symmetra:
        heroName = "Symmetra";
        attackRange = AttackRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 1;
        secondaryFireButtonPressMax = 3;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 3;
        ability2ButtonReleaseMax = 15;
        break;

        case Hero.Torbjorn:
        heroName = "Torbjorn";
        attackRange = AttackRange.Middle;
        break;

        case Hero.Tracer:
        heroName = "Tracer";
        attackRange = AttackRange.Close;
        break;

        case Hero.Widowmaker:
        heroName = "Widowmaker";
        attackRange = AttackRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 1;
        primaryFireButtonReleaseMax = 1.5;
        primaryFireButtonPressMin = 0;
        primaryFireButtonPressMax = 1;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 15;
        secondaryFireButtonPressMax = 20;
        ability1ButtonReleaseMin = 10;
        ability1ButtonReleaseMax = 15;
        ability2ButtonReleaseMin = 10;
        ability2ButtonReleaseMax = 15;
        break;

        case Hero.Winston:
        heroName = "Winston";
        attackRange = AttackRange.Melee;
        ultimateButtonReleaseMax = 30;
        break;

        case Hero.WreckingBall:
        heroName = "WreckingBall";
        attackRange = AttackRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMax = 15;
        primaryFireButtonPressMax = 20;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 5;
        secondaryFireButtonPressMax = 15;
        ability1ButtonReleaseMin = 5;
        ability1ButtonReleaseMax = 10;
        break;

        case Hero.Zarya:
        heroName = "Zarya";
        attackRange = AttackRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 1;
        break;

        case Hero.Zenyatta:
        heroName = "Zenyatta";
        attackRange = AttackRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 1;
        secondaryFireButtonReleaseMin = 1;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 2;
        ability1ButtonPressMin = 1;
        ability1ButtonPressMax = 2;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 2;
        ability2ButtonPressMin = 1;
        ability2ButtonPressMax = 2;
        ultimateButtonReleaseMax = 30;
        supportProbability = 50;
        supportButtons = [
            Button.Ability1,
            Button.Ability2
        ];
        break;
    }

    // set Attack range
    switch(attackRange){
        case AttackRange.Melee:
            attackRangeMin = 0;
            attackRangeMax = 4;
            break;
        case AttackRange.Close:
            attackRangeMin = 0;
            attackRangeMax = 8;
            break;
        case AttackRange.Middle:
            attackRangeMin = 8;
            attackRangeMax = 16;
            break;
        case AttackRange.Far:
            attackRangeMin = 16;
            attackRangeMax = 34;
            break;
    }
}

enum ActionState{
    Non,
    Attack,
    Support
}

enum AttackRange{
    Non,
    Melee,
    Close,
    Middle,
    Far
}

enum ButtonBehaivor{
    Non,
    ReleasePress,
    ReleaseRepeat
}

// TAG Bot/ActionState/StateControl ----------
define needHealRate: 0.7;

define watchUpdateRate:
    isServerOverLoading ? 3 : 1.5;

define FindAttackTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(OppositeTeamOf(TeamOf())),
                (
                    HasSpawned(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock) &&
                    IsAlive(ae) &&
                    !(
                        IsStealth(ae) &&
                        DistanceBetween(ae, ep) >= 3
                    )
                )
            ),
            DistanceBetween(ae, ep)
        )
    );

define FindSupportTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(TeamOf()),
                (
                    ae != ep &&
                    HasSpawned(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock) &&
                    IsAlive(ae) &&
                    NormalizedHealth(ae) < needHealRate
                )
            ),
            Health(ae) - MaxHealth(ae)
        )
    );

rule: "Bot/ActionState/StateControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
    // if(target != 0)
    //     dbgPlyBemEft(EyePosition(), EyePosition(target) - PositionOffset());

    define targetSighted! =
        target != 0 &&
        HasSpawned(target) &&
        IsInLineOfSight(ep, target, BarrierLOS.NoBarriersBlock) &&
        IsAlive(target) &&
        !(
            IsStealth(target) &&
            DistanceBetween(target, ep) >= 3
        );

    // get new target
    if(
        // target not Sighted
        !targetSighted
            ||
        (
            // if attack target higher health, search new target.
            actionState == ActionState.Attack &&
            ProbabilityTrue(NormalizedHealth(target) *
            // if support hero, highly likely to search new target.
            (supportProbability > 0) ? 100 : 50)
        ) 
            ||
        (
            // if support target higher health, search new target.
            actionState == ActionState.Support &&
            ProbabilityTrue(NormalizedHealth(target) * 50)
        )
    ){
        // set target
        if(
            ProbabilityTrue(supportProbability)    // each support hero Probability
        ){
            // find support target
            target = FindSupportTarget();
            // not found heal target
            if(target == 0){
                // find attack target
                target = FindAttackTarget();
            }
        }
        else{
            // find attack target
            target = FindAttackTarget();
        }
    }

    // get next state
    define nextActionState!;
    // is dead
    if(IsDead()){
        // Non state by dead
        nextActionState = ActionState.Non; 
    }
    else
    { 
        // has target
        if(target != 0){
            // target is enemy team
            if(TeamOf() != TeamOf(target)){
                // Attack state by enemy target
                nextActionState = ActionState.Attack; 
            }
            else{
                // Support state by friend target
                nextActionState = ActionState.Support; 
            }
        }
        else{
            // Non state by target not found
            nextActionState = ActionState.Non;
        }       
    }

    // state changed
    if(nextActionState != actionState){
        // next state is not Non state
        if(nextActionState != ActionState.Non){
            // temporary set Non state for restart button control
            actionState = ActionState.Non;
            WaitChangeState();
        }
        // set next state
        actionState = nextActionState;
    }
    // wait check cycle
    Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
    if(isServerOverLoading){
        // for distribute load
        Wait(RandomInteger(0, 1), WaitBehavior.AbortWhenFalse);
    }
    WaitUntil(!isServerOverLoading, 10);
    LoopIfConditionIsTrue();
}

rule: "Bot/ActionState/GotoAttackStateOnDamageTaken"
Event.OnDamageTaken
if(
    IsDummyBot() &&
    actionState == ActionState.Non &&
    Attacker() != ep
){
    // set attack target
    target = Attacker();
    // temporary set Non state for restart button control
    actionState = ActionState.Non;
    WaitChangeState();
    actionState = ActionState.Attack;
}

playervar define actionState;
playervar define prevActionState;
playervar define statusText;
playervar define target;
playervar define moveDir;
playervar define sideStepDir;

void StopMove()"StopMove"{
    StopThrottleInDirection();
} 

void StartMove()"StartMove"{
    if(moveDir != 0 && sideStepDir != 0){
        StartMoveInDir(moveDir + sideStepDir);
    }
    else if(moveDir == 0 && sideStepDir != 0){
        StartMoveInDir(sideStepDir);
    }
    else if(moveDir != 0 && sideStepDir == 0){
        StartMoveInDir(moveDir);
    }
    else{
        StopMove();
    }
}

void StartMoveInDir(define dir){
    StartThrottleInDirection(
        ep, dir, 1,
        Relative.ToPlayer,
        ThrottleBehavior.ReplaceExistingThrottle,
        ThrottleRev.DirectionAndMagnitude
    );
}

define stuckRange: 0.25;

playervar define prevPosition;

void checkAndBreakStuck()"checkAndBreakStuck"{
    if(
        (   // has move
            sideStepDir != 0 ||
            moveDir != 0
        ) &&
        prevPosition != 0 &&
        DistanceBetween(ep, prevPosition) <= stuckRange    // not moved from prevPosition
    ){
        statusText = <"checkAndBreakStuck">;
        while(
            // keep try break stuck prob
            ProbabilityTrue(75) &&
            // not moved from prevPosition yet
            DistanceBetween(ep, prevPosition) <= stuckRange
        ){
            //dbgPlyEft(ep);
            // try jump or croch
            define breakStuckButton! = RandomValueInArray([Button.Jump, Button.Crouch]);
            StartHoldingButton(ep, breakStuckButton);
            Wait(RandomReal(0.5, 1.0), WaitBehavior.IgnoreCondition);   // keep hold button
            StopHoldingButton(ep, breakStuckButton);
        }
    }
    else{
        prevPosition = PositionOf();
    }
}

void setMoveDir(define forwardIsAbyss!, define backwardIsAbyss!)"setMoveDir"{
    // set move direction
    define distToTarget! = DistanceBetween(ep, target);

    // select dir
    // target is close
    if(
        distToTarget > attackRangeMax
    ){
        moveDir = Forward();
    }
    // target is close
    //   and in attack state (not backward in support state)
    else if(
        actionState == ActionState.Attack &&    
        distToTarget < attackRangeMin

    ){
        moveDir = Backward();
    }
    else{
        // no moveDir
        moveDir = 0;
    }

    // check abyss
    // can't forward
    if(forwardIsAbyss && moveDir == Forward()){
        moveDir = 0;
    }
    // can't backward
    if(backwardIsAbyss && moveDir == Backward()){
        moveDir = 0;
    }
}

void setSideStep(define rightIsAbyss!, define leftIsAbyss!)"setSideStep"{
    define distToTarget! = DistanceBetween(ep, target);
    define isGoodRange! =
        distToTarget >= attackRangeMin &&
        distToTarget <= attackRangeMax;
    // now my range or set side step prob
    if(isGoodRange || ProbabilityTrue(50)){
        // select dir
        // no change sidestep prob
        if(ProbabilityTrue(50)){
            sideStepDir = sideStepDir;
        }
        // no sidestep prob
        else if(ProbabilityTrue(25)){
            sideStepDir = 0;
        }
        // has sidestep
        else{
            // right sidestep prob
            if(ProbabilityTrue(50)){
                sideStepDir = Right();
            }
            // left sidestep
            else{
                sideStepDir = Left();
            }
        }

        // check abyss
        // first check right prob
        if(ProbabilityTrue(50)){
            // can't right
            if(rightIsAbyss && sideStepDir == Right()){
                sideStepDir = Left();
            }
            // can't left too
            if(leftIsAbyss && sideStepDir == Left()){
                sideStepDir = 0;
            }
        }
        else{
            // can't left
            if(leftIsAbyss && sideStepDir == Left()){
                sideStepDir = Right();
            }
            // can't right too
            if(rightIsAbyss && sideStepDir == Right()){
                sideStepDir = 0;
            }
        }
    }
    else{
        sideStepDir = 0;
    }
    // else
}

void StartFacingTarget(){
    StartFacing(
        ep,
        DirectionTowards(
            EyePosition(),
            EyePosition(target) + RandomPosition(botAimScat, botAimScat, botAimScat)
        ),
        250,
        Relative.ToWorld, FacingRev.DirectionAndTurnRate
    );
}

// TAG Bot/ActionState/Non ----------
rule: "Bot/ActionState/Non"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Non
){
    statusText = <"Non">;

    // hero unique action
    heroUniqueAction();

    moveDir = 0;
    sideStepDir = 0;
    target = 0;

    StopMove();
    StopFacing();
    Wait(0.1, WaitBehavior.AbortWhenFalse);

    // set initial face direction
    SetFacing(
        ep,
        DirectionFromAngles(
            RandomReal(0, 360), RandomReal(30, -30),
        ),
        Relative.ToWorld);
}

// TAG Bot/ActionState/Attack ----------
rule: "Bot/ActionState/Attack"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Attack
){
    statusText = <"Attack">;

    // start facing direction
    StartFacingTarget();
    Wait(0.1, WaitBehavior.AbortWhenFalse);
    
    // action loop
    while(actionState == ActionState.Attack){
        // hero unique action
        heroUniqueAction();

        // check abyss
        define distance! = moveMeterPerSec * watchUpdateRate;
        define fAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Forward(), distance));
        define bAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Backward(), distance));

        // set moveDir direction
        setMoveDir(fAbyss, bAbyss);

        define rAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Right(), distance));    // care moveDir
        define lAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Left(), distance));

        // set sidestep direction
        setSideStep(rAbyss, lAbyss);

        // start move by moveDir and sideStepDir
        StartMove();

        // check stuck
        checkAndBreakStuck();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
        WaitUntil(!isServerOverLoading, 10);
    }
}

// TAG Bot/ActionState/Support ----------
rule: "Bot/ActionState/Support"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Support
){
    statusText = <"Support">;

    // start facing direction
    StartFacingTarget();
    Wait(0.1, WaitBehavior.AbortWhenFalse);

    // action loop
    while(actionState == ActionState.Support){
        // hero unique action
        heroUniqueAction();
    
        // check abyss
        define distance! = moveMeterPerSec * watchUpdateRate;
        define fAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Forward(), distance));
        define bAbyss! = IsBelowAbyss(BlockedPositonByLocalVector(Backward(), distance));
    
        // set moveDir direction
        setMoveDir(fAbyss, bAbyss);

        // no sidestepDir
        sideStepDir = 0;

        // start move by moveDir and sideStepDir
        StartMove();
        
        // check stuck
        checkAndBreakStuck();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
        WaitUntil(!isServerOverLoading, 10);
    }
}

// TAG Bot/HeroUniqueAction ----------
define MercyStaff: 1;
define MercyPistol: 2;

void heroUniqueAction(){
    if(actionState == ActionState.Non){
        // reload
        PressButton(ep, Button.Reload);

    }
    else{
        if(HeroOf() == Hero.Mercy){
            if(actionState == ActionState.Attack){
                SetWeapon(ep, MercyPistol);
            }
            else{
                SetWeapon(ep, MercyStaff);
            }
        }
        else if(HeroOf() == Hero.WreckingBall){
            if(
                AbilityCooldown(ep, Button.Crouch) <= 0 &&
                IsButtonHeld(ep, Button.Crouch) &&
                IsOnGround(ep)
            ){
                // for pile driver
                ApplyImpulse(ep, Up(),
                    10, Relative.ToWorld, ContraryMotion.Cancel);
            }

            while(IsFiringSecondary(ep)){
                // for ball charge
                define boostDir! = RandomValueInArray([Right(), Forward(), Backward(), Left()]);
                ApplyImpulse(ep, boostDir,
                    30, Relative.ToWorld, ContraryMotion.Cancel);
                Wait(0.5, WaitBehavior.AbortWhenFalse);
            }
        }
    }
}


// TAG Bot/ButtonControl ----------
enum ButtonControl{
    button,
    buttonOperation,
    releaseMin,
    releaseMax,
    pressMin,
    pressMax
}

define noShooting: false;   // for debug

playervar define isRepeating;
playervar define repeatMin;
playervar define repeatMax;

Boolean isMatchButton(define matchButton!, define butttons!){
    return IsTrueForAny(butttons, ae == matchButton);
}

define buttonControlTimeScale: 1;

void StartButtonControl(define bc){
    AbortIf(
        noShooting &&
        (
            bc[ButtonControl.button] == Button.PrimaryFire ||   
            bc[ButtonControl.button] == Button.SecondaryFire ||
            bc[ButtonControl.button] == Button.Ability1 ||
            bc[ButtonControl.button] == Button.Ability2 ||
            bc[ButtonControl.button] == Button.Ultimate
        )
    );

    define startButton = false;
    define fireAndAbilityButtons = [Button.PrimaryFire, Button.SecondaryFire, Button.Ability1, Button.Ability2];
    // if fire or ability button
    if(
        isMatchButton(bc[ButtonControl.button], fireAndAbilityButtons)
    ){
        // if match attack buttons
        if(
            actionState == ActionState.Attack &&
            isMatchButton(bc[ButtonControl.button], attackButtons)
        ){
            startButton = true;
        }
        // if match Support buttons
        else if(
            actionState == ActionState.Support &&
            isMatchButton(bc[ButtonControl.button], supportButtons)
        ){
            startButton = true;
        }
    }
    else{
        startButton = true;
    }

    // is not start button
    if(!startButton){
        // stop button
        StopHoldingButton(ep, bc[ButtonControl.button]);
    }
    else{
        /*
        // role value to low if inverse value
        if(bc[ButtonControl.releaseMin] > bc[ButtonControl.releaseMax]){
            bc[ButtonControl.releaseMin] = bc[ButtonControl.releaseMax];
        }
        if(bc[ButtonControl.pressMin] > bc[ButtonControl.pressMax]){
            bc[ButtonControl.pressMin] = bc[ButtonControl.pressMax];
        }
        */

        // start button
        if(bc[ButtonControl.buttonOperation] == ButtonBehaivor.ReleasePress){
            while(true){
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.releaseMin], bc[ButtonControl.releaseMax]), WaitBehavior.AbortWhenFalse);

                StartHoldingButton(ep, bc[ButtonControl.button]);
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.pressMin], bc[ButtonControl.pressMax]), WaitBehavior.AbortWhenFalse);

                StopHoldingButton(ep, bc[ButtonControl.button]);
            }
        }
        else if(bc[ButtonControl.buttonOperation] == ButtonBehaivor.ReleaseRepeat){
            repeatMin = bc[ButtonControl.pressMin];
            repeatMax = bc[ButtonControl.pressMax];
            while(true){
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.releaseMin], bc[ButtonControl.releaseMax]), WaitBehavior.AbortWhenFalse);

                isRepeating = true;
                while(isRepeating){
                    PressButton(ep, bc[ButtonControl.button]); 
                    Wait(0.2, WaitBehavior.AbortWhenFalse);
                }
            }
        }
        // else
    }
}

rule: "Bot/ButtonControl/releaseIsRepeating"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    isRepeating
){
    Wait(RandomReal(repeatMin, repeatMax), WaitBehavior.AbortWhenFalse);
    isRepeating = false;
}

// TAG Bot/ButtonControl/PrimaryFire ----------
define enableButtonControlState:
    actionState == ActionState.Attack ||
    actionState == ActionState.Support;

rule: "Bot/ButtonControl/PrimaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.PrimaryFire,
            primaryFireButtonBehaivor,
            primaryFireButtonReleaseMin,
            primaryFireButtonReleaseMax,
            primaryFireButtonPressMin,
            primaryFireButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/SeconaryFire ----------
rule: "Bot/ButtonControl/SeconaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.SecondaryFire,
            secondaryFireButtonBehaivor,
            secondaryFireButtonReleaseMin,
            secondaryFireButtonReleaseMax,
            secondaryFireButtonPressMin,
            secondaryFireButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ability1 ----------
rule: "Bot/ButtonControl/Ability1/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Ability1,
            ButtonBehaivor.ReleasePress,
            ability1ButtonReleaseMin,
            ability1ButtonReleaseMax,
            ability1ButtonPressMin,
            ability1ButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ability2 ----------
rule: "Bot/ButtonControl/Ability2/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Ability2,
            ButtonBehaivor.ReleasePress,
            ability2ButtonReleaseMin,
            ability2ButtonReleaseMax,
            ability2ButtonPressMin,
            ability2ButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ultimate ----------
rule: "Bot/ButtonControl/Ultimate/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState &&
    UltimateChargePercent() >= 100
){
    StartButtonControl(
        [
            Button.Ultimate,
            ButtonBehaivor.ReleasePress,
            ultimateButtonReleaseMin,
            ultimateButtonReleaseMax,
            ultimateButtonPressMin,
            ultimateButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Jump ----------
rule: "Bot/ButtonControl/Jump/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Jump,
            ButtonBehaivor.ReleasePress,
            0,
            7,
            1,
            5
        ]
    );
}

// TAG Bot/ButtonControl/Crouch ----------
rule: "Bot/ButtonControl/Crouch/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Crouch,
            ButtonBehaivor.ReleasePress,
            5,
            10,
            1,
            2
        ]
    );
}

// TAG Bot/ButtonControl/Reload ----------
rule: "Bot/ButtonControl/Reload/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Reload,
            ButtonBehaivor.ReleasePress,
            5,
            10,
            0,
            1
        ]
    );
}

// TAG Bot/ButtonControl/StopButtonControl ----------
rule: "Bot/ButtonControl/StopButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    !enableButtonControlState
){
    StopHoldingButton(ep, Button.PrimaryFire);
    StopHoldingButton(ep, Button.SecondaryFire);
    StopHoldingButton(ep, Button.Ability1);
    StopHoldingButton(ep, Button.Ability2);
    StopHoldingButton(ep, Button.Ultimate);
    StopHoldingButton(ep, Button.Jump);
    StopHoldingButton(ep, Button.Crouch);
    StopHoldingButton(ep, Button.Reload);
}

// TAG Debug ----------
/*
rule: "Debug/Init"
Event.OngoingPlayer
if(
    ep == HostPlayer() &&
    HasSpawned(ep)
){
    CreateHudText(HostPlayer(),
        null,
        <"<0> <1>", "Server Load", ServerLoad()>,
        null,
        Location.Left, -1,
        Color.White, Color.Orange, Color.White,
        HudTextRev.VisibleToAndString, Spectators.VisibleNever);

    CreateHudText(ep,
        null, <"hold Interact button <0> to toggle all bots",
            ButtonGuide(Button.Interact)>, null,
        Location.Left, 0,
        Color.Yellow, Color.Yellow, Color.Yellow,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);

    CreateHudText(ep,
        null, <"press Interact button <0> to spawn one enemy bot",
            ButtonGuide(Button.Interact)>, null,
        Location.Left, 0,
        Color.Yellow, Color.Yellow, Color.Yellow,
        HudTextRev.VisibleToAndString, Spectators.DefaultVisibility);
    
    CreateIcon(spawnPos != 0 ? HostPlayer() : null, spawnPos, Icon.ArrowDown,
            IconRev.VisibleToAndPosition, Color.White, true);

}

globalvar define spawnPos;

define dummyBots:
        FilteredArray(
            AllPlayers(),
            (
                IsDummyBot(ae)
            )
        );

rule: "Debug/TeleportAllStandbyBots"
Event.OngoingPlayer
if(
    HostPlayer() == ep &&
    0 < CountOf(dummyBots)
){
    Teleport(
        FilteredArray(
            AllPlayers(),
            (
                ae.actionState == ActionState.Non &&
                IsDummyBot(ae)
            )
        ),
        NearestWalkablePosition(spawnPos + RandomPosition(5, 0, 5)));
    Wait(2, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}


define maxBotNum: 24;

rule: "Debug/ToggleSpawnAllBots"
Event.OngoingPlayer
if(
    ep == HostPlayer() &&
    IsButtonHeld(ep, Button.Interact)

){
    WaitLongPressConfirm();

    if(0 == CountOf(dummyBots)){
        spawnPos = NearestWalkablePosition(AimingPosition());
        define botNum = WorkshopSettingInteger("Enemy Bot Settings", "Bot Count", 24, 0, 24, 2);
        CreateBothBotsByInput(AllHeroes(), botNum);
    }
    else{
        DestroyBots(Team.Team1);
        DestroyBots(Team.Team2);
    }
}

rule: "Debug/SpawnOneEnemyBots"
Event.OngoingPlayer
if(
    ep == HostPlayer() &&
    IsButtonHeld(ep, Button.Interact)

){
    WaitUntil(!IsButtonHeld(ep, Button.Interact), 0.4);
    AbortIf(IsButtonHeld(ep, Button.Interact));

    spawnPos = NearestWalkablePosition(AimingPosition());
    CreateDummyBot(
        RandomValueInArray(AllHeroes()),
        OppositeTeamOf(TeamOf()),
        botSlot,
        NearestWalkablePosition(AimingPosition()),
        RandomDirection()
    );
    botSlot ++;
}
*/

/*
define debugInfoDisplayeTo:
    [HostPlayer(), ep];

rule: "Debug/ShowDebugInfo"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){  
    // show status text
    CreateInWorldText(
        debugInfoDisplayeTo,
        actionState,
        ep + (Up() * 2), 1, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);
    // show actionState ring
    CreateEffect(
        ep.actionState != ActionState.Non ? debugInfoDisplayeTo : null,
        Effect.Ring,
            actionState == ActionState.Search ?
                Color.White:
                actionState == ActionState.Attack ?
                    Color.Red :
                    Color.Blue,
        ep, 1, EffectRev.VisibleToPositionRadiusAndColor);
    // show target path beam
    CreateBeamEffect(
        (
            (
                (
                    actionState == ActionState.Attack ||
                    actionState == ActionState.Support
                ) &&
                target != 0
            ) ||                
            (
                actionState == ActionState.Search
            )
        )
        ? debugInfoDisplayeTo : null,
        BeamType.BadBeam,
        EyePosition(),
            actionState == ActionState.Attack ||
            target,
            actionState == ActionState.Search ?
                Color.White:
                actionState == ActionState.Attack ?
                    Color.Red :
                    Color.Blue,
        EffectRev.VisibleToPositionRadiusAndColor);
    while(true){
        if(IsBelowAbyss(targetDirBackward)){
            dbgPlyEft(ep);
        }
        Wait(1);
    }
}
*/