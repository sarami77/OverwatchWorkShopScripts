import "Utility.del";
import "Debug.del";

// TAG public property
globalvar define botAimScat = WorkshopSettingReal("Enemy Bot Settings", "Aim Scattering (0 is head shot)",              0.2, 0, 1000, 3);

// TAG Bot/Spawn ----------
enum BotAsset{
    EnemyType,
    hero,
    count
}

// TAG EnemyType ----------
playervar define enemyType;
enum EnemyType{
    None,
    Minion,
    Elite,
    Boss,
    GiantBoss
}

// TAG BotAsset ----------
define RandomHero: RandomValueInArray(AllHeroes());
define RandomTankHero: RandomValueInArray(AllTankHeroes());
define RandomDamageHero: RandomValueInArray(AllDamageHeroes());
define RandomSupportHero: RandomValueInArray(AllSupportHeroes());

globalvar define enemyTeamName! = "";

void CreateBotsByAssetList(define assetList, define team)"CreateBotsByAssetList"{
    DestroyBots(team);
    define asset = ValueInArray(assetList, RandomInteger(0, CountOf(assetList) - 1));
    // set team name
    enemyTeamName = asset[0];
    // create bot by asset
    for(define assetIdx! = 1; assetIdx < CountOf(asset); assetIdx++){
        for(define botCount! = 0; botCount < asset[assetIdx][BotAsset.count]; botCount++){
            // get empty slot
            define slot! = getEmptySlotNo(team);
            while(!HasSpawned(PlayersInSlot(slot, team))){
                // create bot
                CreateDummyBot(
                    asset[assetIdx][BotAsset.hero],
                    team,
                    slot,
                    SpawnPoints(team)[0],
                    RandomDirection()
                );
                Wait(0.1, WaitBehavior.IgnoreCondition);
                WaitUntilServerFree();
            }
            // set Bot type as enemy type
            define bot! = PlayersInSlot(slot, team);
            bot.enemyType = asset[assetIdx][BotAsset.EnemyType];
        }
    }
}

void CreateBotsByHeroList(define heroList, define num, define team)"CreateBotsByHeroList"{
    DestroyBots(team);
    Wait(0.1, WaitBehavior.IgnoreCondition);

    // create bot by asset
    for(define slotIdx = 0; slotIdx < num; slotIdx++){
        // get empty slot
        define slot! = getEmptySlotNo(team);
        while(!HasSpawned(PlayersInSlot(slot, team))){
            // create bot
            CreateDummyBot(
                RandomValueInArray(heroList),
                team,
                slot,
                SpawnPoints(team)[0],
                RandomDirection()
            );
            Wait(0.1, WaitBehavior.IgnoreCondition);
            WaitUntilServerFree();
        }
    }
}

void CreateBotsByHeroInput(define hero, define num, define team)"CreateBotsByHeroInput"{
    // create bot by asset
    for(define slotIdx = 0; slotIdx < num; slotIdx++){
        // get empty slot
        define slot! = getEmptySlotNo(team);
        // create bot
        CreateDummyBot(
            hero,
            team,
            slot,
            SpawnPoints(team)[0],
            RandomDirection()
        );
        Wait(0.1, WaitBehavior.IgnoreCondition);
        WaitUntilServerFree();
    }
}

define builtinMaxSlotNum: 24;

void DestroyBots(define team!)"DestroyBots"{
    for(define slotIdx = 0; slotIdx < builtinMaxSlotNum; slotIdx ++){
        DestroyDummyBot(
            team, slotIdx);
        Wait(0.01, WaitBehavior.IgnoreCondition);
    }
}

Number getEmptySlotNo(define team!)"getEmptySlotNo"{
    for(define slotNo! = 0; slotNo < builtinMaxSlotNum; slotNo++){
        if(PlayersInSlot(slotNo, team) == null) {
            return slotNo;
        }
    }
    dbgMsg("getEmptySlotNo not found empty slot");
    return -1;
}

// TAG Bot/SetBotInfo ----------

playervar define heroName!;
playervar define healingPrioloty!;
playervar define supportButtons!;
playervar define attackButtons!;

playervar define primaryFireButtonBehaivor;
playervar define primaryFireButtonReleaseMin;
playervar define primaryFireButtonReleaseMax;
playervar define primaryFireButtonPressMin;
playervar define primaryFireButtonPressMax;
playervar define secondaryFireButtonBehaivor;
playervar define secondaryFireButtonReleaseMin;
playervar define secondaryFireButtonReleaseMax;
playervar define secondaryFireButtonPressMin;
playervar define secondaryFireButtonPressMax;
playervar define ability1ButtonReleaseMin;
playervar define ability1ButtonReleaseMax;
playervar define ability1ButtonPressMin;
playervar define ability1ButtonPressMax;
playervar define ability2ButtonReleaseMin;
playervar define ability2ButtonReleaseMax;
playervar define ability2ButtonPressMin;
playervar define ability2ButtonPressMax;
playervar define ultimateButtonReleaseMin;
playervar define ultimateButtonReleaseMax;
playervar define ultimateButtonPressMin;
playervar define ultimateButtonPressMax;

playervar define actionRange!;
playervar define actionRangeMin!;
playervar define actionRangeMax!;

rule: "Bot/SetActionRoutine"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
    SetHeroActionRoutine();
}

rule: "Bot/SetActionRoutine"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned() &&
    HeroOf() == Hero.Echo &&
    IsUsingUltimate()
){
    Wait(1, WaitBehavior.AbortWhenFalse);
    SetHeroActionRoutine();
}

void SetHeroActionRoutine()"SetHeroActionRoutine"{
        target = 0;

        actionRange = ActionRange.Middle;
        heroName = "Default";
        healingPrioloty = 0;
        supportButtons = [];
        attackButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];

        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 2.5;
        primaryFireButtonPressMin = 0;
        primaryFireButtonPressMax = 5;

        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonReleaseMin = 0;
        secondaryFireButtonReleaseMax = 2.5;
        secondaryFireButtonPressMin = 0;
        secondaryFireButtonPressMax = 5;

        ability1ButtonReleaseMin = 0;
        ability1ButtonReleaseMax = 5;
        ability1ButtonPressMin = 0;
        ability1ButtonPressMax = 1;

        ability2ButtonReleaseMin = 0;
        ability2ButtonReleaseMax = 5;
        ability2ButtonPressMin = 0;
        ability2ButtonPressMax = 1;

        ultimateButtonReleaseMin = 0;
        ultimateButtonReleaseMax = 5;
        ultimateButtonPressMin = 0;
        ultimateButtonPressMax = 1;

    switch(HeroOf()){
        case Hero.Ana:
        heroName = "Ana";
        actionRange = ActionRange.Far;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 10;
        secondaryFireButtonPressMax = 15;
        healingPrioloty = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability2
        ];
        break;

        case Hero.Ashe:
        heroName = "Ashe";
        actionRange = ActionRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleaseRepeat;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 10;
        secondaryFireButtonPressMax = 10;
        break;

        case Hero.Baptiste:
        heroName = "Baptiste";
        actionRange = ActionRange.Far;
        healingPrioloty = 100;
        supportButtons = [
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        attackButtons = [
            Button.PrimaryFire,
            Button.Ability1
        ];
        break;

        case Hero.Bastion:
        heroName = "Bastion";
        actionRange = ActionRange.Far;
        secondaryFireButtonReleaseMin = 10;
        secondaryFireButtonReleaseMax = 15;
        ability1ButtonReleaseMin = 5;
        ability1ButtonReleaseMax = 20;
        ability1ButtonPressMin = 0.25;
        ability1ButtonPressMax = 0.25;
        ultimateButtonReleaseMax = 40;
        break;

        case Hero.Brigitte:
        heroName = "Brigitte";
        actionRange = ActionRange.Melee;
        secondaryFireButtonReleaseMax = 10;
        secondaryFireButtonPressMin = 3;
        secondaryFireButtonPressMax = 6;
        primaryFireButtonPressMax = 10;
        primaryFireButtonReleaseMax = 1;
        ability2ButtonPressMax = 1;
        ability2ButtonReleaseMax = 1;
        ultimateButtonReleaseMax = 30;
        healingPrioloty = 75;
        supportButtons = [
            Button.SecondaryFire,
            Button.Ability2
        ];
        break;

        case Hero.Doomfist:
        heroName = "Doomfist";
        actionRange = ActionRange.Melee;
        primaryFireButtonReleaseMax = 3;
        secondaryFireButtonPressMin = 2;
        secondaryFireButtonPressMax = 2.5;
        break;

        case Hero.Dva:
        heroName = "Dva";
        actionRange = ActionRange.Close;
        ultimateButtonReleaseMax = 40;
        break;

        case Hero.Echo:
        heroName = "Echo";
        actionRange = ActionRange.Middle;
        break;

        case Hero.Genji:
        heroName = "Genji";
        actionRange = ActionRange.Melee;
        break;

        case Hero.Hanzo:
        heroName = "Hanzo";
        actionRange = ActionRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMax = 1;
        primaryFireButtonPressMin = 0.75;
        primaryFireButtonPressMax = 1.25;
        secondaryFireButtonBehaivor = ButtonBehaivor.Non;
        break;

        case Hero.Junkrat:
        heroName = "Junkrat";
        actionRange = ActionRange.Middle;
        break;

        case Hero.Lucio:
        heroName = "Lucio";
        actionRange = ActionRange.Middle;
        ultimateButtonReleaseMax = 15;
        healingPrioloty = 25;
        supportButtons = [
            Button.Ability1,
            Button.Ability2
        ];
        break;

        case Hero.Cassidy:
        heroName = "Cassidy";
        actionRange = ActionRange.Close;
        primaryFireButtonReleaseMin = 0;
        primaryFireButtonReleaseMax = 2;
        secondaryFireButtonReleaseMax = 20;
        break;

        case Hero.Mei:
        heroName = "Mei";
        actionRange = ActionRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        ultimateButtonReleaseMax = 30;
        break;

        case Hero.Mercy:
        heroName = "Mercy";
        actionRange = ActionRange.Far;
        primaryFireButtonReleaseMax = 1;
        secondaryFireButtonReleaseMax = 10;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 1;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 1;
        healingPrioloty = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        break;

        case Hero.Moira:
        heroName = "Moira";
        actionRange = ActionRange.Middle;
        healingPrioloty = 100;
        supportButtons = [
            Button.PrimaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        attackButtons = [
            Button.SecondaryFire,
            Button.Ability1,
            Button.Ability2
        ];
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        break;

        case Hero.Orisa:
        heroName = "Orisa";
        actionRange = ActionRange.Far;
        break;

        case Hero.Pharah:
        heroName = "Pharah";
        actionRange = ActionRange.Middle;
        break;

        case Hero.Reaper:
        heroName = "Reaper";
        actionRange = ActionRange.Close;
        ability2ButtonReleaseMax = 10;
        break;

        case Hero.Reinhardt:
        heroName = "Reinhardt";
        actionRange = ActionRange.Melee;
        secondaryFireButtonReleaseMax = 15;
        secondaryFireButtonPressMin = 3;
        secondaryFireButtonPressMax = 6;
        primaryFireButtonPressMax = 20;
        primaryFireButtonReleaseMax = 1;
        break;

        case Hero.Roadhog:
        heroName = "Reaper";
        actionRange = ActionRange.Close;
        break;

        case Hero.Sigma:
        heroName = "Sigma";
        actionRange = ActionRange.Middle;
        primaryFireButtonReleaseMax = 0;
        secondaryFireButtonReleaseMax = 5;
        secondaryFireButtonPressMax = 1;
        ultimateButtonReleaseMax = 25;
        break;

        case Hero.Soldier76:
        heroName = "Soldier76";
        actionRange = ActionRange.Middle;
        break;

        case Hero.Sombra:
        heroName = "Sombra";
        actionRange = ActionRange.Close;
        secondaryFireButtonPressMin = 1.5;
        ability2ButtonReleaseMax = 10;
        ability2ButtonPressMax = 0.5;
        break;

        case Hero.Symmetra:
        heroName = "Symmetra";
        actionRange = ActionRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 1;
        secondaryFireButtonPressMax = 3;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 3;
        ability2ButtonReleaseMax = 30;
        break;

        case Hero.Torbjorn:
        heroName = "Torbjorn";
        actionRange = ActionRange.Middle;
        break;

        case Hero.Tracer:
        heroName = "Tracer";
        actionRange = ActionRange.Close;
        ultimateButtonReleaseMin = 1;
        ultimateButtonReleaseMax = 1;
        break;

        case Hero.Widowmaker:
        heroName = "Widowmaker";
        actionRange = ActionRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 1;
        primaryFireButtonReleaseMax = 1.5;
        primaryFireButtonPressMin = 0;
        primaryFireButtonPressMax = 1;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 20;
        secondaryFireButtonPressMax = 30;
        ability1ButtonReleaseMin = 20;
        ability1ButtonReleaseMax = 30;
        ability2ButtonReleaseMin = 20;
        ability2ButtonReleaseMax = 30;
        break;

        case Hero.Winston:
        heroName = "Winston";
        actionRange = ActionRange.Melee;
        ultimateButtonReleaseMax = 40;
        break;

        case Hero.WreckingBall:
        heroName = "WreckingBall";
        actionRange = ActionRange.Close;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMax = 15;
        primaryFireButtonPressMax = 20;
        secondaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        secondaryFireButtonPressMin = 6;
        secondaryFireButtonPressMax = 15;
        ability1ButtonReleaseMin = 5;
        ability1ButtonReleaseMax = 10;
        break;

        case Hero.Zarya:
        heroName = "Zarya";
        actionRange = ActionRange.Middle;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        ability2ButtonReleaseMin = 0.5;
        ability2ButtonReleaseMax = 0.5;
        break;

        case Hero.Zenyatta:
        heroName = "Zenyatta";
        actionRange = ActionRange.Far;
        primaryFireButtonBehaivor = ButtonBehaivor.ReleasePress;
        primaryFireButtonReleaseMin = 1;
        secondaryFireButtonReleaseMin = 1;
        ability1ButtonReleaseMin = 1;
        ability1ButtonReleaseMax = 1;
        ability1ButtonPressMin = 1;
        ability1ButtonPressMax = 1;
        ability2ButtonReleaseMin = 1;
        ability2ButtonReleaseMax = 1;
        ability2ButtonPressMin = 1;
        ability2ButtonPressMax = 1;
        ultimateButtonReleaseMax = 30;
        healingPrioloty = 25;
        supportButtons = [
            Button.Ability1,
            Button.Ability2
        ];
        break;
    }

    // set Attack range
    switch(actionRange){
        case ActionRange.Melee:
            actionRangeMin = 0;
            actionRangeMax = 4;
            break;
        case ActionRange.Close:
            actionRangeMin = 0;
            actionRangeMax = 8;
            break;
        case ActionRange.Middle:
            actionRangeMin = 8;
            actionRangeMax = 16;
            break;
        case ActionRange.Far:
            actionRangeMin = 16;
            actionRangeMax = 30;
            break;
    }
}

enum ActionState{
    Non,
    Changing,
    Attack,
    Support
}

enum ActionRange{
    Non,
    Melee,
    Close,
    Middle,
    Far
}

enum ButtonBehaivor{
    Non,
    ReleasePress,
    ReleaseRepeat
}

// TAG Bot/ActionState/StateControl ----------
define watchUpdateRate:
    isServerOverLoading() ? 4 : 2;

define FindAttackTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(OppositeTeamOf(TeamOf())),
                (
                    HasSpawned(ae) &&
                    IsAlive(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock) &&
                    !(
                        IsStealth(ae) &&
                        DistanceBetween(ae, ep) >= 3
                    )
                )
            ),
            DistanceBetween(ae, ep)
        )
    );

define FindSupportTarget():
    FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(TeamOf()),
                (
                    ae != ep &&
                    ProbabilityTrue((1 - NormalizedHealth(ae)) * healingPrioloty) &&
                    HasSpawned(ae) &&
                    IsAlive(ae) &&
                    IsInLineOfSight(ep, ae, BarrierLOS.NoBarriersBlock)
                )
            ),
            Health(ae) - MaxHealth(ae)
        )
    );

define changeTargetProbForAttack: 50;

rule: "Bot/ActionState/StateControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    HasSpawned()
){
    // distribute serverload
    if(isServerOverLoading()){
        Wait(RandomReal(0, 1), WaitBehavior.AbortWhenFalse);
    }

    // get next state
    define nextActionState!;
    // is dead
    if(IsDead()){
        // Non state by dead
        nextActionState = ActionState.Non; 
    }
    else
    { 
        // get targetSighted
        define targetSighted! =
            target != 0 &&
            HasSpawned(target) &&
            IsAlive(target) &&
            IsInLineOfSight(ep, target, BarrierLOS.NoBarriersBlock) &&
            !(
                IsStealth(target) &&
                DistanceBetween(target, ep) >= 3
            );

        define tryGetTarget! = false;
        // target not Sighted
        if(!targetSighted){
            tryGetTarget = true;
        }
        // is support role hero
        else if(0 < healingPrioloty){
            if(actionState == ActionState.Attack){
                // change target dependent on attack target helth & high healing prioloty
                if(
                    ProbabilityTrue(NormalizedHealth(target) * healingPrioloty * 2.0)
                ){
                    tryGetTarget = true;
                }
            }
            else if(actionState == ActionState.Support){
                // change target dependent on support target helth & low healing prioloty
                if(
                    ProbabilityTrue(NormalizedHealth(target) * healingPrioloty * 0.5)
                ){
                    tryGetTarget = true;
                }
                // heal completed
                else if(
                    NormalizedHealth(target) >= 1
                ){
                    tryGetTarget = true;
                }
            }
        }
        // is not support role hero
        else{
            // change target dependent on attack target helth & attack prob
            if(
                ProbabilityTrue(NormalizedHealth(target) * changeTargetProbForAttack)
            ){
                tryGetTarget = true;
            }
        }

        // get new target
        if(tryGetTarget){
            // is support role hero
            if(0 < healingPrioloty)
            {
                // find support target
                target = FindSupportTarget();
                // not found heal target
                if(target == 0){
                    // find attack target
                    target = FindAttackTarget();
                }
            }
            else{
                // find attack target
                target = FindAttackTarget();
            }
        }

        // has target
        if(
            target != 0 && target != null
        ){
            // dbgPlyBemEft(EyePosition(), target);

            // target is enemy
            if(TeamOf() != TeamOf(target)){
                // Attack state
                nextActionState = ActionState.Attack; 
            }
            else{
                // Support state
                nextActionState = ActionState.Support; 
            }
        }
        else{
            // Non state by target not found
            nextActionState = ActionState.Non;
        }       
    }

    // state changed
    if(nextActionState != actionState){
        // next state is not Non state
        if(nextActionState != ActionState.Non){
            // set Non state for restart button control
            actionState = ActionState.Changing;
            WaitChangeState();
        }
        // set next state
        actionState = nextActionState;
    }

    Wait(actionState == ActionState.Non ? watchUpdateRate / 2 : watchUpdateRate, WaitBehavior.AbortWhenFalse);
    WaitUntilServerFree();
    LoopIfConditionIsTrue();
}

rule: "Bot/ActionState/ChangeTargetByTargetDead"
Event.OnElimination
if(
    IsDummyBot() &&
    Victim() == target
){
    // is support role hero
    if(0 < healingPrioloty)
    {
        // find support target
        target = FindSupportTarget();
        // not found heal target
        if(target == 0){
            // find attack target
            target = FindAttackTarget();
        }
    }
    else{
        // find attack target
        target = FindAttackTarget();
    }
}

rule: "Bot/ActionState/ChangeTargetByDamageTaken"
Event.OnDamageTaken
if(
    IsDummyBot() &&
    Attacker() != target
){
    Wait(0.25, WaitBehavior.RestartWhenTrue);
    if(
        // no target
        target == 0 ||
        // change target dependent on attack target helth & attack prob
        ProbabilityTrue(NormalizedHealth(target) * changeTargetProbForAttack)
    ){
        // set attacker to target
        target = Attacker();
        actionState = ActionState.Attack;
    }
}

playervar define actionState;
playervar define prevActionState;
playervar define statusText;
playervar define target;
playervar define moveDir;
playervar define sideStepDir;

void StopMove()"StopMove"{
    StopThrottleInDirection();
} 

void StartMove(){
    if(moveDir != 0 && sideStepDir != 0){
        StartMoveInDir(moveDir + sideStepDir);
    }
    else if(moveDir == 0 && sideStepDir != 0){
        StartMoveInDir(sideStepDir);
    }
    else if(moveDir != 0 && sideStepDir == 0){
        StartMoveInDir(moveDir);
    }
    else{
        StopMove();
    }
}

void StartMoveInDir(define dir){
    StartThrottleInDirection(
        ep, dir, 1,
        Relative.ToPlayer,
        ThrottleBehavior.ReplaceExistingThrottle,
        ThrottleRev.DirectionAndMagnitude
    );
}

define stuckRange: 0.25;

playervar define prevPosition;

playervar define isInStuck;

void checkAndBreakStuck() playervar "checkAndBreakStuck"{
    if(
        (   // has move
            sideStepDir != 0 ||
            moveDir != 0
        ) &&
        prevPosition != 0 &&
        DistanceBetween(ep, prevPosition) <= stuckRange    // not moved from prevPosition
    ){
        statusText = <"checkAndBreakStuck">;
        while(
            // keep try break stuck prob
            ProbabilityTrue(75) &&
            // not moved from prevPosition yet
            DistanceBetween(ep, prevPosition) <= stuckRange
        ){
            isInStuck = true;
            //dbgPlyEft(ep);
            // try jump or croch
            define breakStuckButton! = RandomValueInArray([Button.Jump, Button.Crouch]);
            StartHoldingButton(ep, breakStuckButton);
            Wait(RandomReal(0.5, 1.0), WaitBehavior.IgnoreCondition);   // keep hold button
            StopHoldingButton(ep, breakStuckButton);
        }
    }
    else{
        isInStuck = false;
        prevPosition = PositionOf();
    }
}

void setMoveDir(define forwardIsAbyss!, define backwardIsAbyss!) playervar "setMoveDir"{
    // set move direction
    define distToTarget! = DistanceBetween(ep, target);

    // select dir
    // attack state
    if(actionState == ActionState.Attack){
        // target is far
        if(
            distToTarget > actionRangeMax
        ){
            moveDir = Forward();
        }
        // target is close
        else if(
            distToTarget < actionRangeMin
        ){
            moveDir = Backward();
        }
        else{
            // no moveDir
            moveDir = 0;
        }
    }
    // support state
    else if(actionState == ActionState.Support){
        // target is far
        if(
            distToTarget > (actionRangeMax * 0.75)
        ){
            moveDir = Forward();
        }
        // target is close
        else if(
            distToTarget < (actionRangeMin * 0.75)
        ){
            moveDir = Backward();
        }
        else{
            // no moveDir
            moveDir = 0;
        }
    }

    // check abyss
    // can't forward
    if(forwardIsAbyss && moveDir == Forward()){
        moveDir = 0;
    }
    // can't backward
    else if(backwardIsAbyss && moveDir == Backward()){
        moveDir = 0;
    }
}

void setSideStep(define rightIsAbyss!, define leftIsAbyss!) playervar "setSideStep"{
    // by own range or health
    if(
        ProbabilityTrue(((1 - NormalizedHealth()) * 100) + 25)
    ){
        // start sidestep
        // already has sidestep
        if(sideStepDir != 0){
            if(ProbabilityTrue(30)){
                // reverse sidestep
                sideStepDir = -sideStepDir;
            }
            //else
        }
        else{
            if(ProbabilityTrue(50)){
                // right sidestep
                sideStepDir = Right();
            }
            else{
                // left sidestep
                sideStepDir = Left();
            }
        }

        // check abyss
        // first check right prob
        if(ProbabilityTrue(50)){
            // can't right
            if(rightIsAbyss && sideStepDir == Right()){
                sideStepDir = Left();
            }
            // can't left too
            if(leftIsAbyss && sideStepDir == Left()){
                sideStepDir = 0;
            }
        }
        else{
            // can't left
            if(leftIsAbyss && sideStepDir == Left()){
                sideStepDir = Right();
            }
            // can't right too
            if(rightIsAbyss && sideStepDir == Right()){
                sideStepDir = 0;
            }
        }
    }
    else{
        // stop sidestep
        sideStepDir = 0;
    }
}

define aimingPosition:
    IsInAlternateForm(target) ?
    EyePosition(target) - Vector(0, 0.7, 0):
    EyePosition(target) + Vector(0, 0.2, 0);

void StartFacingTarget(){
    if(
        target != 0 &&
        target != null &&
        IsAlive(target)
    ){
        StartFacing(
            ep,
            DirectionTowards(
                EyePosition(),
                aimingPosition + RandomPosition(botAimScat, botAimScat, botAimScat)
            ),
            250,
            Relative.ToWorld, FacingRev.DirectionAndTurnRate
        );
    }
}

// TAG Bot/ActionState/Non ----------
rule: "Bot/ActionState/Non"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Non
){
    statusText = <"Non">;

    // hero unique action
    heroAction();

    moveDir = 0;
    sideStepDir = 0;
    target = 0;

    StopMove();
    StopFacing();
    Wait(0.1, WaitBehavior.AbortWhenFalse);

    while(actionState == ActionState.Non){
        // set initial face direction
        SetFacing(
            ep,
            DirectionTowards(ep, ClosestPlayerTo(ep, EnemyTeamOf())),
            Relative.ToWorld);

        Wait(watchUpdateRate * 2, WaitBehavior.AbortWhenFalse);
        WaitUntilServerFree();
    }    // action loop
}

// TAG Bot/ActionState/Attack ----------
rule: "Bot/ActionState/Attack"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Attack
){
    statusText = <"Attack">;

    // start facing direction
    StartFacingTarget();
    Wait(0.1, WaitBehavior.AbortWhenFalse);
    
    while(actionState == ActionState.Attack){
        // hero unique action
        heroAction();

        // check abyss
        define distance! = walkMeterPerSec * watchUpdateRate;
        define fCheckPos! = BlockedPositonByLocalVector(Forward(), distance);
        define fAbyss! = IsBelowAbyss(fCheckPos);
        define bCheckPos! = BlockedPositonByLocalVector(Backward(), distance);
        define bAbyss! = IsBelowAbyss(bCheckPos);

        // set moveDir direction
        setMoveDir(fAbyss, bAbyss);

        // check abyss
        define rCheckPos! = BlockedPositonByLocalVector(Right(), distance);
        define rAbyss! = IsBelowAbyss(rCheckPos);
        define lCheckPos! = BlockedPositonByLocalVector(Left(), distance);
        define lAbyss! = IsBelowAbyss(lCheckPos);

        // set sidestep direction
        setSideStep(rAbyss, lAbyss);

        // start move by moveDir and sideStepDir
        StartMove();

        // check stuck
        checkAndBreakStuck();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
        WaitUntilServerFree();
    }    // action loop

}

// TAG Bot/ActionState/Support ----------
rule: "Bot/ActionState/Support"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    actionState == ActionState.Support
){
    statusText = <"Support">;

    // start facing direction
    StartFacingTarget();
    Wait(0.1, WaitBehavior.AbortWhenFalse);

    // action loop
    while(actionState == ActionState.Support){
        // hero unique action
        heroAction();
    
        // check abyss
        define distance! = walkMeterPerSec * watchUpdateRate;
        define fCheckPos! = BlockedPositonByLocalVector(Forward(), distance);
        define fAbyss! = IsBelowAbyss(fCheckPos);
        define bCheckPos! = BlockedPositonByLocalVector(Backward(), distance);
        define bAbyss! = IsBelowAbyss(bCheckPos);
    
        // set moveDir direction
        setMoveDir(fAbyss, bAbyss);

        // no sidestepDir
        sideStepDir = 0;

        // start move by moveDir and sideStepDir
        StartMove();
        
        // check stuck
        checkAndBreakStuck();

        Wait(watchUpdateRate, WaitBehavior.AbortWhenFalse);
        WaitUntilServerFree();
    }
}

// TAG Bot/HeroUniqueAction ----------
define MercyStaff: 1;
define MercyPistol: 2;

void heroAction(){
    // common action
    // melee
    while(
        actionState == ActionState.Attack &&
        IsAlive(target) &&
        Health(target) <= (25 * 2) &&
        DistanceBetween(ep, target) <= 2.5
    ){
        PressButton(ep, Button.Melee);
        Wait(0.5, WaitBehavior.AbortWhenFalse);
    }

    // reload
    if(
        actionState == ActionState.Non ||
        (MaxAmmo(ep, 0) > 0 && Ammo(ep, 0) <= 0) ||
        (MaxAmmo(ep, 1) > 0 && Ammo(ep, 1) <= 0)
    ){
        PressButton(ep, Button.Reload);
    }

    // hero unique action
    else if(
        actionState != ActionState.Non
    ){
        // Mercy's change weapon
        if(HeroOf() == Hero.Mercy){
            if(actionState == ActionState.Attack){
                SetWeapon(ep, MercyPistol);
            }
            else{
                SetWeapon(ep, MercyStaff);
            }
        }
        
        else if(HeroOf() == Hero.WreckingBall){
            // WreckingBall's jump for pile driver
            if(
                AbilityCooldown(ep, Button.Crouch) <= 0 &&
                IsButtonHeld(ep, Button.Crouch) &&
                IsOnGround(ep)
            ){
                ApplyImpulse(ep, Up(),
                    10, Relative.ToWorld, ContraryMotion.Cancel);
            }

            // WreckingBall's accsell for ball tackle
            while(IsFiringSecondary(ep)){
                // for ball charge
                define boostDir! = RandomValueInArray([Right(), Forward(), Backward(), Left()]);
                ApplyImpulse(ep, boostDir,
                    30, Relative.ToWorld, ContraryMotion.Cancel);
                Wait(0.5, WaitBehavior.AbortWhenFalse);
            }
        }
    }
}


// TAG Bot/ButtonControl ----------
enum ButtonControl{
    button,
    buttonOperation,
    releaseMin,
    releaseMax,
    pressMin,
    pressMax
}

define noShooting: false;   // for debug

playervar define isRepeating;
playervar define repeatMin;
playervar define repeatMax;

Boolean isMatchButton(define matchButton!, define butttons!){
    return IsTrueForAny(butttons, ae == matchButton);
}

define buttonControlTimeScale: 1;

void StartButtonControl(define bc){
    AbortIf(
        noShooting &&
        (
            bc[ButtonControl.button] == Button.PrimaryFire ||   
            bc[ButtonControl.button] == Button.SecondaryFire ||
            bc[ButtonControl.button] == Button.Ability1 ||
            bc[ButtonControl.button] == Button.Ability2 ||
            bc[ButtonControl.button] == Button.Ultimate
        )
    );

    define startButton = false;
    define fireAndAbilityButtons = [Button.PrimaryFire, Button.SecondaryFire, Button.Ability1, Button.Ability2];
    // if fire or ability button
    if(
        isMatchButton(bc[ButtonControl.button], fireAndAbilityButtons)
    ){
        // if match attack buttons
        if(
            actionState == ActionState.Attack &&
            isMatchButton(bc[ButtonControl.button], attackButtons)
        ){
            startButton = true;
        }
        // if match Support buttons
        else if(
            actionState == ActionState.Support &&
            isMatchButton(bc[ButtonControl.button], supportButtons)
        ){
            startButton = true;
        }
    }
    else{
        startButton = true;
    }

    // is not start button
    if(!startButton){
        // stop button
        StopHoldingButton(ep, bc[ButtonControl.button]);
    }
    else{
        // start button
        if(bc[ButtonControl.buttonOperation] == ButtonBehaivor.ReleasePress){
            while(true){
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.releaseMin], bc[ButtonControl.releaseMax]), WaitBehavior.AbortWhenFalse);

                StartHoldingButton(ep, bc[ButtonControl.button]);
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.pressMin], bc[ButtonControl.pressMax]), WaitBehavior.AbortWhenFalse);

                StopHoldingButton(ep, bc[ButtonControl.button]);
            }
        }
        else if(bc[ButtonControl.buttonOperation] == ButtonBehaivor.ReleaseRepeat){
            repeatMin = bc[ButtonControl.pressMin];
            repeatMax = bc[ButtonControl.pressMax];
            while(true){
                Wait(buttonControlTimeScale * RandomReal(bc[ButtonControl.releaseMin], bc[ButtonControl.releaseMax]), WaitBehavior.AbortWhenFalse);

                isRepeating = true;
                while(isRepeating){
                    PressButton(ep, bc[ButtonControl.button]); 
                    Wait(0.2, WaitBehavior.AbortWhenFalse);
                }
            }
        }
        // else
    }
}

rule: "Bot/ButtonControl/releaseIsRepeating"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    isRepeating
){
    Wait(RandomReal(repeatMin, repeatMax), WaitBehavior.AbortWhenFalse);
    isRepeating = false;
}

// TAG Bot/ButtonControl/PrimaryFire ----------
define enableButtonControlState:
    actionState == ActionState.Attack ||
    actionState == ActionState.Support;

rule: "Bot/ButtonControl/PrimaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.PrimaryFire,
            primaryFireButtonBehaivor,
            primaryFireButtonReleaseMin,
            primaryFireButtonReleaseMax,
            primaryFireButtonPressMin,
            primaryFireButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/SecondaryFire ----------
rule: "Bot/ButtonControl/SecondaryFire/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.SecondaryFire,
            secondaryFireButtonBehaivor,
            secondaryFireButtonReleaseMin,
            secondaryFireButtonReleaseMax,
            secondaryFireButtonPressMin,
            secondaryFireButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ability1 ----------
rule: "Bot/ButtonControl/Ability1/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Ability1,
            ButtonBehaivor.ReleasePress,
            ability1ButtonReleaseMin,
            ability1ButtonReleaseMax,
            ability1ButtonPressMin,
            ability1ButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ability2 ----------
rule: "Bot/ButtonControl/Ability2/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Ability2,
            ButtonBehaivor.ReleasePress,
            ability2ButtonReleaseMin,
            ability2ButtonReleaseMax,
            ability2ButtonPressMin,
            ability2ButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Ultimate ----------
rule: "Bot/ButtonControl/Ultimate/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState &&
    UltimateChargePercent() >= 100
){
    StartButtonControl(
        [
            Button.Ultimate,
            ButtonBehaivor.ReleasePress,
            ultimateButtonReleaseMin,
            ultimateButtonReleaseMax,
            ultimateButtonPressMin,
            ultimateButtonPressMax
        ]
    );
}

// TAG Bot/ButtonControl/Jump ----------
rule: "Bot/ButtonControl/Jump/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Jump,
            ButtonBehaivor.ReleasePress,
            0,
            7.5,
            0.5,
            5
        ]
    );
}

// TAG Bot/ButtonControl/Crouch ----------
rule: "Bot/ButtonControl/Crouch/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Crouch,
            ButtonBehaivor.ReleasePress,
            5,
            15,
            0.5,
            2.5
        ]
    );
}

// TAG Bot/ButtonControl/Reload ----------
/*
disabled rule: "Bot/ButtonControl/Reload/StartButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    enableButtonControlState
){
    StartButtonControl(
        [
            Button.Reload,
            ButtonBehaivor.ReleasePress,
            5,
            15,
            0,
            1
        ]
    );
}
*/

// TAG Bot/ButtonControl/StopButtonControl ----------
rule: "Bot/ButtonControl/StopButtonControl"
Event.OngoingPlayer
if(
    IsDummyBot() &&
    !enableButtonControlState
){
    StopHoldingButton(ep, Button.PrimaryFire);
    StopHoldingButton(ep, Button.SecondaryFire);
    StopHoldingButton(ep, Button.Ability1);
    StopHoldingButton(ep, Button.Ability2);
    StopHoldingButton(ep, Button.Ultimate);
    StopHoldingButton(ep, Button.Jump);
    StopHoldingButton(ep, Button.Crouch);
    // StopHoldingButton(ep, Button.Reload);
}

// TAG Debug ----------
