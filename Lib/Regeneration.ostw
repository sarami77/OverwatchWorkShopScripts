import "Utility.ostw";

// Function enable team
// Set team in import destination parent file
globalvar define Regeneration_enableTeam;
globalvar define Regeneration_needBalanceTeam1;
globalvar define Regeneration_needBalanceTeam2;
globalvar define Regeneration_regenerationRateTeam1;
globalvar define Regeneration_regenerationRateTeam2;
globalvar define Regeneration_waitRegenTimeTeam1;
globalvar define Regeneration_waitRegenTimeTeam2;

define Regeneration_isEnableTeam:
    Regeneration_enableTeam == TeamOf() ||
    Regeneration_enableTeam == Team.All;
globalvar define playerRegenerationRateTeam1;
globalvar define playerRegenerationRateTeam2;
playervar define waitRegeneration;

rule: "Player/Regeneration/updateRate //if there are few players, incrase regeneration rate for fair play."
Event.OngoingGlobal
{
    // If don't enough players, adjust player regeneration rate to make the game fair
    WaitUpdateLongCycle();
    // Multiply heal unit
    playerRegenerationRateTeam1 = (Regeneration_regenerationRateTeam1 / 5);
    playerRegenerationRateTeam2 = (Regeneration_regenerationRateTeam2 / 5);
    // Apply balance multiplier
    if(Regeneration_needBalanceTeam1)
        playerRegenerationRateTeam1 *= ConvertToBalancedValue(0.75, Team.Team1);  // TAG player regeneration scale
    else if(Regeneration_regenerationRateTeam2)
        playerRegenerationRateTeam2 *= ConvertToBalancedValue(0.75, Team.Team2);  // TAG player regeneration scale
    LoopIfConditionIsTrue();
}

rule: "Player/Regeneration/waitRegenerate //by taken damage"
Event.OnDamageTaken
if(
    Regeneration_isEnableTeam &&
    IsAlive(EventPlayer())
){
    // Stop regeneration when player receives damage
    // Resume regeneration over time
    waitRegeneration = true;
    if(TeamOf() == Team.Team1)
        Wait(Regeneration_waitRegenTimeTeam1, WaitBehavior.RestartWhenTrue);  // TAG wait regenenation timer
    else if(TeamOf() == Team.Team2)
        Wait(Regeneration_waitRegenTimeTeam2, WaitBehavior.RestartWhenTrue);
    waitRegeneration = false;
}

rule: "Player/Regeneration/regenerate //heal if not damaged for a short time"
Event.OngoingPlayer
if(
    Regeneration_isEnableTeam &&
    IsAlive(EventPlayer()) &&
    !waitRegeneration &&
    Health(EventPlayer()) < MaxHealth(EventPlayer())
){
    // Heal until the player's health is maximized
    WaitUpdateShortCycle();
    if(TeamOf() == Team.Team1)
        Heal(EventPlayer(), null, playerRegenerationRateTeam1);
    else if(TeamOf() == Team.Team2)
        Heal(EventPlayer(), null, playerRegenerationRateTeam2);
    LoopIfConditionIsTrue();
}

