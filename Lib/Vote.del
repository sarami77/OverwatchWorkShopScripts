import "Debug.del";

// TAG Vote ----------
playervar define voteGameDifficulty! = GameDifficulty.NO_VOTE;

enum GameDifficulty{
    INIT,
    NO_VOTE,
    EASY,
    NORMAL,
    HARD,
    EXPERT,
    LEGENDARY,
    INSANITY,
    LAST
}

define DifficultyText(in define inputDifficulty!):
    inputDifficulty == GameDifficulty.NO_VOTE ?     "NO VOTE" :
    inputDifficulty == GameDifficulty.EASY ?        "EASY" :
    inputDifficulty == GameDifficulty.NORMAL ?      "NORMAL" :
    inputDifficulty == GameDifficulty.HARD ?        "HARD" :
    inputDifficulty == GameDifficulty.EXPERT ?      "EXPERT" :
    inputDifficulty == GameDifficulty.LEGENDARY ?   "LEGENDARY" :
    inputDifficulty == GameDifficulty.INSANITY ?    "HEROES MUST DIE!" :
    null;

define VoteGameDifficultyText(define inputDifficulty!) playervar "VoteGameDifficultyText"{
    return DifficultyText(inputDifficulty);
}

define ConfirmGameDifficultyText(define inputDifficulty!) globalvar "ConfirmGameDifficultyText"{
    return DifficultyText(inputDifficulty);
}

define VD_AscKey: Button.Reload;
define VD_DisKey: Button.Interact;

playervar define voteOpeText!;

void UpdateVoteOperationText()"UpdateVoteOperationText"{
    DestroyHudText(voteOpeText[0]);
    DestroyHudText(voteOpeText[1]);
    voteOpeText = [];

    if(
        IsInSetup()
    ){
        if(CanChangeVote()){
            voteOpeText[0] = CreateHudText(ep, null,
                <
                    "You can change vote of game difficulty until round starts.\n\rPress <2>/<3> key while standing still to change vote.\n\r●Your vote = <0> <1>",
                    VoteGameDifficultyText(voteGameDifficulty),
                    <"(<0>/<1>)", voteGameDifficulty - 1, GameDifficulty.LAST - 2>,
                    ButtonGuide(VD_AscKey),
                    ButtonGuide(VD_DisKey)
                >,
                null, Location.Left, 100, null, Color.Yellow, null, HudTextRev.StringAndColor, Spectators.DefaultVisibility);

        }
        else{
            voteOpeText[1] = CreateHudText(ep, null,
                <
                    "You can change vote of game difficulty until round starts in respawn area.\n\r●Your vote = <0> <1>",
                    VoteGameDifficultyText(voteGameDifficulty),
                    <"(<0>/<1>)", voteGameDifficulty - 1, GameDifficulty.LAST - 2>
                >,
                null, Location.Left, 100, null, Color.Yellow, null, HudTextRev.StringAndColor, Spectators.DefaultVisibility);
        }
    }
}

define CanChangeVote():
    !IsDummyBot() &&
    IsInSpawnRoom() &&
    IsInSetup();

rule: "Vote/showVoteOperation"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    HasSpawned() &&
    CanChangeVote()
){
    UpdateVoteOperationText();
}

rule: "Vote/hideVoteOperation"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    HasSpawned() &&
    !CanChangeVote()
){
    UpdateVoteOperationText();
}

rule: "Vote/switchVoteGameDifficulty"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam &&
    HasSpawned() &&
    CanChangeVote() &&
    (
        ( IsButtonHeld(ep, VD_AscKey) && !IsButtonHeld(ep, VD_DisKey) ) ||
        ( !IsButtonHeld(ep, VD_AscKey) && IsButtonHeld(ep, VD_DisKey) )
    )
){
    AbortIf(IsMoving());
    // switch vote
    voteGameDifficulty = 
        ( IsButtonHeld(ep, VD_AscKey) && !IsButtonHeld(ep, VD_DisKey) ) ?
            voteGameDifficulty < GameDifficulty.LAST - 1 ? voteGameDifficulty + 1 : GameDifficulty.INIT + 1 :
            voteGameDifficulty > GameDifficulty.INIT + 1 ? voteGameDifficulty - 1 : GameDifficulty.LAST - 1 ;

    // update text
    UpdateVoteOperationText();

    PlayEffect(ep, PlayEffect.DebuffImpactSound, null, ep, 75);
}

globalvar define confirmGameDifficulty! = GameDifficulty.NO_VOTE;

globalvar define settingGameDifficulty! = 
    WorkshopSettingInteger("Game Settings", "Game Difficulty (Override voting difficulty when set other of 0)", GameDifficulty.INIT + 0, GameDifficulty.INIT + 0, GameDifficulty.LAST - 2, 0)
    + 1;

rule: "Vote/confirmGameDifficulty"
Event.OngoingGlobal
if(
    IsGameInProgress()
    // IsJumping(hp)    // debug
){
                // // debug
                // dbgHudTxt(<"EASY      = <0>", GameDifficulty.EASY>);
                // dbgHudTxt(<"NORMAL    = <0>", GameDifficulty.NORMAL>);
                // dbgHudTxt(<"HARD      = <0>", GameDifficulty.HARD>);
                // dbgHudTxt(<"EXPERT    = <0>", GameDifficulty.EXPERT>);
                // dbgHudTxt(<"LEGENDARY = <0>", GameDifficulty.LEGENDARY>);
                // dbgHudTxt(<"INSANITY  = <0>", GameDifficulty.INSANITY>);
                // Wait(0.5);
                
                // // debug
                // for(define index! = 0; index < CountOf(AllPlayers(playerTeam)); index++){
                //     define player! = AllPlayers(playerTeam)[index];
                //     player.voteGameDifficulty = RandomValueInArray([
                //         GameDifficulty.INIT,
                //         GameDifficulty.NO_VOTE,
                //         GameDifficulty.EASY,
                //         GameDifficulty.NORMAL,
                //         GameDifficulty.HARD,
                //         GameDifficulty.EXPERT,
                //         GameDifficulty.LEGENDARY,
                //         GameDifficulty.INSANITY,
                //         GameDifficulty.LAST
                //     ]);
                //     if(index == 3){
                //         player.voteGameDifficulty = GameDifficulty.LEGENDARY;
                //     }
                //     else if(index == 4){
                //         player.voteGameDifficulty = GameDifficulty.INSANITY;
                //     }
                //     // else{
                //     //     player.voteGameDifficulty = GameDifficulty.NO_VOTE;
                //     // }
                //     dbgHudTxt(<"<0> voteGameDifficulty = <1>", player, player.voteGameDifficulty>);
                // }
                // Wait(0.5);

    // make voteCountList
    define voteCountList! = [
        0,
        0,
        0,
        0,
        0,
        0,
        0,
        0,
        0
    ];
    define voteIndexList! = [
        GameDifficulty.INIT,
        GameDifficulty.NO_VOTE,
        GameDifficulty.EASY,
        GameDifficulty.NORMAL,
        GameDifficulty.HARD,
        GameDifficulty.EXPERT,
        GameDifficulty.LEGENDARY,
        GameDifficulty.INSANITY,
        GameDifficulty.LAST
    ];
    for(define idx! = 0; idx < CountOf(AllPlayers(playerTeam)); idx++){
        define player! = AllPlayers(playerTeam)[idx];
        define voteIndex! = IndexOfArrayValue(voteIndexList, player.voteGameDifficulty);
        voteCountList[voteIndex] += 1;
    }

    /// reset count of can't vote game diffculty type
    voteCountList[GameDifficulty.INIT]      = 0;
    voteCountList[GameDifficulty.NO_VOTE]   = 0;
    voteCountList[GameDifficulty.LAST]      = 0;

                // // debug
                // for(define index! = 0; index < CountOf(voteCountList); index++){
                //     dbgHudTxt(<"voteCountList[<0>] = <1>", index, voteCountList[index]>);
                // }
                // Wait(0.5);

    // find mostVoteCount
    define mostVoteCount! = 0;
    for(define idx! = 0; idx < CountOf(voteCountList); idx++){
        if(
            0 < voteCountList[idx] &&
            mostVoteCount < voteCountList[idx]
        ){ 

            mostVoteCount = voteCountList[idx];
        }
    }

                // // debug
                // dbgHudTxt(<"mostVoteCount = <0>", mostVoteCount>);
                // Wait(0.5);

    // make mostVoteIndexList
    define mostVoteIndexList! = [];
    if(0 < mostVoteCount){
        // non mostVoteCount
        for(define idx! = 0; idx < CountOf(voteCountList); idx++){
            // if most count vote, add index to list
            if(
                mostVoteCount == voteCountList[idx]
            ){ 
                mostVoteIndexList[CountOf(mostVoteIndexList)] = idx;
            }
        }
                // // debug
                // for(define index! = 0; index < CountOf(mostVoteIndexList); index++){
                //     dbgHudTxt(<"mostVoteIndexList[<0>] = <1>", index, mostVoteIndexList[index]>);
                // }
                // Wait(0.5);
    }
    
    // set mostVoteCountDifficluty
    define mostVoteCountDifficluty!;
    if(CountOf(mostVoteIndexList) <= 0){
        // non mostVoteIndexList
        // set difficult to NORMAL as default
        mostVoteCountDifficluty = GameDifficulty.NORMAL;
    }
    else{
        // set difficult by random from mostVoteIndexList
        mostVoteCountDifficluty = RandomValueInArray(mostVoteIndexList);
    }

                // // debug
                // dbgHudTxt(<"mostVoteCountDifficluty = <0>", mostVoteCountDifficluty>);
                // Wait(0.5);

    // override vote by host debug key
    if(
        IsButtonHeld(hp, Button.Interact) &&
        hp.voteGameDifficulty != GameDifficulty.NO_VOTE
    ){
        // set difficult from host's vote
        confirmGameDifficulty = hp.voteGameDifficulty;
    }
    // override vote by game setting
    else if(
        settingGameDifficulty != GameDifficulty.NO_VOTE
    ){
        // set difficult from game setting
        confirmGameDifficulty = settingGameDifficulty;
    }
    // not override vote
    else{
        // set difficult from vote count difficluty
        confirmGameDifficulty = mostVoteCountDifficluty;
    }

                // // debug
                // dbgHudTxt(<"confirmGameDifficulty = <0>", ConfirmGameDifficultyText(confirmGameDifficulty)>);
                // Wait(0.5);
}

playervar define gameDiffText!;

define DifficultyTextColor(define inputDifficulty):
    confirmGameDifficulty == GameDifficulty.EASY ? Color.Blue :
    confirmGameDifficulty == GameDifficulty.NORMAL ? Color.Green :
    confirmGameDifficulty == GameDifficulty.HARD ? Color.Yellow :
    confirmGameDifficulty == GameDifficulty.EXPERT ? Color.Orange :
    confirmGameDifficulty == GameDifficulty.LEGENDARY ? Color.Red :
    confirmGameDifficulty == GameDifficulty.INSANITY ? cautionColor :
    null;

void UpdateConfirmGameDifficultyText()"UpdateConfirmGameDifficultyText"{
    DestroyHudText(gameDiffText[0]);
    gameDiffText = [];

    // show difficulty text
    gameDiffText[0] = CreateHudText(ep,
        null,
        <
            "●game difficulty = <0> <1>",
            ConfirmGameDifficultyText(confirmGameDifficulty),
            <"(<0>/<1>)", confirmGameDifficulty - 1, GameDifficulty.LAST - 2>
        >,
        null, Location.Left, 100,
        Color.White, 
        DifficultyTextColor(confirmGameDifficulty),
        Color.White,
        HudTextRev.StringAndColor, Spectators.VisibleNever);
}

rule: "Vote/showConfirmGameDifficuly"
Event.OngoingPlayer
if(
    TeamOf() == playerTeam  &&
    HasSpawned() &&
    IsGameInProgress() &&
    confirmGameDifficulty != GameDifficulty.NO_VOTE
){
    Wait(0.1);
    UpdateVoteOperationText();      // delete opetation text
    UpdateConfirmGameDifficultyText();
}

// TAG Debug ----------
rule: "Debug/increaseDifficulty"
Event.OngoingPlayer
Team.All
if(
    hp == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Melee) &&
    IsButtonHeld(ep, Button.Ability2)
){
    WaitLongPressConfirm();
    if(confirmGameDifficulty < GameDifficulty.INSANITY){
        confirmGameDifficulty ++;
    }
    UpdateConfirmGameDifficultyText();
    LoopIfConditionIsTrue();
}

rule: "Debug/decreaseDifficulty"
Event.OngoingPlayer
Team.All
if(
    hp == ep &&
    IsButtonHeld(ep, Button.Interact) &&
    IsButtonHeld(ep, Button.Melee) &&
    IsButtonHeld(ep, Button.Ability1)
){
    WaitLongPressConfirm();
    if(confirmGameDifficulty > GameDifficulty.EASY){
        confirmGameDifficulty --;
    }
    UpdateConfirmGameDifficultyText();
    LoopIfConditionIsTrue();
}

