import "Utility.del";

// TAG ServerOverLoadMeasures ----------
define maxGameLogicSpeed: DefaultPercentage();

globalvar define slowControlHandler;

enum SlowControlHandler{
    None,
    ServerOverLoadMeasures,
    GameLogic
}

globalvar define countGameLogicSpeedDecrease = 0;
globalvar define gameLogicSpeed = maxGameLogicSpeed;
define watchTimeSpeedDecrease: 2.0;
define watchTimeSpeedIncrease: 0.75;
define speedDecreaseUnit: 10;
define speedIncreaseUnit: speedDecreaseUnit * 1.0;

rule: "ServerOverLoadMeasures/increaseGameLogicSpeed"
Event.OngoingGlobal
if(
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    !isServerLoadLevel(ServerLoadLevel.Overload) &&
    gameLogicSpeed < maxGameLogicSpeed
){
    Wait(
        watchTimeSpeedIncrease,
        // watchTimeSpeedIncrease * (gameLogicSpeed / DefaultPercentage()),
        WaitBehavior.AbortWhenFalse);
    
    // increase slow level
    gameLogicSpeed += speedDecreaseUnit;
    gameLogicSpeed = Min(gameLogicSpeed, maxGameLogicSpeed);
    // apply slow level
    SetSlowMotion(gameLogicSpeed);

    Loop();
}

rule: "ServerOverLoadMeasures/decreaseGameLogicSpeed"
Event.OngoingGlobal
if(
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    isServerLoadLevel(ServerLoadLevel.Overload) &&
    gameLogicSpeed > 0
){
    Wait(
        watchTimeSpeedDecrease,
        // watchTimeSpeedDecrease * (gameLogicSpeed / DefaultPercentage()),
        WaitBehavior.AbortWhenFalse);
    
    // decrease slow level
    gameLogicSpeed -= speedIncreaseUnit;
    gameLogicSpeed = Max(gameLogicSpeed, 0);
    // apply slow level
    SetSlowMotion(gameLogicSpeed);

    countGameLogicSpeedDecrease ++;
    Loop();
}

// TAG ServerOverLoading ----------
enum ServerLoadLevel{
    Overload = 255,
    Busy = 200,
    Hot = 150,
    Warm = 100,
    Cold = 50
}

define isServerLoadLevel(in define loadLevel): loadLevel <= ServerLoad();

enum ServerLoadType{
    GameMain,
    BotCreate,
    BotAction,
    BotMustAction
}

globalvar define countWaitActionByServerLoad;

void WaitReleaseServerLoad(in define loadType!){
    define loadLevel! = 
            loadType == ServerLoadType.GameMain ?       ServerLoadLevel.Busy :
            loadType == ServerLoadType.BotCreate ?      ServerLoadLevel.Overload :
            loadType == ServerLoadType.BotAction ?      ServerLoadLevel.Hot :
            loadType == ServerLoadType.BotMustAction ?  ServerLoadLevel.Overload :
                                                        ServerLoadLevel.Overload;
    define waitTimeLoadDist! = 
            loadType == ServerLoadType.GameMain ?       1.0 :
            loadType == ServerLoadType.BotCreate ?      0.0 :
            loadType == ServerLoadType.BotAction ?      2.0 :
            loadType == ServerLoadType.BotMustAction ?  0.0 :
                                                        0.0;
    if(
        isServerLoadLevel(loadLevel)
    ){
        countWaitActionByServerLoad ++;
        WaitUntil(!isServerLoadLevel(loadLevel), MaxValue());
        Wait(RandomReal(0, waitTimeLoadDist),WaitBehavior.IgnoreCondition);
    }
}

