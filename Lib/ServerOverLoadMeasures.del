import "Utility.del";

// TAG SerServerLoadMeasures ----------
define serverLoadMax: 255;
define isServerOverload(): serverLoadMax <= ServerLoad();
define serverLoadLimitMax: serverLoadMax;
define isServerLoadLimit(): serverLoadLimit <= ServerLoad();

// TAG SerServerLoadMeasures/WaitUntilServerLoadLimitRelease ----------
globalvar define countWaitActionByServerLoad;

void WaitUntilServerLoadLimitRelease(define baseTime = 1.0){
    if(
        isServerLoadLimit()
    ){
        countWaitActionByServerLoad ++;
        // WaitUntil(!isServerLoadLimit(), 10);

        while(isServerLoadLimit()){
            Wait(
                // baseTime * 0.1,
                0.1,
                WaitBehavior.IgnoreCondition);
        }
        
        Wait(RandomReal(0.0, 3.0), WaitBehavior.IgnoreCondition);
    }
}

// TAG SerServerLoadMeasures/ServerLoadLimit ----------
define watchTimeLoadLimitDecrease: 0.1;
define watchTimeLoadLimitIncrease: 0.1;
define limitChangeUnit: 50;

globalvar define serverLoadLimit = serverLoadLimitMax;

rule: "ServerOverloadMeasures/ServerLoadLimit/decrease"
Event.OngoingGlobal
if(
    serverLoadLimit > 0 &&
    isServerOverload()
){
    Wait(watchTimeLoadLimitDecrease, WaitBehavior.AbortWhenFalse);
    TuneServerLoadLimit(-limitChangeUnit);
    Loop();
}

rule: "ServerOverloadMeasures/ServerLoadLimit/increase"
Event.OngoingGlobal
if(
    serverLoadLimit < serverLoadLimitMax &&
    !isServerOverload()
){
    Wait(watchTimeLoadLimitIncrease, WaitBehavior.AbortWhenFalse);
    TuneServerLoadLimit(limitChangeUnit);
    Loop();
}

void TuneServerLoadLimit(define changeVal)"TuneServerLoadLimit"{
    serverLoadLimit += changeVal;
    if(serverLoadLimit > serverLoadLimitMax){
        serverLoadLimit = serverLoadLimitMax;
    }
    else if(serverLoadLimit < 0){
        serverLoadLimit = 0;
    }
}

// TAG ServerLoadMeasures/GameSpeed ----------
define watchTimGameSpeedDecrease: 1.5;
define watchTimeGameSpeedIncrease: 1.5;
define watchTimeGameSpeedForceIncrease: 5.0;
define speedChangeUnit: 10;
define gameSpeedMax: 100;

globalvar define gameSpeed = gameSpeedMax;
globalvar define countGameSpeedDecrease;

rule: "ServerOverloadMeasures/GameSpeed/decrease"
Event.OngoingGlobal
if(
    gameSpeed > 0 &&
    isServerOverload()
){
    Wait(watchTimGameSpeedDecrease, WaitBehavior.AbortWhenFalse);
    countGameSpeedDecrease ++;
    TuneGameSpeed(-speedChangeUnit);
    Loop();
}

rule: "ServerOverloadMeasures/GameSpeed/increase"
Event.OngoingGlobal
if(
    gameSpeed < gameSpeedMax &&
    !isServerOverload()
){
    Wait(watchTimeGameSpeedIncrease, WaitBehavior.AbortWhenFalse);
    TuneGameSpeed(speedChangeUnit);
    Loop();
}

rule: "ServerOverloadMeasures/GameSpeed/increaseByTimeout"
Event.OngoingGlobal
if(
    gameSpeed < gameSpeedMax
){
    Wait(watchTimeGameSpeedForceIncrease, WaitBehavior.AbortWhenFalse);
    TuneGameSpeed(speedChangeUnit);
    Loop();
}

void TuneGameSpeed(define changeVal)"TuneGameSpeed"{
    gameSpeed += changeVal;
    if(gameSpeed > gameSpeedMax){
        gameSpeed = gameSpeedMax;
    }
    else if(gameSpeed < 0){
        gameSpeed = 0;
    }
    SetSlowMotion(gameSpeed);
}