import "Utility.del";

// TAG ServerOverloadMeasures/ServerLoadLimit ----------
globalvar define currentServerLoadLimit = serverLoadMax;
define serverLoadMax: 255;
define watchTimeLimitDecrease: 0.25;
define watchTimeLimitIncrease: 0.25;
define limitChangeUnit: 50;

rule: "ServerOverloadMeasures/ServerLoadLimit/increase"
Event.OngoingGlobal
if(
    currentServerLoadLimit < serverLoadMax &&
    !(ServerLoad() >= serverLoadMax)
){
    Wait(watchTimeLimitIncrease, WaitBehavior.AbortWhenFalse);
    currentServerLoadLimit += limitChangeUnit;
    if(currentServerLoadLimit > serverLoadMax){
        currentServerLoadLimit = serverLoadMax;
    }
    Loop();
}

rule: "ServerOverloadMeasures/ServerLoadLimit/decrease"
Event.OngoingGlobal
if(
    currentServerLoadLimit > 0 &&
    (ServerLoad() >= serverLoadMax)
){
    Wait(watchTimeLimitDecrease, WaitBehavior.AbortWhenFalse);
    currentServerLoadLimit -= limitChangeUnit;
    if(currentServerLoadLimit < 0){
        currentServerLoadLimit = 0;
    }
    Loop();
}

// TAG ServerOverloadMeasures/GameSpeed ----------
globalvar define currentGameSpeed = gameSpeedMax;
define gameSpeedMax: 100;
define watchTimeSpeedDecrease: 1.5;
define watchTimeSpeedIncrease: 1.0;
define speedChangeUnit: 10;
globalvar define countGameSpeedDecrease;


rule: "ServerOverloadMeasures/GameSpeed/increase"
Event.OngoingGlobal
if(
    currentGameSpeed < gameSpeedMax &&
    !(ServerLoad() >= serverLoadMax)
){
    Wait(watchTimeSpeedIncrease, WaitBehavior.AbortWhenFalse);
    currentGameSpeed += speedChangeUnit;
    if(currentGameSpeed > gameSpeedMax){
        currentGameSpeed = gameSpeedMax;
    }
    SetSlowMotion(currentGameSpeed);
    Loop();
}

rule: "ServerOverloadMeasures/GameSpeed/decrease"
Event.OngoingGlobal
if(
    currentGameSpeed > 0 &&
    (ServerLoad() >= serverLoadMax)
){
    Wait(watchTimeSpeedDecrease, WaitBehavior.AbortWhenFalse);
    currentGameSpeed -= speedChangeUnit;
    if(currentGameSpeed < 0){
        currentGameSpeed = 0;
    }
    SetSlowMotion(currentGameSpeed);
    countGameSpeedDecrease ++;
    Loop();
}


// TAG ServerOverloading ----------
define isServeLoadLimit(): ServerLoad() >= currentServerLoadLimit;

globalvar define countWaitActionByServerLoad;

void WaitUntilServerLoadLimitRelease(){
    if(
        isServeLoadLimit()
    ){
        countWaitActionByServerLoad ++;
        while(isServeLoadLimit()){
            Wait(RandomReal(0.9, 1.1), WaitBehavior.IgnoreCondition);
        }
    }
}

