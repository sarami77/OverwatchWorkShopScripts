import "Utility.del";

// TAG ServerOverLoadMeasures ----------
define maxGameLogicSpeed: 110;

globalvar define slowControlHandler!;
enum SlowControlHandler{
    None,
    ServerOverLoadMeasures,
    GameEnd
}

globalvar define gameLogicSpeed = 100;

rule: "ServerOverLoadMeasures/increaseGameLogicSpeed"
Event.OngoingGlobal
if(
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    !isServerOverLoading() &&
    gameLogicSpeed < maxGameLogicSpeed
){
    Wait(0.2 * (gameLogicSpeed / DefaultPercentage()), WaitBehavior.AbortWhenFalse);
    // increase slow level
    // gameLogicSpeed += 1;
    gameLogicSpeed += RoundToInteger((maxGameLogicSpeed - gameLogicSpeed) / 10, Rounding.Up);
    gameLogicSpeed = Min(gameLogicSpeed, maxGameLogicSpeed);
    // apply slow level
    if(gameLogicSpeed <= DefaultPercentage()){
        SetSlowMotion(gameLogicSpeed);
    }
    LoopIfConditionIsTrue();
}

rule: "ServerOverLoadMeasures/decreaseGameLogicSpeed"
Event.OngoingGlobal
if(
    slowControlHandler == SlowControlHandler.ServerOverLoadMeasures &&
    isServerOverLoading() &&
    gameLogicSpeed > 0
){
    Wait(0.2 * (gameLogicSpeed / DefaultPercentage()), WaitBehavior.AbortWhenFalse);
    // decrease slow level
    gameLogicSpeed -= 1;
    // gameLogicSpeed -= 1 + RoundToInteger((DefaultPercentage()  - gameLogicSpeed) / 10, Rounding.Up);
    gameLogicSpeed = Max(gameLogicSpeed, 0);
    // apply slow level
    if(gameLogicSpeed <= DefaultPercentage()){
        SetSlowMotion(gameLogicSpeed);
    }
    LoopIfConditionIsTrue();
}
