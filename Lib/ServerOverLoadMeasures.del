import "Utility.del";

// TAG SerServerLoadMeasures ----------
globalvar define loadLimit = ServerLoad.FULL;
globalvar define countActionWait;

// TAG SerServerLoadMeasures/WaitUntilLoadLimitRelease ----------
void WaitUntilServerLoadLimitRelease(define baseTime = 1.0){
    if(
        ServerLoad() >= loadLimit
    ){
        countActionWait ++;

        WaitUntil(ServerLoad() < loadLimit, MaxValue());

        // while(ServerLoad() >= loadLimit){
        //     Wait(0.2, WaitBehavior.IgnoreCondition);
        // }

        // distribute load
        Wait(RandomReal(0.0, 3.0), WaitBehavior.IgnoreCondition);
    }
}

// TAG ServerLoadMeasures/LoadLimit ----------
define ServerLoadOfDoLoadLimitDown : ServerLoad.HIGH;
define WaitTimeLoadLimitDown : 0.1;
define ChangeStepLoadLimitDown : 50;
define WaitTimeLoadLimitUp : 0.1;
define ChangeStepLoadLimitUp : 50;

enum ServerLoad{
    LOW = 100,
    MID = 150,
    HIGH = 200,
    FULL = 255
}

rule: "ServerOverloadMeasures/LoadLimit/down"
Event.OngoingGlobal
if(
    ServerLoad() >= ServerLoadOfDoLoadLimitDown &&
    loadLimit > 0
){
    Wait(WaitTimeLoadLimitDown, WaitBehavior.AbortWhenFalse);
    TuneLoadLimit(-ChangeStepLoadLimitDown);

    Loop();
}

rule: "ServerOverloadMeasures/LoadLimit/up"
Event.OngoingGlobal
if(
    ServerLoad() < ServerLoadOfDoLoadLimitDown &&
    loadLimit < ServerLoad.HIGH
){
    Wait(WaitTimeLoadLimitUp, WaitBehavior.AbortWhenFalse);
    TuneLoadLimit(ChangeStepLoadLimitUp);

    Loop();
}

void TuneLoadLimit(define changeVal)"TuneLoadLimit"{
    loadLimit += changeVal;
    if(loadLimit > ServerLoad.FULL){
        loadLimit = ServerLoad.FULL;
    }
    else if(loadLimit < 0){
        loadLimit = 0;
    }
}

// TAG ServerLoadMeasures/GameSpeed ----------
define ServerLoadOfDoGameSpeedDown : ServerLoad.FULL;
define WaitTimeGameSpeedDown : 1.5;
define ChangeStepGameSpeedDown : 10;
define WaitTimeGameSpeedUp : 5.0;
define ChangeStepGameSpeedUp : 10;

define gameSpeedMax: 100;
globalvar define gameSpeed = gameSpeedMax;
globalvar define countGameSpeedDecrease;

rule: "ServerOverloadMeasures/GameSpeed/down"
Event.OngoingGlobal
if(
    ServerLoad() >= ServerLoadOfDoGameSpeedDown &&
    gameSpeed > 0
){
    Wait(WaitTimeGameSpeedDown, WaitBehavior.AbortWhenFalse);
    TuneGameSpeed(-ChangeStepGameSpeedDown);

    countGameSpeedDecrease ++;
    Loop();
}

rule: "ServerOverloadMeasures/GameSpeed/up"
Event.OngoingGlobal
if(
    // ServerLoad() < ServerLoadOfDoGameSpeedDown &&
    gameSpeed < gameSpeedMax
){
    Wait(WaitTimeGameSpeedUp, WaitBehavior.AbortWhenFalse);
    TuneGameSpeed(ChangeStepGameSpeedUp);

    Loop();
}

void TuneGameSpeed(define changeVal)"TuneGameSpeed"{
    gameSpeed += changeVal;
    if(gameSpeed > gameSpeedMax){
        gameSpeed = gameSpeedMax;
    }
    else if(gameSpeed < 0){
        gameSpeed = 0;
    }
    SetSlowMotion(gameSpeed);
}
