import "Utility.del";

// Function enable team
// Set team in import destination parent file

// TAG public property
globalvar define damageTextEnableTeam;

define IsDamageTextEnableTeam:
    damageTextEnableTeam == TeamOf() ||
    damageTextEnableTeam == Team.All;

define maxScaleDamage: 1000;

define minTextSize: basicInWorldTextSize;
define maxTextSize: 4;
playervar define damageTextEntity;
playervar define damageTextPosition;
define minTextDisplayTime: 0.2;
define maxTextDisplayTime: 1.5;
playervar define damageTextDisplayTime;
playervar define sumDamage;

rule: "DamageFloatText/show"
Event.OnDamageTaken
if(
    IsDamageTextEnableTeam &&
    EventDamage() >= 1
){
    // add damage
    sumDamage += EventDamage();
    // calc work value
    define damageScale = Min(sumDamage, maxScaleDamage) / maxScaleDamage;
    // set text color
    define damageTextColor;
    if(maxScaleDamage * 1.00 <= sumDamage){
        damageTextColor = Color.Purple;      
    }
    else if(maxScaleDamage * 0.75 <= sumDamage){
        damageTextColor = Color.Red;      
    }
    else if(maxScaleDamage * 0.5 <= sumDamage){
        damageTextColor = Color.Orange;      
    }
    else if(maxScaleDamage * 0.25 <= sumDamage){
        damageTextColor = Color.Yellow;    
    }
    else{
        damageTextColor = Color.White;   
    }
    // set text position 
    damageTextPosition = EyePosition(EventPlayer()) + PositionOffset();
    // set text size
    define damageTextSize = minTextSize + (damageScale * (maxTextSize - minTextSize));
    // set text display time according damage
    damageTextDisplayTime = minTextDisplayTime + (damageScale * (maxTextDisplayTime - minTextDisplayTime));
    // show text
    DestroyInWorldText(damageTextEntity);
    damageTextEntity = CreateInWorldText(
        AllPlayers(), RoundToInteger(sumDamage), damageTextPosition, damageTextSize,
        Clipping.ClipAgainstSurfaces, InworldTextRev.VisibleToPositionStringAndColor,
        damageTextColor, Spectators.DefaultVisibility);
}

rule: "DamageFloatText/hide"
Event.OnDamageTaken
if(
    IsDamageTextEnableTeam &&
    sumDamage > 0
){
    // wait for visual recognition
    Wait(damageTextDisplayTime, WaitBehavior.RestartWhenTrue);
    // hide text
    DestroyInWorldText(damageTextEntity);
    // claer sum damage
    sumDamage = 0;
}