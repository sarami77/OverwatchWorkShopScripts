import "PerkCore.del";

rule: "Perk/Player/DeathFromAbove/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using
){
    // set time limit
    perkTimeLimit = 180;
    // set param
    perkEffectStatus = PerkEffectStatus.Ready;
    // set hud
    perkEffectAndText = [];
    CreatePerkEffectHud( 
        perkEffectStatus == PerkEffectStatus.Triggered ? 
            <"Hold secondary fire button <0> to accelerate in facing direction.", ButtonGuide(Button.SecondaryFire)> : 
            ""
        );
    perkEffectAndText [0] = LastTextID();
    CreatePerkEffectHud(
        IsUsingUltimate() ?
            "howitzer available." :
            <"Press ability1 button <1> to equip <0>.", IsInAlternateForm() ? "auto cannon" : "minigun", ButtonGuide(Button.Ability1)>
    );
    perkEffectAndText [1] = LastTextID();
    CreatePerkEffectHud( 
        perkEffectStatus == PerkEffectStatus.Triggered ? 
            <"Hold crouch button <0> to walking mode.", ButtonGuide(Button.Crouch)> : 
            <"Hold jump button <0> to flying mode.", ButtonGuide(Button.Jump)>
        );
    perkEffectAndText [2] = LastTextID();
    // set perk hero
    PerkHero perkHero = GetPerkHero();
    perkHero.hero = Hero.Bastion;
    perkHero.statusUnkillable = true;
    perkHero.secondaryFireEnabled = false;
    perkHero.maxHealth = 300;
    perkHero.healingReceived = 5.0;
    perkHero.gravity = 5;
    ForcePlayerPerkHero(ep, perkHero);
    // set status
    StartScalingPlayer(ep, 1.5, false);
    StartModifyingHeroVoiceLines(ep, 0.8, false);

    // fly and change mode
    Wait(0.5, WaitBehavior.AbortWhenFalse);
    StartHoldingButton(ep, Button.Jump);
    Wait(0.5, WaitBehavior.IgnoreCondition);
    StopHoldingButton(ep, Button.Jump);
    PressButton(ep, Button.Ability1);
}

rule: "Perk/Player/DeathFromAbove/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Non
){
    // reset status
    ClearStatus(ep, Status.Rooted);
    StopScalingPlayer(ep);
    StopModifyingHeroVoiceLines(ep);
    // reset hud
    DestroyHudText(perkEffectAndText [0]);
    DestroyHudText(perkEffectAndText [1]);
    DestroyHudText(perkEffectAndText [2]);
    // reset perk hero
    StopForcingPerkHero(ep);
    // reset type
    perkType = PerkType.Non;
}

rule: "Perk/Player/DeathFromAbove/brokenPerkTransform //by helth 1"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    Health() <= 1
){
    perkState = PerkState.Non;
}

rule: "Perk/Player/DeathFromAbove/changeFlyingMode"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    perkEffectStatus  == PerkEffectStatus.Ready &&
    IsButtonHeld(ep, Button.Jump)
){
    WaitLongPressConfirm();
    SetStatus(ep, null, Status.Rooted, MaxValue());
    SetGravity(ep, 5);
    perkEffectStatus = PerkEffectStatus.Triggered;
}

rule: "Perk/Player/DeathFromAbove/changeToLandingMode"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    perkEffectStatus  == PerkEffectStatus.Triggered &&
    IsButtonHeld(ep, Button.Crouch)
){
    WaitLongPressConfirm();
    ClearStatus(ep, Status.Rooted);
    SetGravity(ep, 100);
    perkEffectStatus = PerkEffectStatus.Ready;
}

define keepHeight: 20;

rule: "Perk/Player/DeathFromAbove/flying"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    perkEffectStatus  == PerkEffectStatus.Triggered
){
    // if press accelerate button
    if(IsButtonHeld(ep, Button.SecondaryFire)){
        // move facing direction
        ApplyImpulse(ep, 
            VectorScaledY(
                FacingDirectionOf(),
                0 > YOf(FacingDirectionOf()) ? 0.1 : 1  // if downward, y direction scale down
            ), 1,
            Relative.ToWorld, ContraryMotion.Incorporate);
    }
    else{
        // check surface altitude
        define distanceFromSurface = DistanceBetween(ep, BelowPosition(ep));
        // near surface
        if(
            distanceFromSurface < keepHeight
        ){
            // move to up
            ApplyImpulse(ep, Up(),
                (keepHeight - distanceFromSurface) * 0.05,
                Relative.ToPlayer, ContraryMotion.Incorporate);
        }
        // far surface
        else if(
            distanceFromSurface > keepHeight + 1
        ){
            // move to down
            ApplyImpulse(ep, Down(),
                0.01,
                Relative.ToPlayer, ContraryMotion.Cancel);  // force down
        }        
        // move to fotward    
        ApplyImpulse(ep, Forward(),
            0.1,
            Relative.ToPlayer, ContraryMotion.Incorporate);
        
    }
    WaitApplyImpulseCycle();
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/DeathFromAbove/playAccelerate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    perkEffectStatus  == PerkEffectStatus.Triggered &&
    IsButtonHeld(ep, Button.SecondaryFire)
){
    // play Accelerate
    PlayEffect(
        AllPlayers(), PlayEffect.GoodExplosion,
        Color.Orange, PositionOf(ep), 0.5);
    WaitUpdateShortCycle();
    LoopIfConditionIsTrue();
}

define splashDamageMinigun: 50;
define splashDamageRangeMinigun: 1.0;

rule: "Perk/Player/DeathFromAbove/splashDamageMinigun"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    !IsUsingUltimate() &&
    IsInAlternateForm() &&
    IsFiringPrimary()
){
    // get aimPosition
    aimPosition = AimingPosition();
    define scat! = DistanceBetween(EyePosition(), aimPosition) * 0.03;
    landPosition = aimPosition + RandomPosition(scat, scat, scat);
    // splash damage
    Damage(
        PlayersWithinRadius(
            landPosition, Max(splashDamageRangeMinigun, 1.75),
            Team.Team2, RadiusLOS.Off),
        ep, splashDamageMinigun);
    // play blast
    PlayEffect(
        AllPlayers(), PlayEffect.BadExplosion,
        Color.Yellow,
        landPosition,
        splashDamageRangeMinigun);

    WaitContinuousBlastCycle();
    LoopIfConditionIsTrue();
}

define splashDamageAutoCanon: 100;
define splashDamageRangeAutoCanon: 2.0;

rule: "Perk/Player/DeathFromAbove/splashDamageAutoCanon"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    !IsUsingUltimate() &&
    !IsInAlternateForm() &&
    IsFiringPrimary()
){
    // get aimPosition
    landPosition = AimingPosition() + RandomPosition(0.5, 0.5, 0.5);
    // splash damage
    Damage(
        PlayersWithinRadius(
            landPosition, splashDamageRangeAutoCanon,
            Team.Team2, RadiusLOS.Off),
        ep, splashDamageAutoCanon);
    // play blast
    PlayEffect(
        AllPlayers(Team.All), PlayEffect.GoodExplosion,
        Color.Orange, landPosition,
        splashDamageRangeAutoCanon);
    Wait(0.015, WaitBehavior.IgnoreCondition);
    CancelPrimaryAction();
}

rule: "Perk/Player/DeathFromAbove/chargingUlt"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    !IsUsingUltimate() &&
    UltimateChargePercent() < 100
){
    WaitUpdateASecond();
    SetUltimateCharge(ep, (UltimateChargePercent() + 3));
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/DeathFromAbove/setTankModeDamageBuff"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    IsUsingUltimate()
){
    SetDamageDealt(ep, 300);
}

rule: "Perk/Player/DeathFromAbove/resetTankModeDamageBuff"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.DeathFromAbove &&
    perkState  == PerkState.Using &&
    !IsUsingUltimate()
){
    SetDamageDealt(ep, 100);
}
