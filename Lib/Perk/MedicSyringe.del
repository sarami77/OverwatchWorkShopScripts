import "PerkCore.del";

define MedicSyringeRange: 3;

rule: "Perk/Player/MedicSyringe/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.MedicSyringe &&
    perkState == PerkState.Using
){
    // set time limit
    perkTimeLimit = 300;
    // set hud
    CreatePerkEffectHud(<"Hold melee button <0> to heal full and instantly revive teammate of melee range", ButtonGuide(Button.Melee)>);
    perkEffectAndText [0] = LastTextID();
}

rule: "Perk/Player/MedicSyringe/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.MedicSyringe &&
    perkState  == PerkState.Non
){
    // reset text
    DestroyHudText(perkEffectAndText [0]);
    // reset effect
    DestroyEffect(perkEffectAndText [1]);
    // reset type
    perkType = PerkType.Non;
}

rule: "Perk/Player/MedicSyringe/inject"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.MedicSyringe &&
    perkState  == PerkState.Using &&
    IsButtonHeld(ep, Button.Melee)
){
    WaitLongPressConfirm();

    // get nearest reticle player
    perkTargets = PlayerClosestToReticle(ep, TeamOf());

    // player is far or not facing
    if(
        DistanceBetween(ep, perkTargets) > MedicSyringeRange ||
        !IsInViewAngle(ep, perkTargets, 90)
    ){
        // get nearest distance player
        perkTargets = ClosestPlayerTo(ep, TeamOf());

        // player is far or not facing
        if(
            DistanceBetween(ep, perkTargets) > MedicSyringeRange ||
            !IsInViewAngle(ep, perkTargets, 90)
        ){
            // get facing position
            perkTargets = PositionOf(ep) + PositionOffset() + FacingDirectionOf();
        }
    }

    DestroyEffect(perkEffectAndText [1]);
    perkEffectAndText [1] = CreateBeamEffect(AllPlayers(),
        BeamType.BadBeam,
        PositionOf(ep) + PositionOffset(),
        PositionOf(perkTargets) + PositionOffset(),
        Color.Yellow, EffectRev.VisibleToPositionAndRadius);

    Wait(0.1, WaitBehavior.IgnoreCondition);
    
    if(
        // is player
        HasSpawned(perkTargets)
    ){
        if(IsAlive(perkTargets)){
            // heal
            PlayEffect(
                AllPlayers(Team.All), PlayEffect.GoodPickupEffect,
                Color.Yellow, perkTargets, 1);
            PlayEffect(
                AllPlayers(Team.All), PlayEffect.BuffImpactSound,
                Color.Yellow, perkTargets, MaxValue());

            Heal(perkTargets, ep, MaxValue());        
        }
        else{
            // revive
            revive(ep, perkTargets, reviveRange * 3);
        }
    }

    Wait(0.1, WaitBehavior.IgnoreCondition);

    DestroyEffect(perkEffectAndText [1]);
}

rule: "Perk/Player/MedicSyringe/tryInjectForBot"
Event.OngoingPlayer
Team.Team1
if(
    IsDummyBot() &&
    perkType == PerkType.MedicSyringe &&
    perkState  == PerkState.Using
){
    Wait(1.5, WaitBehavior.AbortWhenFalse);

    perkTargets = FirstOf(
        SortedArray(
            FilteredArray(
                AllPlayers(TeamOf()),
                (
                    ae != ep &&
                    NormalizedHealth(ae) < 1.0 &&
                    DistanceBetween(ep, ae) <= MedicSyringeRange
                )
            ),
            Health(ae)
        )
    );

    if(
        perkTargets != 0
    ){
        define prevTarget! = target;
        target = perkTargets;
        SetFacing(ep, DirectionTowards(ep, target), Relative.ToWorld);
        
        SetStatus(ep, null, Status.Rooted, 1);
        StartHoldingButton(ep, Button.Melee);
        Wait(0.5, WaitBehavior.IgnoreCondition);
        StopHoldingButton(ep, Button.Melee);

        target = prevTarget;
    }
    LoopIfConditionIsTrue();
}