import "PerkCore.del";

// TAG CarePackage ----------
rule: "Perk/Player/CarePackage/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.CarePackage &&
    perkState  == PerkState.Using
){
    // set time limit
    perkTimeLimit = 7;
    // set param
    perkEffectStatus = 0;
    // show text
    // landing zone text
    perkEffectAndText = [];
    CreateInWorldText(
        AllPlayers(), <"<0> <1> <2>\n\rArrival in <3> second. Stand by...", ep, HeroIconString(HeroOf()), "Care Package", perkTimeLimit >,
        landPosition + PositionOffset(), basicInWorldTextSize, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);
    perkEffectAndText [0] = LastTextID();
    perkEffectAndText [1] = LastTextID();
    // set effect
    // package body
    CreateEffect(AllPlayers(), Effect.Orb, Color.Turquoise,
        perkPosition, 1, EffectRev.VisibleToPositionAndRadius);
    perkEffectAndText [2] = LastCreatedEntity();
    // package flare
    CreateEffect(AllPlayers(), Effect.GoodAura, Color.White,
        perkPosition, 5, EffectRev.VisibleToPositionAndRadius);
    perkEffectAndText [3] = LastCreatedEntity();
    // package sound
    CreateEffect(AllPlayers(), Effect.BadAuraSound, Color.White,
        perkPosition, MaxLength(), EffectRev.VisibleToPositionAndRadius);
    perkEffectAndText [4] = LastCreatedEntity();
    // landing zone smoke
    CreateEffect(AllPlayers(), Effect.Cloud, Color.Turquoise,
        landPosition, 5, EffectRev.VisibleToPositionAndRadius);
    perkEffectAndText [5] = LastCreatedEntity();
    // landing zone light shaft
    CreateEffect(AllPlayers(), Effect.LightShaft, Color.Turquoise,
        landPosition, 0.1, EffectRev.VisibleToPositionAndRadius);
    perkEffectAndText [6] = LastCreatedEntity();
    // landing zone check icon
    CreateIcon(AllPlayers(), landPosition + (Up() * 2), Icon.Heart,
        IconRev.VisibleToAndPosition, Color.Turquoise, true);
    perkEffectAndText [7] = LastCreatedEntity();
}

rule: "Perk/Player/CarePackage/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.CarePackage &&
    perkState  == PerkState.Non
){
    // reset text
    DestroyInWorldText(perkEffectAndText [0]);
    DestroyInWorldText(perkEffectAndText [1]);
    // reset effect
    DestroyEffect(perkEffectAndText [2]);
    DestroyEffect(perkEffectAndText [3]);
    DestroyEffect(perkEffectAndText [4]);
    DestroyEffect(perkEffectAndText [5]);
    DestroyEffect(perkEffectAndText [6]);
    StopChasingVariable(perkPosition);
    // reset icon
    DestroyIcon(perkEffectAndText [7]);
    // reset type
    perkType = PerkType.Non;
}

rule: "Perk/Player/CarePackage/playTrajectory"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.CarePackage &&
    perkState  == PerkState.Using &&
    perkTimeLimit > floatDropTime
){
    // play blast
    PlayEffect(
        AllPlayers(), PlayEffect.BadExplosion,
        Color.Orange, perkPosition, 1.0);

    WaitUpdateVeryShortCycle();
    LoopIfConditionIsTrue();
}

define jetDropTime: perkTimeLimit - floatDropTime;
define floatDropTime: 3;
define floatDropHight: 3;

rule: "Perk/Player/CarePackage/dropPackage"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.CarePackage &&
    perkState  == PerkState.Using
){
    // set package at horizontal positions over 250
    perkPosition = ep +
        Vector(0, 125, 0) + RandomPosition(0, 0, 0);
    // set package land position to player position
    landPosition = BelowPosition(ep);
    // start package move
    if(!isServerOverLoading){
        UpdateEveryFrame(perkPosition);
        ChaseVariableOverTime(
            perkPosition, landPosition + (Up() * floatDropHight), jetDropTime,
            TimeChaseReevaluation.DestinationAndDuration);
        Wait(jetDropTime);
        ChaseVariableOverTime(
            perkPosition, landPosition, floatDropTime,
            TimeChaseReevaluation.DestinationAndDuration);
    }
}

rule: "Perk/Player/CarePackage/landedToPoint"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.CarePackage &&
    perkState  == PerkState.Using &&
    perkTimeLimit  <= 0
){
    StopChasingVariable(perkPosition);

    // play landing effect
    PlayEffect(
        AllPlayers(), PlayEffect.RingExplosion,
        Color.Turquoise, perkPosition, 10);
    PlayEffect(
        AllPlayers(), PlayEffect.GoodExplosion,
        Color.Turquoise, perkPosition, 10);
    PlayEffect(
        AllPlayers(), PlayEffect.RingExplosionSound,
        Color.White, perkPosition, MaxLength());
    
    // set perk crate count from player count
    perkTargets = RandomizedArray(AllPlayers(Team.Team1));
    perkEffectStatus = CountOf(perkTargets);
    // set perk crate position to around of landPosition
    for(pLoopIndex = 0; pLoopIndex < perkEffectStatus; pLoopIndex++){
        perkTargets[pLoopIndex].perkCratePosition
            = NearestWalkablePosition(landPosition + RandomPosition(3, 0, 3)) + PositionOffset();    
        perkTargets[pLoopIndex].perkCrateState = PerkCrateState.Dropped;
        Wait(0.1, WaitBehavior.IgnoreCondition);
    }
    perkState = PerkState.Non;
}
