import "PerkCore.del";

rule: "Perk/Player/GrapplingBeam/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.GrapplingBeam &&
    perkState  == PerkState.Using
){
    // set time limit
    perkTimeLimit = 300;
    // set param
    perkEffectStatus = PerkEffectStatus.Ready;
    // set hud
    perkEffectAndText = [];
    CreatePerkEffectHud(<"Hold melee button <0> to attract enemy with grapple beam.", ButtonGuide(Button.Melee)>);
    perkEffectAndText [0] = LastTextID();
    CreatePerkEffectHud(<"Hold jump button <0> to move in air with grapple beam.", ButtonGuide(Button.Jump)>);
    perkEffectAndText [1] = LastTextID();
}

rule: "Perk/Player/GrapplingBeam/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.GrapplingBeam &&
    perkState  == PerkState.Non
){
    // reset hud
    DestroyHudText(perkEffectAndText [0]);
    DestroyHudText(perkEffectAndText [1]);
    // reset effect
    DestroyEffect(perkEffectAndText [2]);
    DestroyEffect(perkEffectAndText [3]);
    // reset type
    perkType = PerkType.Non;
}

rule: "Perk/Player/GrapplingBeam/moveInAir"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.GrapplingBeam &&
    perkState  == PerkState.Using &&
    IsButtonHeld(ep, Button.Jump)
){
    AbortIf(perkEffectStatus  == PerkEffectStatus.Triggered);
    WaitLongPressConfirm();

    // move in air
    aimPosition = AimingPosition();
    if(
        DistanceBetween(ep, aimPosition) <= 100
    ){
        CreateBeamEffect(AllPlayers(), BeamType.GoodBeam,
            ep, aimPosition,
            Color.Green, EffectRev.VisibleToPositionAndRadius);
        perkEffectAndText [2] = LastCreatedEntity();
        ApplyImpulse(ep, 
            VectorTowards(ep, aimPosition + (Up() * 1)),
            DistanceBetween(ep, aimPosition) * 2.5 > minMoveImpulse ?
                DistanceBetween(ep, aimPosition) * 2.5 :
                minMoveImpulse,
            Relative.ToWorld, ContraryMotion.Cancel);
    }
    perkEffectStatus = PerkEffectStatus.Triggered;

    // show beam effect
    WaitPlayEffect();
    DestroyEffect(perkEffectAndText [2]);
    perkEffectStatus = PerkEffectStatus.Ready;
}

rule: "Perk/Player/GrapplingBeam/pullTarget"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.GrapplingBeam &&
    perkState  == PerkState.Using &&
    IsButtonHeld(ep, Button.Melee)
){
    AbortIf(perkEffectStatus  == PerkEffectStatus.Triggered);
    WaitLongPressConfirm();
    
    // pull target
    perkTargets = AimingPlayer();
    if(
        DistanceBetween(ep, perkTargets) <= 100 &&
        perkTargets != null
    ){
        SetStatus(perkTargets, ep, Status.Stunned, 1.0);
        // wait move stop
        Wait(0.1, WaitBehavior.IgnoreCondition);
        CreateBeamEffect(AllPlayers(), BeamType.GoodBeam,
            ep, perkTargets,
            Color.Red, EffectRev.VisibleToPositionAndRadius);
        perkEffectAndText [3] = LastCreatedEntity();
        ApplyImpulse(perkTargets, 
            VectorTowards(perkTargets, PositionOf(ep) + (Up() * 2.0)),
            DistanceBetween(perkTargets, ep) * 2.5,
            Relative.ToWorld, ContraryMotion.Cancel);
    }
    perkEffectStatus = PerkEffectStatus.Triggered;

    // show beam effect
    WaitPlayEffect();
    DestroyEffect(perkEffectAndText [3]);
    perkEffectStatus = PerkEffectStatus.Ready;
}
