import "PerkCore.del";

define steelRange : 12;

rule: "Perk/Player/SteelFormation/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.SteelFormation &&
    perkState  == PerkState.Using
){
    // set time limit
    perk[Perk.perkTimeLimit] = 180;
    // set hud
    perk[Perk.perkEffectAndText] = [];
    CreatePerkEffectHud(<"Formation include <0> players.", perk[Perk.perkEffectStatus] >);
    perk[Perk.perkEffectAndText] [0] = LastTextID();
    // set effect
    CreateEffect(AllPlayers(), Effect.Ring, Color.Orange,
        ep, steelRange, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [1] = LastCreatedEntity();
}

rule: "Perk/Player/SteelFormation/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.SteelFormation &&
    perkState  == PerkState.Non
){
    // reset hud
    DestroyHudText(perk[Perk.perkEffectAndText] [0]);
    // reset effect
    DestroyEffect(perk[Perk.perkEffectAndText] [1]);
    // reset type
    perkType = PerkType.Non;
}

playervar define isSteel!;
playervar define steelEffect!;

rule: "Perk/Player/SteelFormation/update"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.SteelFormation &&
    perkState  == PerkState.Using
){
    perk[Perk.perkTargets] = 
        FilteredArray(
            PlayersWithinRadius(
                ep, steelRange, Team.Team1, RadiusLOS.Off
            ), IsAlive(ArrayElement())
        );
    perk[Perk.perkTargets].isSteel = true;
    perk[Perk.perkEffectStatus] = CountOf(perk[Perk.perkTargets]);
    WaitUpdateMediumCycle();
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/SteelFormation/setSteel"
Event.OngoingPlayer
Team.Team1
if(
    isSteel
){
    if(
        0 < CountOf(
            FilteredArray(
                PlayersWithinRadius(
                    ep, steelRange, Team.Team1, RadiusLOS.Off
                ), 
                (
                    ae.perkType == PerkType.SteelFormation &&
                    ae.perkState == PerkState.Using
                )
            )
        )
    ){
        SetDamageReceived(ep, 50);
        if(steelEffect == 0)
            steelEffect = CreateEffect(AllPlayers(), Effect.Ring, Color.Orange, ep, 1.0, EffectRev.PositionAndRadius);
    }
    else{
        SetDamageReceived(ep, 100);
        DestroyEffect(steelEffect);
        steelEffect = 0;
        isSteel = false;
    }
    WaitUpdateMediumCycle();
    LoopIfConditionIsTrue();
}
