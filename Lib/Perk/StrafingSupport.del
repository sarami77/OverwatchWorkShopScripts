import "PerkCore.del";

define strafReloadTime: 10;
define strafReqDamage: 250;
define strafReqScatterRange: 5;
define strafReqDamageRange: 3;
define strafReqAttackTime: 2.0;
define strafReqAttackDistance: 50;
define strafReqAttackInterval: 0.03;

rule: "Perk/Player/StrafingSupport/activate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using
){
    // set time limit
    perk[Perk.perkTimeLimit] = 300;
    // set param
    perkPosition = 0;
    perkPosition2 = 0;
    landPosition = -1;
    perk[Perk.perkEffectStatus] = PerkEffectStatus.Ready;
    // set hud
    perk[Perk.perkEffectAndText] = [];
    CreatePerkEffectHud(
        perk[Perk.perkEffectStatus]  == PerkEffectStatus.Ready ?
            <"Hold melee button <0> to call Strafing Request.", ButtonGuide(Button.Melee)> :
            perk[Perk.perkEffectStatus]  == PerkEffectStatus.Reloading ?
                <"Strafing Request in preparation until <0> seconds.", perk[Perk.perkEffectCoolTime]> :
                "Strafing incoming!"
        );
    perk[Perk.perkEffectAndText] [0] = LastTextID();

    // first use
    StartHoldingButton(ep, Button.Melee);
    Wait(0.5, WaitBehavior.IgnoreCondition);
    StopHoldingButton(ep, Button.Melee);
}
rule: "Perk/Player/StrafingSupport/deactivate"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Non
){
    // reset hud
    DestroyHudText(perk[Perk.perkEffectAndText] [0]);
    DestroyInWorldText(perk[Perk.perkEffectAndText] [1]);
    // reset effect
    DestroyEffect(perk[Perk.perkEffectAndText] [2]);
    DestroyEffect(perk[Perk.perkEffectAndText] [3]);
    DestroyEffect(perk[Perk.perkEffectAndText] [4]);
    StopChasingVariable(landPosition);
    // reset type
    perkType = PerkType.Non;
}

rule: "Perk/Player/StrafingSupport/remindStrafingSupport"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.Ready
){
    // remind use perk.
    Wait(30, WaitBehavior.AbortWhenFalse);
    PlayEffect(
        ep, PlayEffect.DebuffImpactSound,
        Color.White, ep, MaxLength());
    SmallMessage(ep, <"Forgotten Strafing Support?"> );
    Wait(2, WaitBehavior.AbortWhenFalse);
    SmallMessage(ep, <"Hold melee button <0> to call Strafing Support.", ButtonGuide(Button.Melee)> );
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/StrafingSupport/startStrafing"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.Ready &&
    IsButtonHeld(ep, Button.Melee)
){
    WaitLongPressConfirm();
    // reset hud
    DestroyInWorldText(perk[Perk.perkEffectAndText] [1]);
    // reset effect
    DestroyEffect(perk[Perk.perkEffectAndText] [2]);
    DestroyEffect(perk[Perk.perkEffectAndText] [3]);
    DestroyEffect(perk[Perk.perkEffectAndText] [4]);
    StopChasingVariable(landPosition);
    perk[Perk.perkEffectStatus] = PerkEffectStatus.Triggered;
}

rule: "Perk/Player/StrafingSupport/playLandingZone"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.Triggered
){
    // play set sound
    PlayEffect(
        ep, PlayEffect.DebuffImpactSound,
        Color.White, ep, MaxValue());

    aimPosition = BelowPosition(AimingPosition());
    perk[Perk.approachingAngle] = DirectionTowards(
            aimPosition, ClosestPlayerTo(
                aimPosition,
                Team.Team2
            )
        );
    perk[Perk.approachingAngle] = VectorWithOutY(perk[Perk.approachingAngle]);
    // set start point
    perkPosition = 
        aimPosition + 
            // add random direction
            -perk[Perk.approachingAngle] *
            // add static distance
            (strafReqAttackDistance / 2);
    // set end point
    perkPosition2 = 
        aimPosition + 
            // add random direction
            perk[Perk.approachingAngle] *
            // add static distance
            (strafReqAttackDistance / 2);

    // landing zone text
    CreateInWorldText(
        AllPlayers(), <"<0> <1> <2>\n\rStrafing incoming!", ep, HeroIconString(HeroOf()), "Strafing Request">,
        aimPosition + PositionOffset(), 1, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString,
        Color.White, Spectators.DefaultVisibility);
    perk[Perk.perkEffectAndText] [1] = LastTextID();

    // set effect
    // landing zone blast range
    CreateEffect(AllPlayers(), Effect.Sphere, Color.Yellow,
        landPosition, strafReqScatterRange, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [2] = LastCreatedEntity();
    // landing zone position
    CreateEffect(AllPlayers(), Effect.Sphere, Color.Red,
        aimPosition, 0.2, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [3] = LastCreatedEntity(); 

    // play landing animation
    landPosition = perkPosition;

    UpdateEveryFrame(landPosition);
    ChaseVariableOverTime(
        landPosition, perkPosition2,
        strafReqAttackTime,
        TimeChaseReevaluation.DestinationAndDuration);

    Wait(strafReqAttackTime, WaitBehavior.AbortWhenFalse);
    StopChasingVariable(landPosition);
    DestroyEffect(perk[Perk.perkEffectAndText] [2]);
    // start attack
    perk[Perk.perkEffectStatus] = PerkEffectStatus.SecondTriggered;
}

rule: "Perk/Player/StrafingSupport/playLanding"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.SecondTriggered
){
    // create Ballistic
    CreateBeamEffect(AllPlayers(), BeamType.BadBeam,
        landPosition2, landPosition2 - (perk[Perk.approachingAngle] * 200) + (Up() * 100),
        Color.White, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [4] = LastCreatedEntity(); 

    // chase landing position
    landPosition = perkPosition;

    UpdateEveryFrame(landPosition);
    ChaseVariableOverTime(
        landPosition, perkPosition2,
        strafReqAttackTime,
        TimeChaseReevaluation.DestinationAndDuration);

    Wait(strafReqAttackTime, WaitBehavior.AbortWhenFalse);
    StopChasingVariable(landPosition);
    DestroyInWorldText(perk[Perk.perkEffectAndText] [1]);
    DestroyEffect(perk[Perk.perkEffectAndText] [3]);
    DestroyEffect(perk[Perk.perkEffectAndText] [4]);
    // start reload
    perk[Perk.perkEffectCoolTime] = strafReloadTime;
    perk[Perk.perkEffectStatus] = PerkEffectStatus.Reloading;
}

rule: "Perk/Player/StrafingSupport/landedToPoint"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.SecondTriggered
){
    // set landed point
    landPosition2 =
        landPosition + 
        RandomPosition(
            strafReqScatterRange / 1.5,
            0,
            strafReqScatterRange / 1.5
        );
    perk[Perk.perkTargets] =
        PlayersWithinRadius(
            landPosition2, strafReqDamageRange,
            Team.Team2, RadiusLOS.Off);
    // damage
    Damage(perk[Perk.perkTargets], ep, strafReqDamage);
    // play blast sound
    PlayEffect(
        AllPlayers(), PlayEffect.RingExplosionSound,
        Color.White,
        landPosition2,
        300);
    // play blast
    PlayEffect(
        AllPlayers(), PlayEffect.BadExplosion,
        Color.White,
        landPosition2,
        strafReqDamageRange);
        
    Wait(strafReqAttackInterval, WaitBehavior.AbortWhenFalse);
    LoopIfConditionIsTrue();
}

rule: "Perk/Player/StrafingSupport/reloadingStrafingSupport"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus] == PerkEffectStatus.Reloading
){
    WaitUpdateMediumCycle();
    // countdown
    perk[Perk.perkEffectCoolTime] -= 1;
    // if not zero
    if(perk[Perk.perkEffectCoolTime] > 0){
        LoopIfConditionIsTrue();
    }
    // if zero
    else{
        // play ready sound
        PlayEffect(
            ep, PlayEffect.BuffImpactSound,
            Color.White, ep, 100);
        // set to ready
        perk[Perk.perkEffectStatus] = PerkEffectStatus.Ready;
    }
}

rule: "Perk/Player/StrafingSupport/playAircraft"
Event.OngoingPlayer
Team.Team1
if(
    perkType == PerkType.StrafingSupport &&
    perkState  == PerkState.Using &&
    perk[Perk.perkEffectStatus]  == PerkEffectStatus.Reloading
){
    // set start point
    perkPosition =
        aimPosition +
        DirectionTowards(aimPosition, perkPosition) * 
        (strafReqAttackDistance * 25) +
        Vector(0, 200, 0); 
    // set end point
    perkPosition2 = 
        aimPosition +
        DirectionTowards(aimPosition, perkPosition2) *
        (strafReqAttackDistance * 25) +
        Vector(0, 1200, 0); 
    // air craft
    CreateEffect(AllPlayers(), Effect.Orb, Color.Red,
        landPosition, 1, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [2] = LastCreatedEntity();
    // air craft aura
    CreateEffect(AllPlayers(), Effect.GoodAura, Color.Red,
        landPosition, 30, EffectRev.VisibleToPositionAndRadius);
    perk[Perk.perkEffectAndText] [3] = LastCreatedEntity();

    // play air craft animation
    landPosition = perkPosition;
    
    if(!isServerOverLoading){
        UpdateEveryFrame(landPosition);
        ChaseVariableOverTime(
            landPosition, perkPosition2,
            3,
            TimeChaseReevaluation.DestinationAndDuration);
    }

    Wait(3, WaitBehavior.IgnoreCondition);
    StopChasingVariable(landPosition);
    DestroyEffect(perk[Perk.perkEffectAndText] [2]);
    DestroyEffect(perk[Perk.perkEffectAndText] [3]);
}
