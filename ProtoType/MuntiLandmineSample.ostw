playervar define landminePos = [];
playervar define landmineEffect = [];

define detectRange: 1.5;

rule: "showOperation"
Event.OngoingPlayer
if(
    HasSpawned()
){
    SmallMessage(EventPlayer(), "Crouch + Intaract -> set landmine");
}

rule: "setLandmine"
Event.OngoingPlayer
if(
    IsAlive() &&
    IsCrouching() &&
    IsButtonHeld(EventPlayer(), Button.Interact)
){
    define placePos = PositionOf();
    # add pos
    landminePos[CountOf(landminePos)] = placePos;
    # set effect to pos
    CreateEffect(AllPlayers(), Effect.Ring, Color.Red,
        landminePos[CountOf(landminePos) - 1], detectRange, EffectRev.None);
    # add id
    landmineEffect[CountOf(landmineEffect)] = LastCreatedEntity();
}

rule: "triggerLandmine"
Event.OngoingPlayer
if(
    IsAlive() &&
    !IsCrouching() &&
    IsTrueForAny(landminePos, detectRange >= DistanceBetween(ArrayElement(), EventPlayer()))
){
    # get closest landmine pos and idx
    define triggerlandminePos =
        FirstOf(
            SortedArray(
                landminePos,
                DistanceBetween(
                    ArrayElement(),
                    EventPlayer()
                )
            )
        );
    define triggerlandminePosIdx =
        IndexOfArrayValue(landminePos, triggerlandminePos);
    # play effect
    PlayEffect(
        AllPlayers(), PlayEffect.RingExplosionSound,
        Color.White, triggerlandminePos, 100);
    PlayEffect(
        AllPlayers(), PlayEffect.RingExplosion,
        Color.Red, triggerlandminePos, detectRange * 2);
    PlayEffect(
        AllPlayers(), PlayEffect.BadExplosion,
        Color.Red, triggerlandminePos, detectRange * 2);
    # give damage
    Damage(EventPlayer(), null, 10);    
    # hide effect from pos
    DestroyEffect(landmineEffect[triggerlandminePosIdx]);
    # clear value
    landmineEffect[triggerlandminePosIdx] = 0;
    landminePos[triggerlandminePosIdx] = Vector(0,-10000,0);
    # trigger multiple 
    LoopIf(
        IsTrueForAny(landminePos, detectRange >= DistanceBetween(ArrayElement(), EventPlayer()))
    );
}